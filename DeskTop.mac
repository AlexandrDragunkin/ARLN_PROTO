//-- Создание объекта "Угловая столешница"

defarr arr[8] arr1[5] arr2[5] arr3[5] arr4[5];
defarr barr1[4] barr2[4]  barr3[4]  barr4[4];

x=DbVar("x",1000);          //-- Ширина
Rt=DbVar("Rt",600);         //-- Ширина правой стороны
Lt=DbVar("Lt",600);         //-- Ширина левой стороны
z=DbVar("z",70);            //-- Высота ступеньки
Sd=DbVar("Sd",20);          //-- Выпуск ступеньки
Mat=DbVar("LongsMat",0);    //-- Материал
Type=DbVar("LongsType",0);  //-- Тип столешницы
//---------------------------------------------------
FurnType="030110";
Namescr="ProtoParams";

NULLOUT=InitArray(arr,0);
NULLOUT=InitArray(arr1,0);
NULLOUT=InitArray(arr2,0);
NULLOUT=InitArray(arr3,0);
NULLOUT=InitArray(arr4,0);
NULLOUT=InitArray(barr1,0);
NULLOUT=InitArray(barr2,0);
NULLOUT=InitArray(barr3,0);
NULLOUT=InitArray(barr4,0);
Dept=600;                   //-- Умолчание для габаритов профиля
H_Mat=28.7;                 //-- Умолчания на толщину материала
y=x+Lt-Rt;                  //-- Глубина

//NULLOUT=SetLongType(1);     //-- Устанавливаем тип длинномера - столешница
arr[1]=1;
NULLOUT=SetPan6Par(30,arr);
//NULLOUT=SetLongMat(Mat);    //-- Для текущего типа выставим новый материал
//DeskType=21
//DeskMat=317
arr[1]=Type;
arr[2]=Mat;
NULLOUT=SetPan6Par(2,arr);
//NULLOUT=GetLongCut(arr);
//NULLOUT=GetLongEnd(arr1,arr2,arr3,arr4);

LongPath=getLongFile(Dept,H_Mat);  //-- Запросим габариты профиля
i=1;
Loop1:
barr1[i]=0;
barr2[i]=0;
barr3[i]=0;
barr4[i]=0;
i=i+1;
if (i<5)
{
  goto Loop1;
}
//-- Рассчитаем подрезы и углы для 1-го длинномера (угол)
Ang=Atan(Lt/x);
S1=Sqrt(x*x+Lt*Lt);
Hh=S1*Cos(pi()/4-Ang);
Dt=Hh-Dept;

arr[1]=135/180*pi();
arr[2]=135/180*pi();
arr[3]=Dept-Dt;
arr[4]=0;
arr1[1]=0;
arr1[2]=0;
arr1[3]=0;
arr1[4]=0;
arr2[1]=0;
arr2[2]=0;
arr2[3]=0;
arr2[4]=0;

DtSd=Dt;
SDy=0.;     //-- Сдвиг по y для коррекции подреза сзади
if (z>H_Mat)
{
  arr[3]=0;
  arr[4]=Dept-Dt-H_Mat-Sd;
  SDy=-arr[4];
  DtSd=Dt+(Sd+H_Mat)
}
else
{
  z=-H_Mat;
}
arr3[1]=1;
arr3[2]=DtSd;
arr3[3]=DtSd;
arr3[4]=0;
arr4[1]=1;
arr4[2]=DtSd;
arr4[3]=DtSd;
arr4[4]=0;

NULLOUT=SetLongCut(arr);
NULLOUT=SetLongEnd(arr1,arr2,arr3,arr4);

//--Создадим длинномер
NULLOUT=SetLongBand(barr1,barr2,barr3,barr4); //-- Назначение кромки
longcreate DtSd SDy z+H_Mat -DtSd SDy z+H_Mat;

//-- Рассчитаем подрезы и углы для 2-го длинномера
arr[1]=135/180*pi();
arr[2]=135/180*pi();
arr[3]=0.;
arr[4]=0.;
F3=Lt*sin(pi()/4);
F4=Rt*sin(pi()/4);
F2=Dept-F3;
F1=Dept-F4;

arr1[1]=1;
arr1[2]=F3;
arr1[3]=F3;
arr2[1]=1;
arr2[2]=F4;
arr2[3]=F4;
arr3[1]=1;
arr3[2]=F1;
arr3[3]=F1;
arr4[1]=1;
arr4[2]=F2;
arr4[3]=F2;
NULLOUT=SetLongEnd(arr1,arr2,arr3,arr4);
NULLOUT=SetLongCut(arr);

//-- Создадим длинномер
NULLOUT=SetLongBand(barr1,barr2,barr3,barr4); //-- Назначение кромки
longcreate Dt+f2 Dt 0 -Dt-F1 Dt 0;

//-- Рассчитаем подрезы и углы для 3-го длинномера
if (z>H_Mat)       //-- Если высота больше толщины материала, формируем ступеньку
{
  arr[1]=pi()/2;
  arr[2]=pi()/2;
  arr[3]=Dept-z;
  arr[4]=0;
  arr1[1]=0;
  arr1[2]=0;
  arr1[3]=0;
  arr1[4]=0;
  arr2[1]=0;
  arr2[2]=0;
  arr2[3]=0;
  arr2[4]=0;
  arr3[1]=0;
  arr3[2]=Dt;
  arr3[3]=Dt;
  arr3[4]=0;
  arr4[1]=0;
  arr4[2]=Dt;
  arr4[3]=Dt;
  arr4[4]=0;

  NULLOUT=SetLongEnd(arr1,arr2,arr3,arr4);
  NULLOUT=SetLongCut(arr);

  NULLOUT=SetLongBand(barr1,barr2,barr3,barr4);     //-- Назначение кромки
  arr[1]=2; //Скос
  arr[2]=1; // Главный полилайн
  arr[3]=7  // сторона В
  arr[4]=4  // секция В
  arr[5]=0  // коэф.сдвига
  arr[6]=0  // сдвиг мм
  arr[7]=45  //угол
  NULLOUT=SetPan6Par(25,arr);
  arr[3]=3  // сторона С
  arr[4]=3  // секция С
  NULLOUT=SetPan6Par(25,arr);

  #DLnr longcreate Dt+H_Mat Dt H_Mat -Dt-H_Mat Dt H_Mat;
  rotate last 1 done 2points  Dt Dt H_Mat -Dt Dt H_Mat -90 nocopy ;
  move last 1 done 0 H_Mat 0 Nocopy
  rotate all done 2points 0 0 0 0 0 1 -45 nocopy ;
  Nobool:
}
else
{
  rotate all done 2points 0 0 0 0 0 1 -45 nocopy;
}
ScrMod=InitScratch();
err=AddScratch(ScrMod,FurnType,"x",x);
err=AddScratch(ScrMod,FurnType,"Rt",Rt);
err=AddScratch(ScrMod,FurnType,"z",z);
err=AddScratch(ScrMod,FurnType,"Lt",Lt);
err=AddScratch(ScrMod,FurnType,"Sd",Sd);
err=AddScratch(ScrMod,FurnType,"LongsType",Type);
err=AddScratch(ScrMod,FurnType,"LongsMat",Mat);
NULLOUT=writescratch(ScrMod,Namescr,0);
NULLOUT=TermScratch(ScrMod);
exit;

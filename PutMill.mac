//-- Установить фрезеровку по линии маркировки
//-- Установить форму, положение и имя фрезы и получить назад ее ID
//-- Входные параметры:
//-- Panel - объект "Панель"
//--         или 0 - если панель не надо инициализировать
//-- IDPoly - ID полилинии по которой фрезеруем (1 - это контур панели - главная полилиния)
//-- IDLine - ID элемента выреза
//-- IDMill - ID профиля в номенклатурном справочнике
//-- SdProc - Сдвиг фрезы вглубь панели в долях толщины
//-- SdMM - Сдвиг фрезы вглубь панели в мм
//-- TypeCut - Тип выреза (0 - сквозной вырез, но фрезеровка по А)
//-- Map - Номер секции раскрашивания 5-А, 6-F, 11-доп. А и пр.
//-- NameMill - Имя фрезеровки

global ProtoPath;
defarr arr[24];

getpar Panel, IDPoly, IDLine, IDMill, SdProc, SdMM, TypeCut, Map, NameMill;

//-------------------------------------------------------------
//-- Если не задан ID фрезеровки, фрезеровку не выполняем
if (idMill<=0)
{
  exit;
}
//-- Ищем имя файла с образующей выреза
Prof=PriceInfo(idMill,"K3File","");
FilePath=MPathExpand("<K3files>\\"+Prof);
//-- Параметры положения контура фрезы
sdx=0;      //-- Сдвиг по оси X
sdy=0;      //-- Сдвиг по оси Y
sdz=0;      //-- Сдвиг по оси Z
Angle=90;   //-- Угол поворота
Side="=";   //-- Знак-разделитель для имени фрезеровки
//-- Добавляем в сцену файл контура
#Kont append FilePath sdx sdy sdz;
if (TypeCut==0)
{
   mirror Kont done 3points 0 0 0 1 0 0 0 0 1 nocopy;
}
rotate Kont done 2points 0 0 0 0 0 1 Angle nocopy;
#Ply pline path Kont; //-- Создадим из контура полилинию

//-- Зачастую приходится добавлять по многу вырезов в панель. Если при этом
//-- перестраивать панель после каждого выреза, то получится очень долго
//-- Поэтому инициализируем панель в вызывающем макросе и там же ее перестраиваем.
//-- А вырезы добавляем здесь.

//-- Если панель задана, то ее инициализируем.
if (isvardef("Panel")==16)
{
  arr[1]=Panel;
  err=GetPan6Par(1,arr);
}
NULLOUT=InitArray(arr,0);
//-- Зададим имя контура образующей фрезеровки.
//-- Имя состоит из имени файла, сдвига в долях толщины, сдвига в мм, тип выреза, угол поворота и 0 в конце
//-- Например, "Профили\Фрезы\FR015.k3=0=5=1=90=0"
arr[1]=Prof+Side+str(SdProc)+Side+str(SdMM)+Side+str(TypeCut)+Side+str(Angle)+Side+"0";
arr[2]=Ply;                     //-- Укажем полилинию образующей
IDMillA=SetPan6Par(13,arr);      //-- Регистрируем полилинию и получаем его ID во внутреннем представлении панели
delete Kont Ply;

//-- Регистрируем фрезеровку
arr[1]=SdProc;         //-- Сдвиг в долях толщины
arr[2]=SdMM;           //-- Сдвиг в мм
arr[3]=0;              //-- Зарезерворованные значения
arr[4]=0;              //-- Зарезерворованные значения
arr[5]=0;              //-- Зарезерворованные значения
SetPan6Par(14,arr);    //-- Добавим положение фрезеровки на вырезе

NULLOUT=InitArray(arr,0);
//-- Зададим имя фрезеровки
//-- Имя фрезеровки состоит из входного параметра имени фрезеровки, сдвига в долях толщины, сдвига в мм, ID профиля фрезы
arr[1]=NameMill+Side+str(SdProc)+Side+str(SdMM)+Side+str(IDMill);
arr[2]=IDMillA;                //-- Зададим форму фрезы (ID профиля во внутреннем представлении панели )
arr[3]=Map;                   //-- Номер секции раскрашивания
idFreza=SetPan6Par(15,arr);   //-- Регистрируем фрезеровку и получаем ее ID

InitArray(arr,0);
arr[1]=IDPoly;               //-- ID выреза
arr[2]=IDLine;               //-- ID элемента выреза
arr[3]=IDFreza;              //-- ID полилинии фрезы
NULLOUT=SetPan6Par(16,arr);  //-- Добавим фрезеровку на вырез

//-- Фрезеруем панель, если она задана
if (isvardef("Panel")==16)
{
  MBPanel execute Panel;
}
exit;
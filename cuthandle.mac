global protopath;


getpar pan DoorBand;
//objident : pan;
//-pan- панель в которой надо сделать вырез

defarr xm[6];
// позиционируем систему координат
e=getsnap()
setucs lcs partly pan;
e=objgab3(pan,xm);
dz=xm[4]-xm[1];
dx=xm[5]-xm[2];
setucs move xm[4] xm[2]+dx/2 xm[3];
setucs rotate 2points 0 0 0 0 0 1 -90;

// создаем вырез
if dx>309 {
	#pl1 pline normal 0 0 1  -dx/2-10 -0.1 0 
							-154.774 -0.1 0
						arc -102.096 -15 0
							-49.418 -30 0
						line 49.418 -30 0
						arc 102.096 -15 0
							154.774 -0.1 0
						line dx/2+10 -0.1 0 
							dx/2+10 10 0 
							-dx/2-10 10 0 
							-dx/2-10 -0.1 0 ;
	//mbpanel incut incut pan pl1 done;
	gosub addCut;
	delete pl1;
						
								
}
else {
	if dx>283 {
		#pl1 pline normal 0 0 1  -dx/2-10 -0.1 0 
							-284/2 -0.1 0
						arc -101.2675 -8.571 0
							101.2675 -8.571 0
							284/2 -0.1 0
						line dx/2+10 -0.1 0 
							dx/2+10 10 0 
							-dx/2-10 10 0 
							-dx/2-10 -0.1 0 ;
		 //mbpanel incut incut pan pl1 done;
		 gosub addCut;
		 delete pl1;
		
		
	}
	else {
		Putmsg("Ширина фасада dx = "+str(dx)+" Должен быть = 284 мм",0)
		
	}
}
e=resnap()
exit;
//------------------------------------------
addCut:
	//--Считываем параметры
	defarr arr_p[20];
	NULLOUT=InitArray(arr_p,0);
	arr_p[1]=pan;
	err=GetPan6Par(1,arr_p);
	//==================================
	CutType=1;
	Dept=0;
		// параметры формы выреза
		InitArray(arr_p,0);
		arr_p[1]=1;          	// тип выреза: линия фрезеровки(0), вырез(1), нарост(8)
		arr_p[2]=1;                      // форма выреза
						//    1 свободный полилайн, плоскость которого должна быть параллельна плоскости панели
						//      при свободной привязке с нулевыми параметрами положения сохраняет свое исходное
						//      расположение (в проекции на плоскость панели)
						//      точка привязки совпадает с началом ЛСК панели
						//      Информация о "прототипности" полилайна ИГНОРИРУЕТСЯ
						//   -1 свободный полилайн, плоскость которого может быть произвольно расположена по отношению к плоскости панели
						//      при свободной привязке с нулевыми параметрами положения ЛСК полилайна совмещается с ЛСК панели
						//      точка привязки совпадает с началом ЛСК полилайна
						//      Информация о "прототипности" полилайна используется, что позволяет его в дальнейшем
						//			редактировать как прототип по его параметрам
						//	502 окружность (радиус)
						//      точка привязки в центре окружности
						//	600 дуга со скруглением (хорда, прогиб, радиус скругления)
						//      точка привязки в центре хорды
						//	601 прямоугольник со скруглением (ширина, высота, радиус скругления)
						//      точка привязки в центре нижнего основания
						//	602 фигурный вырез (длина, ширина, верхний радиус, нижний радиус)
						//      точка привязки в углу выреза
		
		arr_p[3]=pl1;                     // Полилайн

		SetPan6Par(7,arr_p);
		// параметры положения выреза
		InitArray(arr_p,0);
		arr_p[1]=9; //keys[ok_flag];	// привязка выреза к одному из торцев (1 - D,3 - C,5 - E,7 - B) или свободная привязка (9)
		arr_p[2]=0.5;	// если привязка к торцу, то сдвиг вдоль торца, иначе - абсолютная координата x в ЛСК панели
		arr_p[3]=0;       // если привязка к торцу, то сдвиг вглубь панели, иначе - абсолютная координата y в ЛСК панели
		arr_p[4]=0;  	// угол поворота выреза в градусах
		arr_p[5]=0;  	// глубина выреза: 0, если вырез сквозной; > 0 - не сквозной со стороны пласти A; < 0 - не сквозной со стороны пласти F
		SetPan6Par(8,arr_p);
		
		InitArray(arr_p,0);
		Map=9
		arr_p[1]=Map;                             // Секция раскрашивания
													// 1		// торец E (Y+)
													// 2		// торец D (Y-)
													// 3		// торец C (X+)
													// 4		// торец B (X-)
													// 5		// пласть верхняя (Z+)	
													// 6		// пласть нижняя (Z-)	
													// 7		// Угол 1
													// 8		// Угол 2
													// 9		// Угол 3
													// 10	        // Угол 4
													// 11	        // несквозной вырез на верхней пласти
													// 12	        // несквозной вырез на нижней пласти
														
	 ID_Incut=SetPan6Par(9,arr_p);            // Добавить вырез, параметры которого определены кодами 7 и 8
											// На выходе в ID_Incut идентификатор врезки
	 
	Typ_Kro=DoorBand;          //-- Тип лицевой кромки   Я применил тут выбор умолчания, но кромку можно назначить (выбрать) и любым другим способом

	 InitArray(arr_p,0);
	 arr_p[1]=ID_Incut;                       // Id врезки
	 arr_p[2]=-1;                             // Id элемента, если -1 - весь вырез
	 arr_p[3]=Typ_Kro;                       // Тип (материал) кромки
	 arr_p[4]=0;                       // Цвет (материал) кромки  (если надо, или 0)
	 arr_p[5]=1+8;                           // Свойства наложения кромки (см. ниже)
	 
	// 1- кромка строится на эквидистанте  (внутрь панели)
	// 2- кромка строится с предварительной фрезеровкой (это для кромок с пазом)
	// 4- кромка (фрезеровка) строится с переворотом по z (по вертикали)
	// 8- кромку можно резать              (торцевой обработкой и пазами)

	g_OutPar=SetPan6Par(10,arr_p);            // закромить вырез

//================================== Делаем панель
	mbpanel execute pan;
	objident last 1 pan;
	err=setPan6Par(999,arr_p);
	
	if dx>=300
	{
		attrobj attach "PrimM" done pan "Вырез под ручку Тип 1 по "+str(dx);
	}
	else
	{
		if dx>=600
		{
			attrobj attach "PrimM" done pan "Вырез под ручку Тип 2 по "+str(dx);
		}
	}
return;
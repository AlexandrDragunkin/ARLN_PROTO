//--  Изменение положения панели
global ProtoPath;
global iPanME;
iPanME=0;
selbyattr "Posit==10" child all done;
karkas_kol=sysvar(61);
karkas_num=GetCarcNumb();
err=renumerate();		//-- проставляем номера, чтобы иметь UnitPos панелей
if (karkas_kol==0)
{
 putmsg("Сначала надо создать мебельный каркас",0);
 exit;
}
if (karkas_kol==1)
{
 #karkas MBCarcase set yes;
}
if (karkas_kol>1&&karkas_num==0)
{
 #karkas MBCarcase set no yes :
}
iyf=getyadfixing();
//-------------------------------------------------------
NULLOUT=GetSnap();
Start:
//-- Выбираем разрезающую стойку
onerror End;
switch autosingle on;
selbyattr "Left(FurnType,3)==\"010\"&&FurnType!=\"010000\"&&KarkasNumb=="+str(karkas_num)+"&&FurnType!=\"010400\""  prompt "Укажите панель:" wholly  : ;
if (sysvar(61)==0)
{
  goto End;
}
Pan=getselnum(1);
NumPan=getattr(Pan,"UnitPos",0);
//-- Диалог запроса расстояния
#ok_flag
setvar
"Передвинуть панель"
""
left
""
done
real auto default 100 
"Величина сдвига" L 
done;
if (ok_flag==0)
{
  exit;
}
if (iyf==1)  //-- Если крепеж включен, выключаем его.
{
	NULLOUT=setyadfixing(0);
	holes delall all done;
	fixing delauto all done;
	fixing hide all done;
}
selbyattr "KarkasNumb=="+str(karkas_num)+"&&UnitPos=="+str(NumPan)+"" wholly all done;
if (sysvar(61)==0)
{
  goto End;
}
Pan=getselnum(1);
macro ProtoPath+"PanelMoveM.mac" 
Pan										//-- перемещаемая панель
L											//-- расстояние для перемещения
3											//-- область захвата со стороны А
3											//-- область захвата со стороны F
0											//-- отступ (для учета изменений толщины панели)
iif(iPanME>0,1,0);		//-- признак перезаписи объекта в массив
End:
if (iyf==1)  //-- Если крепеж выключали, включаем его.
{
	fixing create all done;
}
NULLOUT=ReSnap();
exit;

//-- Макропрограмма устанавливает столешницу-каплю.
defarr arr[3];
defarr basep[3];

NULLOUT=getvarinst(2,"ProtoPath",ProtoPath,getprotomac("Shkaf"));      //-- Путь к папке с макропрограммами
NULLOUT=getvarinst(2,"Typ_Kro",Typ_Kro,0);          //-- Тип лицевой кромки
NULLOUT=getvarinst(2,"FDMater",PrMater,0);          //-- Умолчание для материалов фигурных столешниц

NULLOUT=setvarinst(2,"MacroSw",1);                //-- Текущий способ построения

Thick=priceinfo(PrMater,"Thickness",16);
onerror final;
NULLOUT=getsnap();
NULLOUT=pushinst(1);
defarr arrin[3], arrout[8];
arrin[1]=Thick;  // Высота резиновой панели
mbget "Укажите положение панели" shell arrin arrout :
basep[1]=arrout[1];
basep[2]=arrout[2];
basep[3]=arrout[3];
xgab=arrout[4];
ygab=arrout[5];
Numb=arrout[6];
Shift=arrout[7];
//mbget "Укажите точку установки столешницы-капли" shell
//  basep[1] basep[2] basep[3] xgab ygab Numb Shift :

NULLOUT=sysarr(33,arr); //-- Запоминаем координаты точки
ptranscs(3,2,arr[1],arr[2],arr[3],arr[1],arr[2],arr[3]);

macro ProtoPath+"SetMat.mac" PrMater ;
macro ProtoPath+"SetBand.mac" Typ_Kro Typ_Kro Typ_Kro Typ_Kro ;
macro ProtoPath+"SetEnam.mac" "Столешница-капля";
macromode undo off;
radius=0.5*min(xgab,ygab);
Horda=radius;
sq=Sqrt(radius*radius-Horda/2*Horda/2);
basep[1]=iif(arr[1]<radius,radius,iif(arr[1]>xgab-radius,xgab-radius,arr[1]));
macro ProtoPath+"MakeDrop.mac" basep[1]-sq sq basep[3] radius, radius, 1, 90;
objident last 1 polka;
if (Numb>1)
{
  move multicopy Numb-1 polka done 0 0 Shift;
}
final:
offerror;
if (isvardef("polka")==16)
{
  macromode undo on "Drop_stol.mac" polka ;
}
else
{
  macromode undo on;
}
NULLOUT=resnap();
NULLOUT=popinst(1);
exit;

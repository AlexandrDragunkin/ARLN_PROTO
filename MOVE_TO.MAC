//-- Команда "Поместить в..."
defarr w1[6];
global ProtoPath;
getpar par;

object_2=sysvar(60);
z=0;
if object_2==0
{
  exit;
}
NULLOUT=GetSnap();
onerror finish;

if (par==1)
{
  objident prompt "Укажите перемещаемый объект" : pnt ;
	select stayblink pnt done;
}
if (par==2)
{
  macro Protopath+"DialFt.mac";
	n=0;
	n=sysvar(61);
	if (n!=0)
	{
	 pnt=getselnum(1);
	}
  else
  {
    cancel;
  }
	obj_1=sysvar(60);
	z=getobjhold(pnt,hgroup);
	if (z==1)
	{
 		extract partly hgroup pnt done;
	}
}
NULLOUT=objgab3(pnt,w1);

#ok_flag
setvar
"Поместить объект в заданную точку"
""
left
"Укажите желаемое положение объекта"
done
real     default w1[1]	       "Положение по ширине (X):"      	X_Copy
real     default w1[2]	       "Положение по глубине (Y):"     	Y_Copy
real     default w1[3]	       "Положение по высоте (Z):"      	Z_Copy
done;

if (ok_flag==0)
{
  goto finish;
}

move pnt done X_Copy-w1[1] Y_Copy-w1[2] Z_Copy-w1[3] nocopy

if (par==2)
{
  if (z==1)
  {
   add partly hgroup pnt done;
  }
  obj_2=sysvar(60);
  if (obj_2-obj_1>0&&z==1)
  {
    add partly hgroup last obj_2-obj_1 done;
  }
}
finish:
offerror;
select pnt done;
NULLOUT=ReSnap();
exit;
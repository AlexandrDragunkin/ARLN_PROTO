//-- Создание отчета "Информация о заказе"
global ProtoPath;

Project="Информация о заказе";  //-- Имя отчета
pref1="Reports\\";              //-- Имя папки внутри папки проекта для создания отчетов
if (udgetentity("ReportType",ValType,ReportType,sVal)==0) //-- Тип создаваемых отчетов
{
  ReportType=0;
}
ProjPath=getfilepath(sysvar(2));
ReportPath=mpathexpand("<Reports>\\");              //-- Метапуть к шаблонам отчетов
BaseName=ProjPath+getfiletitle(sysvar(2))-3+".mdb"; //-- Имя будущей базы
ProjPath=ProjPath+pref1;
Unitpos=0;

//-- Создаем стандартные изображения
stdviews fill "views" list;
if (isvardef("views")!=0) //-- Что-то мы все-таки выбрали
{
  szSrc="Provider=Microsoft.Jet.OLEDB.4.0;Data Source="+BaseName;
  Condcon=adbCon(szSrc);
  szSrc="DELETE FROM TDrawings";
  idRS=adbModify(Condcon,szSrc);
  i=0;
  loop:
  i=i+1;
  picp=views[i];
  namep=getfiletitle(picp)-4;
  picp=right(picp,len(picp)-len(getfilepath(sysvar(2))));
  szSrc="INSERT INTO TDrawings (UnitPos, DrawingName, DrawingDescr, SizeX, SizeY) ";
  szSrc=szSrc+"VALUES ("+str(UnitPos)+", \""+picp+"\", \""+namep+"\", 1500, 1000)";
  idRS=adbModify(Condcon,szSrc);
  idRS=adbClose(idRS);
  if (i<getdimarray(views))
  {
    goto loop;
  }
  Condcon=adbDisCon(Condcon);
}
if (ReportType==0)  //-- Отчет Excel
{
  putmsg("Создается отчет. Пожалуйста, подождите.",1);
  Params=ProtoPath+","+ProjPath+","+Project+","+BaseName;
  NULLOUT=fmdbscript(ReportPath+"Reports71.mdb","Информация о заказе","Start",Params);
}
else  //-- Отчет FastReport
{
  Report=ReportPath+"ClientInfo.frx";
  BaseParam1="ADODataBase1="+BaseName;
  BaseParam2="ADODataBase2="+ProtoPath+"RParams71.mdb";
  BaseParam=BaseParam1+","+BaseParam2;
  ReportParam="prmPathProject='"+getfilepath(BaseName)+"'";
  Project=Project+".fpx";
  NULLOUT=freport(Report,ProjPath+Project,BaseParam,ReportParam);
}
if (NULLOUT==0) //-- Если отчет не создан
{
  #ok_flag
  alternative "Ошибка создания отчета"
  msgbox picture 1 beep 1 text left
  "В процессе создания отчета произошла ошибка"
  "Отчет 'Информация о заказе' не создан!"
  done
  "  OK  "
  done;
}
else
{
  //-- Регистрируем отчет
	regreport(iif(ReportType==0,4,8),0,"Информация о заказе");
}
exit;
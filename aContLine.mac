 //-- Программа рисует линии контакта
 getpar
 w     //-- Ширина объекта
 dl    //-- Глубина объекта слева
 dr    //-- Глубина объекта справа
 h     //-- Высота объекта
 Ang   //-- ПРистенный угол (0 - если угол прямой)
 ТипКонт  //-- Тип приставки
					//-- 1 - прямой,
					//-- 2 - угловой
					//-- 3 - приставляем спереди по правому краю
					//-- 4 - приставляем спереди по левому краю
 Верхний; //-- Объект верха (1) или объект низа (0)

if (ТипКонт==0)
{
	exit;
}
d=max(dl,dr);
sy=sysvar(60);
cool=sysvar(40);
color 15;
if (ТипКонт>2) //-- Приставляем спереди
{
  #L1 Line 0 0 0 -50 0 0;
  rotate last 1 done 2points w,0,0 w,0,10 Ang nocopy;
  Attrobj Attach "Contact" Done L1 2+64*Верхний;  //-- К нему справа + высота навесных элементов
  #L1 Line w 0 0 w+50 0 0;
  Attrobj Attach "Contact" Done L1 5;             //-- К нему слева и его справа
  if (ТипКонт==3) //-- Приставляем спереди по правому краю
	{
    #L1 Line 0 d 0 0 d+50 0;
    Attrobj Attach "Contact" Done L1 10;        //-- К нему справа и его слева
  }
	else //-- Приставляем спереди по левому краю
	{
     #L1 Line w d 0 w d+50 0;
     Attrobj Attach "Contact" Done L1 1;        //-- К нему слева
  }
}
else
{
  #L1 Line w 0 0 w+50 0 0;
  rotate last 1 done 2points 0,0,0 0,0,10 -Ang nocopy;
  Attrobj Attach "Contact" Done L1 5;        //-- К нему слева и его справа
  if (ТипКонт==1) //-- Прямой  контакт
	{
    #L1 Line 0 0 0 0-50 0 0;
    Attrobj Attach "Contact" Done L1 10+64*Верхний;       //-- К нему слева и его справа + высота навесных
  }
  else //-- Угловой контакт
	{
    #L1 Line 0 d 0 0 d+50 0;
    Attrobj Attach "Contact" Done L1 10+64*Верхний;        //-- К нему слева и его справа + высота навесных
  }
}
color cool;
exit;
//-- Добавление комплекта (для версии 7.1 и выше)
global ProtoPath ;
global HoldName ; //-- Имя хозяина
global UnitCode ; //-- Код изделия (текст)
global ElemCode ; //-- Код мебельных элементов
KarkasNumb=getcarcnumb(); //-- Порядковый номер каркаса в сцене

getpar
IdAcces         //-- Тип комплекта
FurnType				//-- Furntype
NAcces	        //-- 1 - правая - 2 - левая   Тип ребенка
IsNew           //-- Пара для комплектующего (0 - новый комплект, 1 - пара для предыдущему)
xn yn zn	      //-- Положение начала локальной системы координат
ax az ax1;	    //-- Углы поворота
if (IdAcces==0)
{
  exit;
}

NULLOUT=getsnap();
NULLOUT=pushinst(1);
object_1=sysvar(60); //-- Кол-во объектов

if (IsNew==1)
{
  accessory FurnType IdAcces  NAcces yes ;
}
else
{
  accessory FurnType IdAcces  NAcces no ;
}
objident last 1 Elem;
move nocopy Elem done xn yn zn ;
rotate Elem done 2points xn yn zn xn+1 yn zn ax nocopy ;
rotate Elem done 2points xn yn zn xn yn zn+1 az nocopy ;
rotate Elem done 2points xn yn zn xn+1 yn zn ax1 nocopy ;

NULLOUT=popinst(1);
NULLOUT=resnap();
object_2=sysvar(60)-object_1; //-- Кол-во объектов
if (object_2==0) //-- Нет объектов
{
  exit;
}
//-- Присваиваем объекту атрибут с порядковым номером каркаса
if (isassign("KarkasNumb",0))
{
  attrobj copy record "KarkasNumb" done Elem done;
}
else
{
  attrobj attach "KarkasNumb" done Elem KarkasNumb;
}
if (isattrdef("HoldName"))
{
  attrobj attach "HoldName" done Elem HoldName ;
}
if len(ElemCode)>0
{
	attrobj attach "UnitCode" done Elem ElemCode ;
}

attrobj attach "XUnit" "YUnit" "ZUnit" done Elem 0 0 0 ; //-- Размеры детали

ChildID=IdAcces;
macro ProtoPath+"CompAttr.mac" ChildID 1 ;
NULLOUT=addattrpi(Elem,1,135,IdAcces);
exit;

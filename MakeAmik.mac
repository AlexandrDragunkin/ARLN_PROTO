//-- Создание столешницы Amik

defarr UsRU[4]; //-- Углы столешницы
defarr xm[6];

NULLOUT=getvarinst(2,"ProtoPath",ProtoPath,getprotomac("Shkaf"));      //-- Путь к папке с макропрограммами
NULLOUT=GetVarInst(2,"MacroSw",MacroSw,0); //-- Способ построения

getpar
x y z		                //-- Правый нижний дальный угол столешницы
Length, WidthL, WidthR; //-- Длина, ширина левой стороны, ширина правой стороны столешницы Amik

//---------------------------------------------------------------
position=143;  //-- Номер прототипа столешницы Amik
Namescr="ProtoParams";
FurnType="011000";
NULLOUT=pushinst(1);
NULLOUT=getsnap();
NULLOUT=getvarinst(1,"ElemName",ElemName," ");    //-- Имя элемента
NULLOUT=getvarinst(1,"PanMater",PanMater,0);      //-- Материал
NULLOUT=getvarinst(1,"g_BandPan",BandPan,0,1);    //-- Тип кромки
NULLOUT=getvarinst(1,"g_CBandPan",PanCKro,0,1);   //-- Цвет кромки
NULLOUT=getarrinst(1,"AmikRU",UsRU);              //-- Углы столешницы Amik
NULLOUT=getvarinst(1,"AmikAngl1",Angl1,30);       //-- Угол скоса слева
NULLOUT=getvarinst(1,"AmikAngl2",Angl2,30);       //-- Угол скоса справа
NULLOUT=getvarinst(1,"BendPan",hh,0,3);           //-- Прогиб сзади
NULLOUT=getvarinst(1,"BendPan",hs,0,4);           //-- Прогиб спереди
NULLOUT=getvarinst(1,"PanDir",PanDir,0);          //-- Направление текстуры
PanDir=iif(PanDir==0,1,iif(PanDir==90,2,0));

NULLOUT=setvarinst(1,"FurnType",FurnType);        //-- FurnType
if (ElemName==""||ElemName==" ")
{
  ElemName="Столешница Amik";
}
D=priceinfo(PanMater,"Thickness",16); //-- Толщина столешницы Amik
//---------------------------------------------------------------------
if (MacroSw==0)
{
  gosub SimpleStol;
}
else
{
  protoobj create "Shkaf.ptl" 143
    "L"       Length
    "Bl"      WidthL
    "Br"      WidthR
    "Al"      Angl1
    "Ar"      Angl2
    "hh"      hh
    "hs"      hs
    "rc1"     UsRU[1]
    "rc2"     UsRU[2]
    "rc3"     UsRU[3]
    "rc4"     UsRU[4]
    "PrMater" PanMater
    "Nshp"    PanDir
    "Band"    BandPan
    "Face_col" PanCKro
    done
    x y z ;
  NULLOUT=setvarinst(2,"MacroSw",MacroSw);          //-- Текущий способ построения
  objident last 1 Pan;
}
NULLOUT=resnap();
NULLOUT=popinst(1);
exit;
//--------------------------------------------------------------
SimpleStol:
macro ProtoPath+"SetEnam.mac" "";
object_1=sysvar(60);
macro ProtoPath+"StAmikForm.mac" Length,WidthL,WidthR,Angl1,Angl2,hh,hs,
  UsRU[1],UsRU[2],UsRU[3],UsRU[4],PanMater,PanDir,BandPan,PanCKro;
object_2=sysvar(60)-object_1;
move last object_2 done x y z nocopy;
setucs move x y z;
#stl group last object_2 done;
NULLOUT=objgab3(stl,xm);
ScrMod=InitScratch();
err=AddScratch(ScrMod,FurnType,"L",Length);
err=AddScratch(ScrMod,FurnType,"Bl",WidthL);
err=AddScratch(ScrMod,FurnType,"Br",WidthR);
err=AddScratch(ScrMod,FurnType,"Al",Angl1);
err=AddScratch(ScrMod,FurnType,"Ar",Angl2);
err=AddScratch(ScrMod,FurnType,"hh",hh);
err=AddScratch(ScrMod,FurnType,"hs",hs);
err=AddScratch(ScrMod,FurnType,"rc1",UsRU[1]);
err=AddScratch(ScrMod,FurnType,"rc2",UsRU[2]);
err=AddScratch(ScrMod,FurnType,"rc3",UsRU[3]);
err=AddScratch(ScrMod,FurnType,"rc4",UsRU[4]);
err=AddScratch(ScrMod,FurnType,"PrMater",PanMater);
err=AddScratch(ScrMod,FurnType,"Nshp",PanDir);
err=AddScratch(ScrMod,FurnType,"Face_col",PanCKro);
err=AddScratch(ScrMod,FurnType,"Band",BandPan);
NULLOUT=writescratch(ScrMod,Namescr,stl);
NULLOUT=TermScratch(ScrMod);
NULLOUT=addattrpi(stl,1,position);
macro ProtoPath+"SetEnam.mac" ElemName;
//-- Присваиваем атрибуты
macro ProtoPath+"EndPanel.mac" stl position xm[4]-xm[1] xm[5]-xm[2] D PanMater "";
return;

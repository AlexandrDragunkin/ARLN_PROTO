//-- Создание отчета "Спецификация на изделия"
global ProtoPath;
defarr obj1[1]; //-- Для экпорта
//-- Направление вида в ВСК
defarr vi[3];
vi[1]=0;
vi[2]=0;
vi[3]=1;
Project="Спецификация на изделия";  //-- Имя отчета
pref1="Reports\\";                  //-- Имя папки внутри папки проекта для создания отчетов
if (udgetentity("ReportType",ValType,ReportType,sVal)==0) //-- Тип создаваемых отчетов
{
  ReportType=0;
}
ProjPath=getfilepath(sysvar(2));
ReportPath=mpathexpand("<Reports>\\");              //-- Метапуть к шаблонам отчетов
BaseName=ProjPath+getfiletitle(sysvar(2))-3+".mdb"; //-- Имя будущей базы
ProjPath=ProjPath+pref1;
szSrc="Provider=Microsoft.Jet.OLEDB.4.0;Data Source="+BaseName;
Condcon=adbCon(szSrc);

selbyattr "left(FurnType,1)==\"1\"" wholly all done ;
object_2=sysvar(61);
defarr obj[object_2];
macro ProtoPath+"Arrobj.mac" object_2, obj;
view save "view";
szSrc="DELETE FROM TDrawings";
idRS=adbModify(Condcon,szSrc);
i=0;
loop:
i=i+1;
obj1[1]=obj[i];
UnitPos=getattr(obj[i],"UnitPos",0);
fnam=ProjPath+"WMF_Sketches\\"+Project+"_"+str(UnitPos)+".wmf";
setucs lcs obj[i];
view user cartesian none ucs 0.33, 0.87, 0.38 done;
zoom byobject obj[i] done;
vi[1]=0;
vi[2]=0;
vi[3]=1;
vtranscs(1,3,vi[1],vi[2],vi[3],vi[1],vi[2],vi[3]);
NULLOUT=exportvi(obj1,1,vi,fnam,1,4,1,1);
zoom previous;
szSrc="INSERT INTO TDrawings (UnitPos, DrawingName, SizeX, SizeY) ";
szSrc=szSrc+"VALUES ("+str(UnitPos)+", \""+pref1+"WMF_Sketches\\"+Project+"_"+str(UnitPos)+".wmf\", 1000, 1000)";
idRS=adbModify(Condcon,szSrc);

if (i<object_2)
{
  goto loop;
}
view restore "view";
idRS=adbClose(idRS);
Condcon=adbDisCon(Condcon);
if (ReportType==0)  //-- Отчет Excel
{
  putmsg("Создается отчет. Пожалуйста, подождите.",1);
  Params=ProtoPath+","+ProjPath+","+Project+","+BaseName;
  NULLOUT=fmdbscript(ReportPath+"Reports71.mdb","Спецификация на изделия","Start",Params);
}
else  //-- Отчет FastReport
{
  Report=ReportPath+"FactoryInfo.frx";
  BaseParam1="ADODataBase1="+BaseName;
  BaseParam2="ADODataBase2="+ProtoPath+"RParams71.mdb";
  BaseParam=BaseParam1+","+BaseParam2;
  ReportParam="prmPathProject='"+getfilepath(BaseName)+"'";
  Project=Project+".fpx";
  NULLOUT=freport(Report,ProjPath+Project,BaseParam,ReportParam);
}
if (NULLOUT==0) //-- Если отчет не создан
{
  #ok_flag
  alternative "Ошибка создания отчета"
  msgbox picture 1 beep 1 text left
  "В процессе создания отчета произошла ошибка"
  "Отчет 'Спецификация на изделия' не создан!"
  done
  "  OK  "
  done;
}
else
{
  //-- Регистрируем отчет
	regreport(iif(ReportType==0,3,7),0,"Спецификация на изделия");
}
exit;
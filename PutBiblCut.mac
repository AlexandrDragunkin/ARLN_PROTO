//-- Макропрограмма устанавливает вырез в панели

global ProtoPath;

defarr arr[20] keys[5];

keys[2]=7
keys[3]=3
keys[4]=1
keys[5]=5

NULLOUT=getsnap();
//Objident : Panel;


NewPan:
#Panel mbget "Укажите торец панели, на которую нужно добавить вырез"    Panel idpoly idline band :
arr[1]=Panel;
err=GetPan6Par(1,arr);

if (idline>7) {
    goto NewPan;
}
macro ProtoPath+"AddCuts.mac" ;

Objident last 1 PPP;       // Назначим полилайну имя

//#ok_flag
//alternative "Установка выреза"
//msgbox picture 2 beep 2 text "Выберите торец установки:" done
//"Отмена"
//  "На торец B"
//  "На торец C"
//  "На торец D"
//  "На торец E"	
//done;
//if (ok_flag==1)
//{
//	exit;
//}

CutType=1;
Dept=0;
	// параметры формы выреза
	InitArray(arr,0);
	arr[1]=CutType;          	// тип выреза: линия фрезеровки(0), вырез(1), нарост(8)
	arr[2]=-1;                      // форма выреза
					//    1 свободный полилайн, плоскость которого должна быть параллельна плоскости панели
					//      при свободной привязке с нулевыми параметрами положения сохраняет свое исходное
					//      расположение (в проекции на плоскость панели)
					//      точка привязки совпадает с началом ЛСК панели
					//      Информация о "прототипности" полилайна ИГНОРИРУЕТСЯ
					//   -1 свободный полилайн, плоскость которого может быть произвольно расположена по отношению к плоскости панели
					//      при свободной привязке с нулевыми параметрами положения ЛСК полилайна совмещается с ЛСК панели
					//      точка привязки совпадает с началом ЛСК полилайна
					//      Информация о "прототипности" полилайна используется, что позволяет его в дальнейшем
					//			редактировать как прототип по его параметрам
					//	502 окружность (радиус)
					//      точка привязки в центре окружности
					//	600 дуга со скруглением (хорда, прогиб, радиус скругления)
					//      точка привязки в центре хорды
					//	601 прямоугольник со скруглением (ширина, высота, радиус скругления)
					//      точка привязки в центре нижнего основания
					//	602 фигурный вырез (длина, ширина, верхний радиус, нижний радиус)
					//      точка привязки в углу выреза
	
	arr[3]=PPP;                     // Полилайн

	SetPan6Par(7,arr);
	// параметры положения выреза
	InitArray(arr,0);
	arr[1]=idline; //keys[ok_flag];	// привязка выреза к одному из торцев (1 - D,3 - C,5 - E,7 - B) или свободная привязка (9)
	arr[2]=0;	// если привязка к торцу, то сдвиг вдоль торца, иначе - абсолютная координата x в ЛСК панели
	arr[3]=0;       // если привязка к торцу, то сдвиг вглубь панели, иначе - абсолютная координата y в ЛСК панели
	arr[4]=180;  	// угол поворота выреза в градусах
	arr[5]=Dept;  	// глубина выреза: 0, если вырез сквозной; > 0 - не сквозной со стороны пласти A; < 0 - не сквозной со стороны пласти F
	SetPan6Par(8,arr);
	
	InitArray(arr,0);
	Map=9
	arr[1]=Map;                             // Секция раскрашивания
                                                // 1		// торец E (Y+)
                                                // 2		// торец D (Y-)
                                                // 3		// торец C (X+)
                                                // 4		// торец B (X-)
                                                // 5		// пласть верхняя (Z+)	
                                                // 6		// пласть нижняя (Z-)	
                                                // 7		// Угол 1
                                                // 8		// Угол 2
                                                // 9		// Угол 3
                                                // 10	        // Угол 4
                                                // 11	        // несквозной вырез на верхней пласти
                                                // 12	        // несквозной вырез на нижней пласти
                                                	
 ID_Incut=SetPan6Par(9,arr);            // Добавить вырез, параметры которого определены кодами 7 и 8
                                        // На выходе в ID_Incut идентификатор врезки
 
NULLOUT=getvarinst(2,"Typ_Kro",Typ_Kro,0);          //-- Тип лицевой кромки   Я применил тут выбор умолчания, но кромку можно назначить (выбрать) и любым другим способом

 InitArray(arr,0);
 arr[1]=ID_Incut;                       // Id врезки
 arr[2]=-1;                             // Id элемента, если -1 - весь вырез
 arr[3]=Typ_Kro;                       // Тип (материал) кромки
 arr[4]=0;                       // Цвет (материал) кромки  (если надо, или 0)
 arr[5]=1+8;                           // Свойства наложения кромки (см. ниже)
 
// 1- кромка строится на эквидистанте  (внутрь панели)
// 2- кромка строится с предварительной фрезеровкой (это для кромок с пазом)
// 4- кромка (фрезеровка) строится с переворотом по z (по вертикали)
// 8- кромку можно резать              (торцевой обработкой и пазами)

g_OutPar=SetPan6Par(10,arr);            // закромить вырез

//================================== Делаем панель
MBPanel execute  Panel ;
delete PPP;	
NULLOUT=resnap();
exit;

//-- Макропрограмма устанавливает овальную столешницу

defarr basep[3];

NULLOUT=getvarinst(2,"ProtoPath",ProtoPath,getprotomac("Shkaf"));      //-- Путь к папке с макропрограммами 
NULLOUT=getvarinst(2,"Typ_Kro",Typ_Kro,0);          //-- Тип лицевой кромки 
NULLOUT=getvarinst(2,"FDMater",PrMater,0);          //-- Умолчание для материалов фигурных столешниц 
NULLOUT=getvarinst(2,"OvalRadius",OvalRadius,200);  //-- Радиус овальной столешницы

NULLOUT=setvarinst(2,"MacroSw",1);                //-- Текущий способ построения

Thick=priceinfo(PrMater,"Thickness",16);
onerror final;
NULLOUT=getsnap();
NULLOUT=pushinst(1);
defarr arrin[3], arrout[8];
arrin[1]=Thick;  // Высота резиновой панели
mbget "Укажите положение панели" shell arrin arrout :
basep[1]=arrout[1];
basep[2]=arrout[2];
basep[3]=arrout[3];
xgab=arrout[4];
ygab=arrout[5];
Numb=arrout[6];
Shift=arrout[7];
//mbget "Укажите точку установки овальной столешницы" shell
//  basep[1] basep[2] basep[3] xgab ygab Numb Shift :

#ok_flag
setvar
"Овальная столешница"
ProtoPath+"Oval_SH.wmf"
left
"Введите значения параметров"
done
real default OvalRadius "Радиус:" OvalRadius
done;
if (ok_flag==0)
{
  goto final;
}
macro ProtoPath+"SetMat.mac" PrMater ;
macro ProtoPath+"SetKrom.mac" 0 Typ_Kro Typ_Kro Typ_Kro Typ_Kro ;
macro ProtoPath+"SetTextureAngle.mac" 0;
macro ProtoPath+"SetEnam.mac" "Овальная столешница";
macromode undo off;
macro ProtoPath+"MakeOval.mac" basep[1]+xgab/2 basep[2]+ygab/2 basep[3] xgab, ygab, OvalRadius;

objident last 1 polka;
if (Numb>1)
{
  move multicopy Numb-1 polka done 0 0 shift;
}
final:
offerror;
if (isvardef("polka")==16)
{
  macromode undo on "Oval_stol.mac" polka ;
}
else
{
  macromode undo on;
}
NULLOUT=setvarinst(2,"OvalRadius",OvalRadius);   //-- Радиус овальной столешницы
NULLOUT=resnap();
NULLOUT=popinst(1);
exit;

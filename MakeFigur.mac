//-- Создание фигурной столешницы

NULLOUT=getvarinst(2,"ProtoPath",ProtoPath,getprotomac("Shkaf"));      //-- Путь к папке с макропрограммами 
NULLOUT=GetVarInst(2,"MacroSw",MacroSw,0); //-- Способ построения 
getpar
x y z		       //-- Правый нижний дальный угол столешницы
Length, Width; //-- Длина, ширина фигурной столешницы

//---------------------------------------------------------------
position=118;  //-- Номер прототипа фигурной столешницы
Namescr="ProtoParams";
FurnType="011000";
NULLOUT=pushinst(1);
NULLOUT=getsnap();
NULLOUT=getvarinst(1,"ElemName",ElemName," ");    //-- Имя элемента 
NULLOUT=getvarinst(1,"PanMater",PanMater,0);      //-- Материал 
NULLOUT=getvarinst(1,"g_BandPan",BandPan,0,1);    //-- Тип кромки 
NULLOUT=getvarinst(1,"g_CBandPan",PanCKro,0,1);   //-- Цвет кромки 
NULLOUT=getvarinst(1,"FRad1",FRad1,350);          //-- Радиус 1
NULLOUT=getvarinst(1,"FRad2",FRad2,300);          //-- Радиус 1
NULLOUT=getvarinst(1,"FLeftFlag",FLeftFlag,0);    //-- Левая ориеннтация
NULLOUT=getvarinst(1,"FAngle",FAngle,45);         //-- Угол стыковки радиусов

NULLOUT=setvarinst(1,"FurnType",FurnType);    //-- FurnType
if (ElemName==""||ElemName==" ")
{
  ElemName="Фигурная столешница";
}
D=priceinfo(PanMater,"Thickness",16); //-- Толщина фигурной столешницы
//---------------------------------------------------------------------
if (MacroSw==0)
{
  gosub SimpleStol;
}
else
{
  protoobj create "Shkaf.ptl" 118
    "Rad1" FRad1
    "Rad2" FRad2
    "Length" Length
    "Width" Width
    "LeftFlag" FLeftFlag
    "Angle" FAngle
    "PrMater" PanMater
    "Band" BandPan
    "Face_col" PanCKro
    done
    x y z ;
  NULLOUT=setvarinst(2,"MacroSw",MacroSw);          //-- Текущий способ построения
  objident last 1 Pan;
}
NULLOUT=resnap();
NULLOUT=popinst(1);
exit;
//--------------------------------------------------------------
SimpleStol:
macro ProtoPath+"SetEnam.mac" "";
object_1=sysvar(60);
macro ProtoPath+"Tbl2RadForm.mac" PanMater, FRad1, FRad2, Length, Width, FLeftFlag, FAngle, BandPan, PanCKro;
object_2=sysvar(60)-object_1;
move last object_2 done x y z nocopy;
setucs move x y z;
#stl group last object_2 done;
ScrMod=InitScratch();
err=AddScratch(ScrMod,FurnType,"Rad1",FRad1);
err=AddScratch(ScrMod,FurnType,"Rad2",FRad2);
err=AddScratch(ScrMod,FurnType,"Length",Length);
err=AddScratch(ScrMod,FurnType,"Width",Width);
err=AddScratch(ScrMod,FurnType,"LeftFlag",FLeftFlag);
err=AddScratch(ScrMod,FurnType,"Angle",FAngle);
err=AddScratch(ScrMod,FurnType,"Band",BandPan);
err=AddScratch(ScrMod,FurnType,"PrMater",PanMater);
err=AddScratch(ScrMod,FurnType,"Face_col",PanCKro);
NULLOUT=writescratch(ScrMod,Namescr,stl);
NULLOUT=TermScratch(ScrMod);
NULLOUT=addattrpi(stl,1,position);
macro ProtoPath+"SetEnam.mac" ElemName;
//-- Присваиваем атрибуты
macro ProtoPath+"EndPanel.mac" stl position Length Width D PanMater "";
return;

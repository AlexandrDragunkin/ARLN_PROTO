//-- Стол двухсекционный с горизонтальными ящиками
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов
defarr x[4],y[4],rad[3];

PrMater=DbVar("КорпМат",0);          //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);            //-- Материал задней стенки
w=DbVar("Шир",330);	                 //-- Ширина стола
d=DbVar("Глуб",320);			           //-- Глубина стола
h=DbVar("Выс",822);			             //-- Высота стола
h_c=DbVar("ВысЦок",100);		         //-- Высота цоколя
ШирСтяж=DbVar("ШирСтяж",100);		     //-- Ширина планок
ЦвКром=DbVar("ЦвКромки",0);   	     //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   	   //-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	       //-- Тип крепежа корпуса
КрепЗС=DbVar("КрепЗС",0);   	       //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);        //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);        //-- Глубина установки задней стенки
ЕстьКрышка=DbVar("Крышка",0);        //-- Наличие крышки
ЗаглПол=DbVar("ЗаглПол",0);          //-- Заглубление полок
ДноМат=DbVar("ДноМат",0);            //-- Материал дна ящика
ТипЯщ=DbVar("ТипЯщ",0);              //-- Тип ящика
ФасВсМат=DbVar("ФасВсМат",0);        //-- Материал вставки в фасад
СторОткр=DbVar("СторОткр",0);        //-- Открывание двери
ТипРуч=DbVar("ТипРуч",0);            //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);        //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);            //-- Поворот ручки
ДвеДвери=DbVar("ДвеДвери",0);        //-- Двойная дверь
ОткрДверь=DbVar("ОткрДверь",0);      //-- Открытая дверь или выдвинутые ящики
Петля=DbVar("Петля",0);              //-- Тип петли
h_niz=DbVar("ВысНиж",822);           //-- Высота нижней секции
Noga=DbVar("Ножка",0);		           //-- Тип ножки
Dno=DbVar("Dno",DbVar("g_Dno",0));   //-- Тип дна
//-- Нижняя секция
Заполн=DbVar("Заполн",0);            //-- Тип заполнения нижней секции
КолПЯ=DbVar("КолПЯ",0);              //-- Количество полок или ящиков нижней секции
РисФас=DbVar("РисФас",0);            //-- Рисунок фасада нижней секции
ФасМат=DbVar("ФасМат",0);            //-- Материал фасада нижней сецкии
Rpl_r=dbvar("Rpl_r",0);			         //-- Расположение ручки нижней секции
//-- Для верхней секции
КолПЯ2=DbVar("КолПЯ2",0);            //-- Количество ящиков верхней секции
РисФас2=DbVar("РисФас2",0);          //-- Рисунок фасада верхней секции
ФасМатВ=DbVar("ФасМатВ",0);          //-- Материал фасада верхней сецкии
СдвРуч=DbVar("СдвРуч",40);           //-- Сдвиг ручки
Rt=dbvar("Rt",1);			               //-- Расположение ручки верхней секции
HasDuct=DbVar("HasDuct",0);          //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0);      //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0);      //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0);      //-- Глубина короба
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4); //-- Толщина материала задней стенки
Накладн=1;                      //-- Фасад накладной
ЗСтВрезка=8;                    //-- Врезка задней стенки
dum=120;                        //-- Размер "уголка" задней стенки под мойку
offset=14;                      //-- Нахлест накладной стенки на корпус
dst=60;                         //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                       //-- Наличие цоколя
Shiftcok=0;                     //-- Сдвиг цоколя
LongPost=0;                     //-- Опорные стойки
FasCok=0;                       //-- Размер фасок под цоколь
FixPlan=0;	                    //-- Наличие крепежной планки
ConfDuct=0;                     //-- Конфигурация выреза под короб по умолчанию
Shift=50;                       //-- Сдвиг опор от края корпуса
//---------------------------------------------------
//-- Константы
D_DxN=DbVar("D_DxN",1.5);	//-- Зазор по ширине для накладных дверей
D_DzN=DbVar("D_DzN",2);		//-- Зазор по высоте для накладных дверей
D_DxD=DbVar("D_DxD",2);		//-- Зазор между двойными дверьми
//---------------------------------------------------
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  d=d-DepthDuct;
  HasDuct=0;
}
//---------------------------------------------------
NULLOUT=getsnap();
//--Построение ножек
x[1]=w;
y[1]=0;
rad[1]=0;
x[2]=w;
y[2]=d;
rad[2]=0;
x[3]=0;
y[3]=d;
rad[3]=0;
x[4]=0;
y[4]=0;
if (ConfDuct==3)
{
  y[1]=DepthDuct;
}
if (ConfDuct==2)
{
  y[4]=DepthDuct;
}
if (ConfDuct==4)
{
  if (2*Shift>ShiftDuct)
  {
    y[4]=DepthDuct;
  }
  if (w-2*Shift<ShiftDuct+WidthDuct)
  {
    y[1]=DepthDuct;
  }
}
macro ProtoPath+"aStBase.mac" Noga,4,x,y,rad,Shift;
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
//-------------------------------------------------
macro ProtoPath+"MakeCorps.mac" 0 0 0
    w           //-- Ширина
    d           //-- Глубина
    h           //-- Высота
    h_c         //-- Высота цоколя
    PrMater     //-- Материал корпуса
    ЗСтМат      //-- Материал задней стенки
    ШирСтяж     //-- Ширина стяжки
    ТипЗСтен    //-- Тип задней стенки
    УстЗСтен    //-- Глубина установки задней стенки
    offset      //-- Врезка\Нахлест задней стенки
    ЕстьКрышка  //-- Наличие крышки
    0           //-- Наличие сушилки
    ЦвКром      //-- Цвет кромки
    ТипКром     //-- Тип кромки
    Крепеж      //-- Крепеж корпуса
    КрепЗС   	  //-- Тип крепежа задней стенки
    Dno         //-- Накладное дно
    Dum         //-- Размер "уголка" задней стенки под мойку
    Dst         //-- Ширина дополнительной планки
    HasCok      //-- Наличие цоколя
    Shiftcok    //-- Сдвиг цоколя
    LongPost    //-- Опорные стойки
    FasCok      //-- Размер фасок под цоколь
    FixPlan     //-- Наличие крепежной планки
;
//-------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0) { ОтстСз=0; }		    //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		    //-- Накладной для мойки
if (ТипЗСтен==0) { ОтстСз=h_dvp; }	  //-- Утопленный
if (ТипЗСтен==2) { ОтстСз=0; }		    //-- Нет
if (ТипЗСтен==3) { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	  //-- Жесткая планка
//---------------------------------------------------------------------
//-- Полка между секциями
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
macro ProtoPath+"SetMat.mac" PrMater;
macro ProtoPath+"SetECod.mac" "1107";
// macro ProtoPath+"SetKCod.mac" "ПОЛК" w-2*h_dsp d-ОтстСз;
macro ProtoPath+"SetEnam.mac" "Полка межсекционная";
macro ProtoPath+"SetFix.mac"  Крепеж Крепеж 0 0;
macro ProtoPath+"MakePan.mac" h_dsp ОтстСз h_c+h_dsp+h_niz w-2*h_dsp d-ОтстСз 12;
if (HasDuct!=0) //-- Если нужен вырез, добавляем его
{
  objident last 1 Pan;
  #Pat rectangle ShiftDuct-h_dsp,0,0 ShiftDuct+WidthDuct+h_dsp,DepthDuct+h_dsp,0;
  macro ProtoPath+"PutCutr.mac" Pan, Pat, 1, 0, 5;
  delete Pat done;
}
//-- Делим секцию на нужное количество частей
if (КолПЯ2>1)
{
  sdvv=(w-h_dsp*(КолПЯ2+1))/КолПЯ2;  //-- Расстояние между ящиками в вержней секции
  macro ProtoPath+"SetMat.mac" PrMater;
  macro ProtoPath+"SetECod.mac" "1107";
  // macro ProtoPath+"SetKCod.mac" "ПОЛК" w-2*h_dsp d-ОтстСз;
  macro ProtoPath+"SetEnam.mac" "Стойка между ящиками";
  i=0;
  loop:
  i=i+1;
  if (i*(sdvv+h_dsp)>ShiftDuct-h_dsp&&i*(sdvv+h_dsp)<=ShiftDuct+WidthDuct+h_dsp)
  {
    macro ProtoPath+"SetCuts.mac" 0 0 DepthDuct+h_dsp 0;
  }
  else
  {
    macro ProtoPath+"SetCuts.mac" 0 0 0 0;
  }
  macro ProtoPath+"MakePan.mac" i*(sdvv+h_dsp) ОтстСз h_c+2*h_dsp+h_niz h-h_c-h_niz-3*h_dsp d-ОтстСз 11;
  if (i<КолПЯ2-1)
  {
    goto loop;
  }
}
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
//---------------------------------------------------------------------
//-- Заполнение нижней секции
macro ProtoPath+"SetFasPlas.mac" D_DxN D_DxN D_DzN D_DzN;
macro ProtoPath+"SetBoxF.mac" 0 0 0 0;
macro ProtoPath+"MakeNiche.mac" h_dsp ОтстСз h_c+h_dsp
    w-2*h_dsp  //-- Ширина
    d-ОтстСз   //-- Глубина
    h_niz-h_dsp/2  //-- Высота
    Заполн     //-- Тип заполнения
    КолПЯ      //-- Количество полок или ящиков
    РисФас     //-- Рисунок фасада
    ЗаглПол    //-- Заглубление полок
    PrMater    //-- Материал корпуса
    ФасМат     //-- Материал фасада
    ДноМат     //-- Материал дна ящика
    ТипЯщ      //-- Тип ящика
    ФасВсМат   //-- Материал  вставки в фасад
    СторОткр   //-- Открывание двери
    ТипРуч     //-- Тип ручки
    ВысотРуч   //-- Высота установки ручки
    ПовРуч     //-- Поворот ручки
    Rpl_r	//-- Расположение ручки нижней секции
    ДвеДвери   //-- Двойная дверь
    ОткрДверь  //-- Открытая дверь
    СдвРуч     //-- Сдвиг ручки
    Петля      //-- Тип петли
    Накладн    //-- Фасад накладной
    ЦвКром     //-- Цвет кромки
    ТипКром    //-- Тип кромки
    Крепеж     //-- Крепеж корпуса
    0          //-- Тип двери
;
//---------------------------------------------------------------
//-- Заполнение верхней секции
if (КолПЯ2>0)
{

  i=0;
  loop1:
  i=i+1;
  //-- Нахлесты фасада
  if (i==1)
  {
    macro ProtoPath+"SetBoxF.mac" -1-offset/2 0 0 0;
  }
  if (i==КолПЯ2)
  {
    macro ProtoPath+"SetBoxF.mac" 0 -1-offset/2 0 0;
  }
  if (i!=1&&i!=КолПЯ2)
  {
    macro ProtoPath+"SetBoxF.mac" -offset/2 -offset/2 0 0;
  }
  macro ProtoPath+"MakeNiche.mac" h_dsp*i+sdvv*(i-1) ОтстСз h_c+2.5*h_dsp+h_niz
      sdvv       //-- Ширина
      d-ОтстСз   //-- Глубина
      h-h_c-h_niz-3.5*h_dsp //-- Высота
      1          //-- Тип заполнения
      1          //-- Количество полок или ящиков
      РисФас2    //-- Рисунок фасада
      ЗаглПол    //-- Заглубление полок
      PrMater    //-- Материал корпуса
      ФасМатВ    //-- Материал фасада
      ДноМат     //-- Материал дна ящика
      ТипЯщ      //-- Тип ящика
      ФасВсМат   //-- Материал  вставки в фасад
      СторОткр   //-- Открывание двери
      ТипРуч     //-- Тип ручки
      ВысотРуч   //-- Высота установки ручки
      ПовРуч     //-- Поворот ручки
      Rt		     //-- Расположение ручки верхней секции
      ДвеДвери   //-- Двойная дверь
      ОткрДверь  //-- Открытая дверь
      СдвРуч     //-- Сдвиг ручки
      Петля      //-- Тип петли
      Накладн    //-- Фасад накладной
      ЦвКром     //-- Цвет кромки
      ТипКром    //-- Тип кромки
      Крепеж     //-- Крепеж корпуса
      0          //-- Тип двери
  ;
  if (i<КолПЯ2)
  {
    goto loop1;
  }
}
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=resnap();
exit;

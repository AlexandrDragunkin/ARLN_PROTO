// -- Создание объекта - стол кухонный шестиугольный
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов
defarr x[6],y[6],rad[5];
//-- Параметры для расчета распашных дверей
global D_DxU D_DzU D_DxN D_DzN D_DxD;

PrMater=DbVar("КорпМат",0);     //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);       //-- Материал задней стенки
w=DbVar("Шир",330);			        //-- Ширина
d=DbVar("Глуб",320);			      //-- Глубина
h=DbVar("Выс",822);			        //-- Высота
h_c=DbVar("ВысЦок",100);		    //-- Высота цоколя
ШирГлух=DbVar("ШирС",100);		  //-- Ширина глухой секции слева
ШирГлух2=DbVar("ШирС2",ШирГлух);		  //-- Ширина глухой секции справа
ЦвКром=DbVar("ЦвКромки",0);     //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   //-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	  //-- Тип крепежа
КрепЗС=DbVar("КрепЗС",0);   	  //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);   //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);   //-- Глубина установки задней стенки
ЗаглПол=DbVar("ЗаглПол",0);     //-- Заглубление полок
ФасМат=DbVar("ФасМат",0);       //-- Материал фасада
ФасВсМат=DbVar("ФасВсМат",0);   //-- Материал вставки в фасад
СторОткр=DbVar("СторОткр",0);   //-- Открывание двери
ТипРуч=DbVar("ТипРуч",0);       //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);   //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);       //-- Поворот ручки
ОткрДверь=DbVar("ОткрДверь",0); //-- Открытая дверь
Петля=DbVar("Петля",0);         //-- Тип петли
КолПЯ=DbVar("КолПЯ",0);         //-- Количество полок
РисФас=DbVar("РисФас",0);       //-- Рисунок фасада
Noga=DbVar("Ножка",0);		      //-- Тип ножки
Dno=DbVar("Dno",0);             //-- Тип дна
СдвРуч=DbVar("СдвРуч",40);      //-- Сдвиг ручки
ЗазорДв=DbVar("ЗазорДв",0);     //-- Зазор между дверьми
Rpl_r=dbvar("Rpl_r",0);		      //-- Расположение ручки
HasDuct=DbVar("HasDuct",0);     //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0); //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0); //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0); //-- Глубина короба
WallAngle=DbVar("WallAngle",90);//-- Пристенный угол
Widsth=DbVar("Widsth",100);     //-- Ширина стяжки
IsTop=DbVar("IsTop",1);         //-- Наличие крышки
WallChamfer=DbVar("WallChamfer",0);//-- Размер пристенной фаски
FixShelfRem=dbvar("FixShelfRem",0);//-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0); //-- Величина подреза съемной полки
Заполн=DbVar("Заполн",0);					  //-- Тип заполнения
//---------------------------------------------------
//-- Константы
D_DxN=DbVar("D_DxN",1.5);	//-- Зазор по ширине для накладных дверей
D_DzN=DbVar("D_DzN",2);		//-- Зазор по высоте для накладных дверей
D_DxD=DbVar("D_DxD",2);		//-- Зазор между двойными дверьми
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4);   //-- Толщина материала задней стенки
ЗСтВрезка=8;                      //-- Врезка задней стенки
Угол=0;                           //-- Угол стола
ТипУгла=3;                        //-- Cтол с прямоугольным вырезом
dum=120;                          //-- Размер "уголка" задней стенки под мойку
offset=14;                        //-- Нахлест накладной стенки на корпус
dst=60;                           //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                         //-- Наличие цоколя
Shiftcok=0;                       //-- Сдвиг цоколя
Накладн=1;                        //-- Фасад накладной
Правый=0;                         //-- Левая ориентация
LongPost=0;                       //-- Опорные стойки
FasCok=0;                         //-- Размер фасок под цоколь
ForOpen=0;                        //-- Для открытого
ConfDuct=0;                       //-- Конфигурация выреза под короб по умолчанию
Shift=50;                         //-- Сдвиг опор от края корпуса
//-- Установим тип двери карусельная
defarr DOORARR[1] ;
furn="'230300'"
DOORTYPE=0
kol1=npgetbywhere(2,"furntype = "+furn,"DOORARR");
if (kol1>0)
{
  DOORTYPE=DOORARR[1];
}
if (WallAngle!=90)
{
  putmsg("Для данного прототипа пристенный угол, отличный от 90 градусов не реализован",0);
  WallAngle=90;
}
//-------------------------------------------------
DimX=w-ШирГлух2;
DimY=((d-ШирГлух)-h_dsp*iif(Dno==0,1,1));
MinDepth=ШирГлух;
MinDepthX=ШирГлух2;
Ang=radian(WallAngle-90);   //-- Пристенный угол
ws=(w-d*sin(Ang))/cos(Ang); //-- Пристенная ширина
ds=(d-w*sin(Ang))/cos(Ang)+h_dsp*sin(Ang)*iif(Ang>0,1,0); //-- Пристенная глубина
DimX=DimX/cos(Ang);
DimY=DimY/cos(Ang);
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  ds=ds-DepthDuct;
  HasDuct=0;
}
//-------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
macro ProtoPath+"SetTypeFill.mac" Заполн;
//#ll line w-20,0,0 w-20,d-w+ШирГлух+iif(Dno==0,h_dsp,0)-20,0 done;
//chprop color ll done 15;
//attrobj attach "Contact" Done ll 16;
//#ll line w-20,d-w+ШирГлух+iif(Dno==0,h_dsp,0)-20,0 ШирГлух-20,d-w+ШирГлух-20+iif(Dno==0,h_dsp,0),0 done;
//chprop color ll done 15;
//attrobj attach "Contact" Done ll 16;
//#ll line ШирГлух-20,d-w+ШирГлух-20+iif(Dno==0,h_dsp,0),0 ШирГлух-20,d+iif(Dno==0,h_dsp,0)-20,0 done;
//chprop color ll done 15;
//attrobj attach "Contact" Done ll 16;
//#ll line ШирГлух-20,d+iif(Dno==0,h_dsp,0)-20,0 0,d+iif(Dno==0,h_dsp,0)-20,0 done;
//chprop color ll done 15;
//attrobj attach "Contact" Done ll 16;

#ll line w-20,0,0 w-20,ШирГлух-20,0 done;
chprop color ll done 15;
attrobj attach "Contact" Done ll 16;
#ll line w-20,ШирГлух-20,0 ШирГлух2-20,ШирГлух-20,0 done;
chprop color ll done 15;
attrobj attach "Contact" Done ll 16;
#ll line ШирГлух2-20,ШирГлух-20,0  ШирГлух2-20,d-20,0 done;
chprop color ll done 15;
attrobj attach "Contact" Done ll 16;
#ll line ШирГлух2-20,d-20,0 0,d-20,0 done;
chprop color ll done 15;
attrobj attach "Contact" Done ll 16;

macro ProtoPath+"SetECod.mac" "7101" ;
// macro ProtoPath+"SetKCod.mac" "НОГА" 0 0;
macro ProtoPath+"SetEnam.mac" "Ножка мебельная" ;
macro ProtoPath+"MakeLeg.mac" Noga 1  w-Shift Shift+iif(ConfDuct==3,DepthDuct,0) 0 0 0 0;
macro ProtoPath+"MakeLeg.mac" Noga 1  w-Shift ШирГлух-Shift 0 0 0 0;
macro ProtoPath+"MakeLeg.mac" Noga 1  ШирГлух2+30 d-w+ШирГлух-Shift 0 0 0 0;
macro ProtoPath+"MakeLeg.mac" Noga 1  ШирГлух2-Shift d-w+ШирГлух+30 0 0 0 0;
macro ProtoPath+"MakeLeg.mac" Noga 1  ШирГлух2-Shift d-Shift 0 0 0 0;
macro ProtoPath+"MakeLeg.mac" Noga 1  Shift d-Shift 0 0 0 0;
macro ProtoPath+"MakeLeg.mac" Noga 1  Shift Shift+iif(ConfDuct==2,DepthDuct,WallChamfer) 0 0 0 0;
if (WallChamfer>0)
{
  macro ProtoPath+"MakeLeg.mac" Noga 1  Shift+WallChamfer Shift+iif(ConfDuct==2,DepthDuct,0) 0 0 0 0;
}
//-------------------------------------------------
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
macro ProtoPath+"SetWallAngle.mac" WallAngle;
macro ProtoPath+"SetCorpsCor.mac" Widsth, IsTop, ШирГлух2;
macro ProtoPath+"SetWallChamfer.mac" WallChamfer;
macro ProtoPath+"SetCorpsCorButts.mac" 0, 0;
//-------------------------------------------------
macro ProtoPath+"MakeCorpsCor.mac" 0 0 0
    w,                    //-- Ширина
    d,                    //-- Глубина
    h,                    //-- Высота
    h_c,                  //-- Высота цоколя
    MinDepth,             //-- Размер меньшей стороны
    ТипУгла,              //-- Тип угла
    DimX,                 //-- Фаска Х/ Радиус
    DimY+h_dsp,                 //-- Фаска Y
    Правый,               //-- Правая ориентация
    PrMater,        	    //-- Материал корпуса
    ЗСтМат,               //-- Материал задней стенки
    ЦвКром,               //-- Цвет кромки
    ТипКром,              //-- Тип кромки
    ТипЗСтен,             //-- Тип задней стенки
    УстЗСтен,             //-- Глубина установки задней стенки
    offset                //-- Врезка\Нахлест задней стенки
    Крепеж,               //-- Тип крепежа корпуса
    КрепЗС,      	        //-- Тип крепежа задней стенки
    Dno,                  //-- Накладное дно
    Dum,                  //-- Размер "уголка" задней стенки под мойку
    Dst,                  //-- Ширина дополнительной планки
    HasCok,               //-- Наличие цоколя
    Shiftcok              //-- Сдвиг цоколя
    LongPost              //-- Опорные стойки
    FasCok                //-- Размер фасок под цоколь
    ForOpen;              //-- Для открытого изделия
;
//-------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0)  { ОтстСз=0; }		      //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		      //-- Накладной для мойки
if (ТипЗСтен==0)  { ОтстСз=h_dvp; }	    //-- Утопленный
if (ТипЗСтен==2)  { ОтстСз=0; }		      //-- Нет
if (ТипЗСтен==3)  { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	    //-- Жесткая планка
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
if (WallChamfer>0)
{
  macro ProtoPath+"SetWallChamfer.mac" WallChamfer-2*iif(ТипЗСтен==1,h_dvp,ОтстСз)+h_dsp*sqrt(2);
}
macro ProtoPath+"MakeNicheCor.mac" iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)) ОтстСз h_c+h_dsp
    w-h_dsp-iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)),      //-- Ширина
    d-ОтстСз-h_dsp, //-- Глубина
    h-h_c-2*h_dsp,  //-- Высота
    MinDepth,       //-- Размер меньшей стороны
    ТипУгла,        //-- Тип угла
    DimX-h_dsp+ЗаглПол,   //-- Фаска Х/ Радиус
    DimY+ЗаглПол, //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    КолПЯ,          //-- Количество полок
    0,              //-- Рисунок фасада
    Угол,    	      //-- Угол поворота двери
    0, 	            //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    ФасМат,         //-- Материал фасада
    ФасВсМат,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    Rpl_r	          //-- Расположение ручки
    0,              //-- Двойная дверь
    ОткрДверь,      //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    Петля,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,				  //-- Тип крепежа
    0 	            //-- Тип двери
;
defarr qwe[1];
furn="'230300'"
kol1=npgetbywhere(2,"furntype = "+furn,"qwe");
if (kol1>0)
{
   DoorKonst=qwe[1];	        //-- Тип двери карусельная
}
macro ProtoPath+"SetDoorK.mac" DoorKonst;
macro ProtoPath+"SetFasdN.mac" ФасМат РисФас ЦвКром ТипКром ФасВсМат ФасВсМат ;
macro ProtoPath+"SetDoor.mac" СторОткр 0 ОткрДверь 2 Петля 0 h_dsp;
macro ProtoPath+"SetHand.mac" 0 Rpl_r ;
macro ProtoPath+"SetHandl.mac" ТипРуч, //-- Тип ручки
      СдвРуч,    //-- Сдвиг ручки по x
      ВысотРуч,  //-- Сдвиг ручки по z
      ПовРуч;    //-- Поворот ручки

macro ProtoPath+"MakeDoorKar.mac"
ШирГлух2 d-h_dsp h_c+h_dsp		//-- Правый нижний дальний угол
W-ШирГлух2-h_dsp D-ШирГлух-h_dsp H-h_c-h_dsp*2   //-- Размер по ширине, глубине и высоте
  h_dsp-D_DxN                             //-- Изменения фасада: Cправа
  h_dsp-D_DxN                             //--                   Слева
  h_dsp-D_DzN                             //--                   Сверху
  h_dsp-D_DzN                             //--                   Снизу
  ЗазорДв                                 //-- Зазор по ширине
;
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;
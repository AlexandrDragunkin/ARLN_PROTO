//-- Пенал трехсекционный нижний и верхний
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов
defarr x[4],y[4],rad[3];

PrMater=DbVar("КорпМат",0);     //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);       //-- Материал задней стенки
w=DbVar("Шир",330);			        //-- Ширина
d=DbVar("Глуб",320);			      //-- Глубина
h=DbVar("Выс",1400);		        //-- Высота
h_niz=DbVar("ВысНиж",500);		  //-- Высота нижней секции
h_sred=DbVar("ВысСред",300);		//-- Высота нижней секции
h_c=DbVar("ВысЦок",100);		    //-- Высота цоколя
ШирСтяж=DbVar("ШирСтяж",100);		//-- Ширина планок
ЦвКром=DbVar("ЦвКромки",0);   	//-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   //-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	  //-- Тип крепежа
КрепЗС=DbVar("КрепЗС",0);   	  //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);   //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);   //-- Глубина установки задней стенки
ЕстьКрышка=DbVar("Крышка",0);   //-- Наличие крышки
ЗаглПол=DbVar("ЗаглПол",0);     //-- Заглубление полок
ДноМат=DbVar("ДноМат",0);       //-- Материал дна ящика
ТипЯщ=DbVar("ТипЯщ",0);         //-- Тип ящика
ФасВсМат=DbVar("ФасВсМат",0);   //-- Материал  вставки в фасад
СторОткр=DbVar("СторОткр",0);   //-- Открывание двери
ТипРуч=DbVar("ТипРуч",0);       //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);   //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);       //-- Поворот ручки
ДвеДвери=DbVar("ДвеДвери",0);   //-- Двойная дверь
ОткрДверь=DbVar("ОткрДверь",0); //-- Открытая дверь или выдвинутые ящики
Петля=DbVar("Петля",0);         //-- Тип петли
Noga=DbVar("Ножка",0);		      //-- Тип ножки
Dno=DbVar("Dno",DbVar("g_Dno",0)); //-- Тип дна
//-- Заполнение нижней секции
Заполн=DbVar("Заполн",0);       //-- Тип заполнения нижней секции
КолПЯ=DbVar("КолПЯ",0);         //-- Количество полок или ящиков нижней секции
РисФас=DbVar("РисФас",0);       //-- Рисунок фасада нижней секции
ФасМат=DbVar("ФасМат",0);       //-- Материал фасада нижней секции
Rpl_r=dbvar("Rpl_r",0);		//-- Расположение ручки нижней секции
//-- Заполнение средней секции
Заполн2=DbVar("Заполн2",0);     //-- Тип заполнения средней секции
КолПЯ2=DbVar("КолПЯ2",0);       //-- Количество полок или ящиков срденей секции
РисФас2=DbVar("РисФас2",0);     //-- Рисунок фасада средней секции
ФасМатС=DbVar("ФасМатС",0);     //-- Материал фасада средней секции
RV=dbvar("RV",0);		//-- Расположение ручки средней секции
//-- Заполнение верхней секции
Заполн3=DbVar("Заполн3",0);     //-- Тип заполнения верхней секции
КолПЯ3=DbVar("КолПЯ3",0);       //-- Количество полок или ящиков верхней секции
РисФас3=DbVar("РисФас3",0);     //-- Рисунок фасада верхней секции
ФасМатВ=DbVar("ФасМатВ",0);     //-- Материал фасада верхней секции
СдвРуч=DbVar("СдвРуч",40);   //-- Сдвиг ручки
Rt=dbvar("Rt",0);		//-- Расположение ручки верхней секции
HasDuct=DbVar("HasDuct",0);          //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0);      //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0);      //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0);      //-- Глубина короба
FixShelfRem=dbvar("FixShelfRem",0);  //-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0);  //-- Величина подреза съемной полки
//---------------------------------------------------
//-- Константы
D_DxN=DbVar("D_DxN",1.5);	//-- Зазор по ширине для накладных дверей
D_DzN=DbVar("D_DzN",2);		//-- Зазор по высоте для накладных дверей
D_DxD=DbVar("D_DxD",2);		//-- Зазор между двойными дверьми
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4);   //-- Толщина материала задней стенки
Накладн=1;                        //-- Фасад накладной
ЗСтВрезка=8;                      //-- Врезка задней стенки
dum=120;                          //-- Размер "уголка" задней стенки под мойку
offset=14;                        //-- Нахлест накладной стенки на корпус
dst=60;                           //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                         //-- Наличие цоколя
Shiftcok=0;                       //-- Сдвиг цоколя
LongPost=0;                       //-- Опорные стойки
FasCok=0;                         //-- Размер фасок под цоколь
FixPlan=0;	                      //-- Наличие крепежной планки
ConfDuct=0;                       //-- Конфигурация выреза под короб по умолчанию.
Shift=50;                         //-- Сдвиг опор от края корпуса
//---------------------------------------------------
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  d=d-DepthDuct;
  HasDuct=0;
}
//---------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
//--Построение ножек
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  d=d-DepthDuct;
  HasDuct=0;
}
x[1]=w;
y[1]=0;
rad[1]=0;
x[2]=w;
y[2]=d;
rad[2]=0;
x[3]=0;
y[3]=d;
rad[3]=0;
x[4]=0;
y[4]=0;
if (ConfDuct==3)
{
  y[1]=DepthDuct;
}
if (ConfDuct==2)
{
  y[4]=DepthDuct;
}
if (ConfDuct==4)
{
  if (2*Shift>ShiftDuct)
  {
    y[4]=DepthDuct;
  }
  if (w-2*Shift<ShiftDuct+WidthDuct)
  {
    y[1]=DepthDuct;
  }
}
macro ProtoPath+"aStBase.mac" Noga,4,x,y,rad,Shift;
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
//-------------------------------------------------
macro ProtoPath+"MakeCorps.mac" 0 0 0
    w           //-- Ширина
    d           //-- Глубина
    h           //-- Высота
    h_c         //-- Высота цоколя
    PrMater     //-- Материал корпуса
    ЗСтМат      //-- Материал задней стенки
    ШирСтяж     //-- Ширина стяжки
    ТипЗСтен    //-- Тип задней стенки
    УстЗСтен    //-- Глубина установки задней стенки
    offset      //-- Врезка\Нахлест задней стенки
    ЕстьКрышка  //-- Наличие крышки
    0           //-- Наличие сушилки
    ЦвКром      //-- Цвет кромки
    ТипКром     //-- Тип кромки
    Крепеж      //-- Крепеж корпуса
    КрепЗС   	  //-- Тип крепежа задней стенки
    Dno         //-- Накладное дно
    Dum         //-- Размер "уголка" задней стенки под мойку
    Dst         //-- Ширина дополнительной планки
    HasCok      //-- Наличие цоколя
    Shiftcok    //-- Сдвиг цоколя
    LongPost    //-- Опорные стойки
    FasCok      //-- Размер фасок под цоколь
    FixPlan     //-- Наличие крепежной планки
;
//-------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0) { ОтстСз=0; }		    //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		    //-- Накладной для мойки
if (ТипЗСтен==0) { ОтстСз=h_dvp; }	  //-- Утопленный
if (ТипЗСтен==2) { ОтстСз=0; }		    //-- Нет
if (ТипЗСтен==3) { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	  //-- Жесткая планка
//---------------------------------------------------------------------
needPolka1=1;
needPolka2=1;
//--Строим общую стойку нижней и средней секции, если тип заполения в них "Горизнтальные ящики"
if (Заполн==4&&КолПЯ>1&&Заполн==Заполн2&&КолПЯ==КолПЯ2)
{
  needPolka1=0;
	macro ProtoPath+"SetNeedPostBox.mac" 0; //--стойка не нужна.
	macro ProtoPath+"SetECod.mac" "1107" ;
	// macro ProtoPath+"SetKCod.mac" "Стойка" h_niz+h_dsp+h_sred  d;
	macro ProtoPath+"SetEnam.mac" "Стойка" ;
	macro ProtoPath+"SetFix.mac"  Крепеж Крепеж 0 0;
	macro ProtoPath+"SetKrom.mac" ЦвКром 0 0 0 ТипКром ;
	macro ProtoPath+"SetMat.mac" PrMater;
	//--Цикл построения стоек
	ip=0;
	LP1:
	ip=ip+1;
	otsSt=0;
	xcoor=(((w-2*h_dsp)-(КолПЯ-1)*h_dsp)/КолПЯ)*ip+h_dsp*(ip-1)+h_dsp;
	if (xcoor>ShiftDuct&&xcoor<ShiftDuct+WidthDuct&&HasDuct==1)||(xcoor+h_dsp>ShiftDuct&&xcoor+h_dsp<ShiftDuct+WidthDuct&&HasDuct==1)
	{
	 otsSt=DepthDuct;
	}
	macro ProtoPath+"MakePan.mac" xcoor ОтстСз h_c+h_dsp h_niz+h_dsp+h_sred d-otsSt 11;
	if (ip<КолПЯ-1) { goto LP1; }
}
else
{
  macro ProtoPath+"SetNeedPostBox.mac" 1; //--стойка нужна.
}
//--Строим общую стойку средней и верхней секции, если тип заполения в них "Горизнтальные ящики"
if (Заполн2==4&&КолПЯ2>1&&Заполн2==Заполн3&&КолПЯ2==КолПЯ3)
{
  needPolka2=0;
	macro ProtoPath+"SetNeedPostBox.mac" 0; //--стойка не нужна.
	macro ProtoPath+"SetECod.mac" "1107" ;
	// macro ProtoPath+"SetKCod.mac" "Стойка" h-h_c-h_niz-3*h_dsp  d;
	macro ProtoPath+"SetEnam.mac" "Стойка" ;
	macro ProtoPath+"SetFix.mac"  Крепеж Крепеж 0 0;
	macro ProtoPath+"SetKrom.mac" ЦвКром 0 0 0 ТипКром ;
	macro ProtoPath+"SetMat.mac" PrMater;
	//--Цикл построения стоек
	ip=0;
	LP2:
	ip=ip+1;
	otsSt=0;
	xcoor=(((w-2*h_dsp)-(КолПЯ2-1)*h_dsp)/КолПЯ2)*ip+h_dsp*(ip-1)+h_dsp;
	if (xcoor>ShiftDuct&&xcoor<ShiftDuct+WidthDuct&&HasDuct==1)||(xcoor+h_dsp>ShiftDuct&&xcoor+h_dsp<ShiftDuct+WidthDuct&&HasDuct==1)
	{
	 otsSt=DepthDuct;
	}
	macro ProtoPath+"MakePan.mac" xcoor ОтстСз h_c+2*h_dsp+h_niz h-h_c-h_niz-3*h_dsp d-otsSt 11;
	if (ip<КолПЯ2-1) { goto LP2; }
}
else
{
  macro ProtoPath+"SetNeedPostBox.mac" 1; //--стойка нужна.
}
//---------------------------------------------------------------
//-- Полка между секциями
if (needPolka2==1)
{
  macro ProtoPath+"SetMat.mac" PrMater;
  macro ProtoPath+"SetECod.mac" "1107" ;
  // macro ProtoPath+"SetKCod.mac" "ПОЛК" w-2*h_dsp d-ОтстСз;
  macro ProtoPath+"SetEnam.mac" "Полка межсекционная" ;
  macro ProtoPath+"SetFix.mac"  Крепеж Крепеж 0 0;
  macro ProtoPath+"SetKrom.mac" ЦвКром 0 0 0 ТипКром ;
  macro ProtoPath+"MakePan.mac" h_dsp ОтстСз h_c+2*h_dsp+h_niz+h_sred w-2*h_dsp d-ОтстСз 12;
  if (HasDuct!=0) //-- Если нужен вырез, добавляем его
  {
    objident last 1 Pan;
    #Pat rectangle 3points ShiftDuct-h_dsp,0,0 ShiftDuct+WidthDuct+h_dsp,0,0 ShiftDuct+WidthDuct+h_dsp,DepthDuct+h_dsp,0;
    macro ProtoPath+"PutCutr.mac" Pan, Pat, 1, 0, 5;
    delete Pat done;
  }
}
//---------------------------------------------------------------------
//-- Полка между секциями
if (needPolka1==1)
{
  macro ProtoPath+"MakePan.mac" h_dsp ОтстСз h_c+h_dsp+h_niz w-2*h_dsp d-ОтстСз 12;
  if (HasDuct!=0) //-- Если нужен вырез, добавляем его
  {
    objident last 1 Pan;
    #Pat rectangle 3points ShiftDuct-h_dsp,0,0 ShiftDuct+WidthDuct+h_dsp,0,0 ShiftDuct+WidthDuct+h_dsp,DepthDuct+h_dsp,0;
    macro ProtoPath+"PutCutr.mac" Pan, Pat, 1, 0, 5;
    delete Pat done;
  }
}
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
//-- Заполнение нижней секции
macro ProtoPath+"SetFasPlas.mac" D_DxN D_DxN iif(РисФас2==0,D_DzN,(h_dsp+D_DxD)/2) D_DzN;
macro ProtoPath+"SetBoxF.mac" 0 0 iif(РисФас2>0,-(h_dsp/2)+D_DxD/2,0) 0;
macro ProtoPath+"MakeNiche.mac" h_dsp ОтстСз h_c+h_dsp
    w-2*h_dsp  //-- Ширина
    d-ОтстСз   //-- Глубина
    h_niz		  //-- Высота
    Заполн     //-- Тип заполнения
    КолПЯ      //-- Количество полок или ящиков
    РисФас     //-- Рисунок фасада
    ЗаглПол    //-- Заглубление полок
    PrMater    //-- Материал корпуса
    ФасМат     //-- Материал фасада
    ДноМат     //-- Материал дна ящика
    ТипЯщ      //-- Тип ящика
    ФасВсМат   //-- Материал вставки в фасад
    СторОткр   //-- Открывание двери
    ТипРуч     //-- Тип ручки
    ВысотРуч   //-- Высота установки ручки
    ПовРуч     //-- Поворот ручки
    Rpl_r	     //-- Расположение ручки нижней секции
    ДвеДвери   //-- Двойная дверь
    ОткрДверь  //-- Открытая дверь
    СдвРуч     //-- Сдвиг ручки
    Петля      //-- Тип петли
    Накладн    //-- Фасад накладной
    ЦвКром     //-- Цвет кромки
    ТипКром    //-- Тип кромки
    Крепеж		 //-- Крепеж корпуса
    0          //-- Тип двери
;
//---------------------------------------------------------------
//-- Заполнение средней секции
macro ProtoPath+"SetFasPlas.mac" D_DxN D_DxN iif(РисФас3==0,D_DzN,(h_dsp+D_DxD)/2) iif(РисФас==0,D_DzN,(h_dsp+D_DxD)/2);
macro ProtoPath+"SetBoxF.mac" 0 0 iif(РисФас3>0,-(h_dsp/2)+D_DxD/2,0) iif(РисФас>0,-(h_dsp/2)+D_DxD/2,0);
macro ProtoPath+"MakeNiche.mac" h_dsp ОтстСз h_c+2*h_dsp+h_niz
    w-2*h_dsp  //-- Ширина
    d-ОтстСз   //-- Глубина
    h_sred		 //-- Высота
    Заполн2    //-- Тип заполнения
    КолПЯ2     //-- Количество полок или ящиков
    РисФас2    //-- Рисунок фасада
    ЗаглПол    //-- Заглубление полок
    PrMater    //-- Материал корпуса
    ФасМатС    //-- Материал фасада
    ДноМат     //-- Материал дна ящика
    ТипЯщ      //-- Тип ящика
    ФасВсМат   //-- Материал вставки в фасад
    СторОткр   //-- Открывание двери
    ТипРуч     //-- Тип ручки
    ВысотРуч   //-- Высота установки ручки
    ПовРуч     //-- Поворот ручки
    RV		     //-- Расположение ручки средней секции
    ДвеДвери   //-- Двойная дверь
    ОткрДверь  //-- Открытая дверь
    СдвРуч     //-- Сдвиг ручки
    Петля      //-- Тип петли
    Накладн    //-- Фасад накладной
    ЦвКром     //-- Цвет кромки
    ТипКром    //-- Тип кромки
    Крепеж		 //-- Крепеж корпуса
    0          //-- Тип двери
;
//---------------------------------------------------------------
//-- Заполнение верхней секции
macro ProtoPath+"SetFasPlas.mac" D_DxN D_DxN D_DzN iif(РисФас2==0,D_DzN,(h_dsp+D_DxD)/2);
macro ProtoPath+"SetBoxF.mac" 0 0 0 iif(РисФас2>0,-(h_dsp/2)+D_DxD/2,0);
macro ProtoPath+"MakeNiche.mac" h_dsp ОтстСз h_c+3*h_dsp+h_niz+h_sred
    w-2*h_dsp  //-- Ширина
    d-ОтстСз   //-- Глубина
    h-h_c-h_niz-h_sred-4*h_dsp //-- Высота
    Заполн3    //-- Тип заполнения
    КолПЯ3     //-- Количество полок или ящиков
    РисФас3    //-- Рисунок фасада
    ЗаглПол    //-- Заглубление полок
    PrMater    //-- Материал корпуса
    ФасМатВ    //-- Материал фасада
    ДноМат     //-- Материал дна ящика
    ТипЯщ      //-- Тип ящика
    ФасВсМат   //-- Материал  вставки в фасад
    СторОткр   //-- Открывание двери
    ТипРуч     //-- Тип ручки
    ВысотРуч   //-- Высота установки ручки
    ПовРуч     //-- Поворот ручки
    Rt		//-- Расположение ручки верхней секции
    ДвеДвери   //-- Двойная дверь
    ОткрДверь  //-- Открытая дверь
    СдвРуч     //-- Сдвиг ручки
    Петля      //-- Тип петли
    Накладн    //-- Фасад накладной
    ЦвКром     //-- Цвет кромки
    ТипКром    //-- Тип кромки
    Крепеж		 //-- Крепеж корпуса
    0          //-- Тип двери
;
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;

//-- Стол угловой
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов
defarr x[4],y[4],rad[3];
defarr error[10];
NULLOUT=initarray(error,"");

PrMater=DbVar("КорпМат",0);     //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);       //-- Материал задней стенки
w=DbVar("Шир",330);			        //-- Ширина
d=DbVar("Глуб",320);			      //-- Глубина
h=DbVar("Выс",822);			        //-- Высота
h_c=DbVar("ВысЦок",100);		    //-- Высота цоколя
ШирГлух=DbVar("ШирС",100);		  //-- Ширина глухой секции
ПравВар=DbVar("ПравВар",1);     //-- Ориентация
ШирСтяж=DbVar("ШирСтяж",100);	  //-- Ширина планок
ЦвКром=DbVar("ЦвКромки",0);     //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   //-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	  //-- Тип крепежа
КрепЗС=DbVar("КрепЗС",0);   	  //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);   //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);   //-- Глубина установки задней стенки
ЕстьКрышка=DbVar("Крышка",0);   //-- Наличие крышки
КолПЯ=DbVar("КолПЯ",0);         //-- Количество полок или ящиков
РисФас=DbVar("РисФас",0);       //-- Рисунок фасада
ЗаглПол=DbVar("ЗаглПол",0);     //-- Заглубление полок
ФасМат=DbVar("ФасМат",0);       //-- Материал фасада
ФасВсМат=DbVar("ФасВсМат",0);   //-- Материал вставки в фасад
СторОткр=DbVar("СторОткр",0);   //-- Открывание двери
ТипРуч=DbVar("ТипРуч",0);       //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);   //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);       //-- Поворот ручки
ДвеДвери=DbVar("ДвеДвери",0);   //-- Двойная дверь
ОткрДверь=DbVar("ОткрДверь",0); //-- Открытая дверь
Петля=DbVar("Петля",0);         //-- Тип петли
IsStop=DbVar("IsStop",0);       //-- Наличие стыкового упора
Noga=DbVar("Ножка",0);		      //-- Тип ножки
Dno=DbVar("Dno",0);             //-- Тип дна
СдвРуч=DbVar("СдвРуч",40);      //-- Сдвиг ручки
Rpl_r=dbvar("Rpl_r",0);		      //-- Расположение ручки
HasDuct=DbVar("HasDuct",0);     //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0); //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0); //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0); //-- Глубина короба
FixShelfRem=dbvar("FixShelfRem",0); //-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0); //-- Величина подреза съемной полки
Заполн=DbVar("Заполн",0);					  //-- Тип заполнения
NeedCorner=DbVar("NeedCorner",0);		//-- Тип угловой планки
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4); //-- Толщина материала задней стенки
Накладн=1;                    //-- Фасад накладной
ЗСтВрезка=8;                  //-- Врезка задней стенки
dum=120;                      //-- Размер "уголка" задней стенки под мойку
offset=14;                    //-- Нахлест накладной стенки на корпус
dst=60;                       //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                     //-- Наличие цоколя
Shiftcok=0;                   //-- Сдвиг цоколя
LongPost=0;                   //-- Опорные стойки
FasCok=0;                     //-- Размер фасок под цоколь
DimX=w-ШирГлух;               //-- Размер выреза по оси X
DimY=ШирСтяж;                 //-- Размер выреза по оси Y
FixPlan=0;	                  //-- Наличие крепежной планки
ConfDuct=0;                   //-- Конфигурация выреза под короб по умолчанию
Shift=50;                     //-- Сдвиг опор от края корпуса
StopWidth=150;                //-- Ширинв стыкового упора
StopDepth=35;                 //-- Глубина стыкового упора
StopShift=50;                 //-- Сдвиг стыкового упора от двери
AngType=3;                    //-- Тип угла - вырез прямоугольный
//---------------------------------------------------
//-- Константы
D_DxN=DbVar("D_DxN",1.5);	//-- Зазор по ширине для накладных дверей
D_DzN=DbVar("D_DzN",2);		//-- Зазор по высоте для накладных дверей
D_DxD=DbVar("D_DxD",2);		//-- Зазор между двойными дверьми
//---------------------------------------------------
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  d=d-DepthDuct;
  HasDuct=0;
}
if (IsStop==0)  //-- Если стыкового упора нет, то обнуляем его параметры
{
  StopWidth=0;
  StopDepth=0;
  StopShift=0;
}
else  //-- Иначе обнуляем паараметры угла
{
  AngType=0;  //-- Вырез отсутствует
  DimX=0;     //-- Фаска Х/ Радиус
  DimY=0;     //-- Фаска Y
}
//---------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetTypeFill.mac" Заполн;
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
//-- Построение ножек
x[1]=w;
y[1]=0;
rad[1]=iif(ПравВар==0,0,-1);
x[2]=w;
y[2]=d;
rad[2]=0;
x[3]=0;
y[3]=d;
rad[3]=iif(ПравВар==1,0,-1);
x[4]=0;
y[4]=0;
if (ConfDuct==3)
{
  y[1]=DepthDuct;
}
if (ConfDuct==2)
{
  y[4]=DepthDuct;
}
if (ConfDuct==4)
{
  if (2*Shift>ShiftDuct)
  {
    y[4]=DepthDuct;
  }
  if (w-2*Shift<ShiftDuct+WidthDuct)
  {
    y[1]=DepthDuct;
  }
}
macro ProtoPath+"aStBase.mac" Noga,4,x,y,rad,Shift;
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
//-------------------------------------------------
macro ProtoPath+"MakeCorps.mac" 0 0 0
    w           //-- Ширина
    d           //-- Глубина
    h           //-- Высота
    h_c         //-- Высота цоколя
    PrMater     //-- Материал корпуса
    ЗСтМат      //-- Материал задней стенки
    ШирСтяж     //-- Ширина стяжки
    ТипЗСтен    //-- Тип задней стенки
    УстЗСтен    //-- Глубина установки задней стенки
    offset      //-- Врезка\Нахлест задней стенки
    ЕстьКрышка  //-- Наличие крышки
    0           //-- Наличие сушилки
    ЦвКром      //-- Цвет кромки
    ТипКром     //-- Тип кромки
    Крепеж      //-- Крепеж корпуса
    КрепЗС   	  //-- Тип крепежа задней стенки
    Dno         //-- Накладное дно
    Dum         //-- Размер "уголка" задней стенки под мойку
    Dst         //-- Ширина дополнительной планки
    HasCok      //-- Наличие цоколя
    Shiftcok    //-- Сдвиг цоколя
    LongPost    //-- Опорные стойки
    FasCok      //-- Размер фасок под цоколь
    FixPlan     //-- Наличие крепежной планки
;
//-------------------------------------------------
if (IsStop==0)  //-- Если нет стыкового упора
{
  //-- Построение передней стенки
  macro ProtoPath+"SetMat.mac" PrMater;
  macro ProtoPath+"SetECod.mac" "1103" ;
  // macro ProtoPath+"SetKCod.mac" "ФДНС" h-h_c ШирГлух;
  macro ProtoPath+"SetEnam.mac" "Фасадная стенка" ;
  macro ProtoPath+"SetKrom.mac" ЦвКром ТипКром ТипКром ТипКром ТипКром ;
  macro ProtoPath+"SetFix.mac"  0 0 0 0 ;
  macro ProtoPath+"MakePan.mac" iif(ПравВар==0,0,w-ШирГлух) d h_c h-h_c ШирГлух 14;
  //-- Построение передней стойки
  macro ProtoPath+"SetECod.mac" "1104" ;
  // macro ProtoPath+"SetKCod.mac" "ФССТ" h-2*h_dsp-h_c w-2*h_dsp;
  macro ProtoPath+"SetEnam.mac" "Фасадная стойка" ;
  macro ProtoPath+"SetKrom.mac" ЦвКром ТипКром ТипКром ТипКром ТипКром ;
  macro ProtoPath+"SetFix.mac"  Крепеж Крепеж 0 0 ;
  macro ProtoPath+"MakePan.mac" iif(ПравВар==0,ШирГлух,w-ШирГлух) d-ШирСтяж h_c+h_dsp
                                h-h_c-2*h_dsp ШирСтяж 11 ;
  macro ProtoPath+"SetECod.mac" "1105" ;
  macro ProtoPath+"MakePan.mac" iif(ПравВар==0,ШирГлух,w-ШирГлух)-h_dsp d-ШирСтяж h_c+h_dsp
                                h-h_c-2*h_dsp ШирСтяж 11 ;
}
else  //-- Построение стыкового упора
{
  //-- Построение упорной стенки
  macro ProtoPath+"SetMat.mac" PrMater;
  macro ProtoPath+"SetECod.mac" "1103" ;
  // macro ProtoPath+"SetKCod.mac" "УПТН" h-h_c StopWidth;
  macro ProtoPath+"SetEnam.mac" "Упорная стенка" ;
  macro ProtoPath+"SetKrom.mac" ЦвКром ТипКром ТипКром ТипКром ТипКром ;
  macro ProtoPath+"SetFix.mac"  0 0 0 0 ;
  macro ProtoPath+"MakePan.mac" iif(ПравВар==0,ШирГлух-StopWidth+StopShift+h_dsp,w-ШирГлух-StopShift-h_dsp) d h_c h-h_c StopWidth 14;
  //-- Построение упорной стойки
  macro ProtoPath+"SetECod.mac" "1104" ;
  // macro ProtoPath+"SetKCod.mac" "УПСТ" h-h_c StopDepth;
  macro ProtoPath+"SetEnam.mac" "Упорная стойка" ;
  macro ProtoPath+"SetKrom.mac" ЦвКром ТипКром ТипКром 0 ТипКром ;
  macro ProtoPath+"SetFix.mac"  0 0 Крепеж 0 ;
  macro ProtoPath+"MakePan.mac" iif(ПравВар==0,ШирГлух,w-ШирГлух-h_dsp) d+h_dsp h_c
                                h-h_c StopDepth 11 ;
}
//-----------------------------------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0) { ОтстСз=0; }		    //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		    //-- Накладной для мойки
if (ТипЗСтен==0) { ОтстСз=h_dvp; }	  //-- Утопленный
if (ТипЗСтен==2) { ОтстСз=0; }		    //-- Нет
if (ТипЗСтен==3) { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	  //-- Жесткая планка
XU=0;
YU=0;
ZU=0;
if (NeedCorner>0)	//-- если есть угловая планка
{
	NameU=priceinfo(NeedCorner,"MatName","",1);	//-- имя уголка
	XU=priceinfo(NeedCorner,"Width",45,1);			//-- ширина уголка
	YU=priceinfo(NeedCorner,"Dept",45,1);				//-- глубина уголка
	ZU=priceinfo(NeedCorner,"Height",720,1);		//-- высота уголка
	if (ZU>h-h_c)
	{
	  XU=0;
	  YU=0;
		ZU=0;
	}
	else
	{
	  macro ProtoPath+"SetEnam.mac" NameU ;
		accessory "040000" NeedCorner 0 yes ; 
		if (ПравВар==1)
		{
			rotate last 1 done 2points 0,0,0 0,0,1 90 nocopy;
		} 
		move last 1 done 
		0+iif(ПравВар==1,w-3*h_dsp-ШирГлух-iif(IsStop==0,0,StopShift+h_dsp)+XU+2*D_DxN,ШирГлух+iif(IsStop==0,0,StopShift+h_dsp)) d h_c;
	}
}
//-----------------------------------------------------------------------------
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
macro ProtoPath+"SetFasPlas.mac" D_DxN D_DxN D_DzN D_DzN;
macro ProtoPath+"SetBoxF.mac" 0 0 0 0;
macro ProtoPath+"MakeNiche.mac" h_dsp+iif(ПравВар==1,0,ШирГлух+iif(IsStop==0,0,StopShift+h_dsp))+iif(ПравВар==1,0,XU) ОтстСз h_c+h_dsp
    w-2*h_dsp-ШирГлух-iif(IsStop==0,0,StopShift+h_dsp)-XU //-- Ширина
    d-ОтстСз  //-- Глубина
    h-h_c-2*h_dsp //-- Высота
    0			     //-- Тип заполнения
    0          //-- Количество полок
    РисФас     //-- Рисунок фасада
    ЗаглПол    //-- Заглубление полок
    PrMater    //-- Материал корпуса
    ФасМат     //-- Материал фасада
    0          //-- Материал дна ящика
    0          //-- Тип ящика
    ФасВсМат   //-- Материал  вставки в фасад
    СторОткр   //-- Открывание двери
    ТипРуч     //-- Тип ручки
    ВысотРуч   //-- Высота установки ручки
    ПовРуч     //-- Поворот ручки
    Rpl_r	     //-- Расположение ручки
    ДвеДвери   //-- Двойная дверь
    ОткрДверь  //-- Открытая дверь
    СдвРуч     //-- Сдвиг ручки
    Петля      //-- Тип петли
    Накладн    //-- Фасад накладной
    ЦвКром     //-- Цвет кромки
    ТипКром    //-- Тип кромки
    Крепеж     //-- Крепеж корпуса
    0          //-- Тип двери
;
if (КолПЯ<=0)
{
  goto end;
}
macro ProtoPath+"MakeNicheCor.mac" h_dsp ОтстСз h_c+h_dsp
    w-2*h_dsp,      //-- Ширина
    d-ОтстСз        //-- Глубина
    h-h_c-2*h_dsp,  //-- Высота
    w-2*h_dsp,      //-- Размер меньшей стороны
    AngType,        //-- Тип угла
    DimX,           //-- Фаска Х/ Радиус
    DimY,           //-- Фаска Y
    ПравВар, 	      //-- Правая ориентация
    КолПЯ,          //-- Количество полок или ящиков
    0,              //-- Рисунок фасада
    0,    	        //-- Угол поворота двери
    ЗаглПол,	      //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    ФасМат,         //-- Материал фасада
    ФасВсМат,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    Rpl_r		        //-- Расположение ручки
    ДвеДвери,       //-- Двойная дверь
    ОткрДверь,      //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    Петля,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,				  //-- Тип крепежа
    0 	            //-- Тип двери
;
end:
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;

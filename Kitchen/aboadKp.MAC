//-- Создание объекта - стол клиновой
global MacroSw; //-- Переключатель режимов работы 0 - в пар.макро 1 - разбор.
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов
global LocalPath;		//-- Путь к папке с макропрограммами локальной библиотеке прототипов
ProtoPath=GetProtoMac("Shkaf.ptl");
LocalPath=GetProtoMac("Kitchen.ptl");
MacroSw=0;

defarr error[10];
NULLOUT=initarray(error,"");

PrMater=DbVar("PrMater",0); //-- Материал стола
w=DbVar("Шир",330);			    //-- Ширина
d=DbVar("Глуб",320);        //-- Глубина
Угол=DbVar("Угол",60);      //-- Угол
h=DbVar("Выс",822);			    //-- Высота
h_c=DbVar("ВысЦок",100);    //-- Высота цоколя
//--------------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",DbVar("HDSP",16));
ТипКонт=1;   //-- Прямой
Верхний=0;   //-- Нижний
ObjTyp=0;    //-- Мебельный
PlcTyp=0;    //-- Нижний
BiblNum=129; //-- В библиотеке ID=129
if (Угол>0)
{
  FurnType="100009";
}
else
{
  FurnType="100006";
}
//--------------------------------------------------------
Ширина=w-2*h_dsp;        //-- Внутренняя ширина
if (Угол>0)
{
  Правый=1;                          //-- Правая ориентация
  DD=Ширина*tan(radian(Угол));       //-- Изменение глубины
  dL=d;	   	                         //-- Глубина справа
  dR=d-DD;		                       //-- Глубина слева
  if (dR<h_dsp)
  {
    error[1]="При введенной величине угла параметры корпуса некорректны.";
    error[2]="Величина угла такова, что правая стойка не может быть построена .";
    error[3]=" ";
    error[4]="Убедитесь, что введено правильное значение угла."
    error[5]="Проверьте, достаточна ли ширина корпуса для построения клинового стола."
    error[6]="Уменьшите ширину или увеличьте значение угла клинового стола."
    macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 6 error;
    cancel;
  }
}
if (Угол<0)
{
  Правый=0;                          //-- Левая ориентация
  DD=Ширина*tan(radian(-Угол));      //-- Изменение глубины
  dL=d-DD;	   	                     //-- Глубина справа
	dR=d;		                           //-- Глубина слева
  if (dL<h_dsp)
  {
    error[1]="При введенной величине угла параметры корпуса некорректны.";
    error[2]="Величина угла такова, что левая стойка не может быть построена .";
    error[3]=" ";
    error[4]="Убедитесь, что введено правильное значение угла."
    error[5]="Проверьте, достаточна ли ширина корпуса для построения клинового стола."
    error[6]="Уменьшите ширину или увеличьте значение угла клинового стола."
    macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 6 error;
    cancel;
  }
}
if (Угол==0)
{
  DD=0;                              //-- Изменение глубины
  dR=d;	   	                         //-- Глубина справа
  dL=d;		                           //-- Глубина слева
}
d=min(dR,dL);
//---------------------------------------------------------------

Macro ProtoPath+"BegUnit.mac" "" ;
macro ProtoPath+"ScrBoxGet.mac" 0; //-- Создание Scratch атрибута ящика
macro LocalPath+"aboadK.mac" ;

//-- Ставим размеры
macro ProtoPath+"aDimLine.mac" w  dl h  ТипКонт Верхний;
//-- Линии контакта
macro ProtoPath+"aContLine.mac" w dl d h 0 ТипКонт Верхний;
#Boad group all done;
macro ProtoPath+"SetEnam.mac" "Стол клиновой";
macro ProtoPath+"EndUnit.mac" w d h ;
//-- Присваиваем атрибуты
macro ProtoPath+"FurnAttr.mac" ObjTyp PlcTyp BiblNum FurnType;
macro ProtoPath+"aCutAtr.mac" (Угол<0) 1 Ширина DD;
macro ProtoPath+"OldArtic.mac" ;
macro ProtoPath+"Scrcopatr.mac" ; //-- Запись в объект Scratch атрибута
exit;

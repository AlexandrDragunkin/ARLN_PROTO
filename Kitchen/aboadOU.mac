//-- Стол угловой открытый
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов

defarr x[6],y[6],rad[5];

PrMater=DbVar("КорпМат",0);   //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);     //-- Материал задней стенки
w=DbVar("Шир",330);			      //-- Ширина
d=DbVar("Глуб",320);			    //-- Глубина
h=DbVar("Выс",822);			      //-- Высота
h_c=DbVar("ВысЦок",100);		  //-- Высота цоколя
ТипУгла=DbVar("ТипУгла",1);   //-- Тип угла
DimX=DbVar("РазмХ",100);      //-- Размер по оси X
DimY=DbVar("РазмУ",100);      //-- Размер по оси Y
ЦвКром=DbVar("ЦвКромки",0);   //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0); //-- Тип кромки
КолПЯ=DbVar("КолПЯ",0);       //-- Количество полок
Правый=DbVar("Правый",0);     //-- Ориентация
Крепеж=DbVar("Крепеж",0);   	//-- Тип крепежа
Noga=DbVar("Ножка",0);		    //-- Тип ножки
Dno=DbVar("Dno",0);		        //-- Тип дна
FixShelfRem=dbvar("FixShelfRem",0); //-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0); //-- Величина подреза съемной полки
Заполн=DbVar("Заполн",0);					  //-- Тип заполнения
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4);   //-- Толщина материала задней стенки
ЗСтВрезка=8;                      //-- Врезка задней стенки
СдвРуч=20;                        //-- Сдвиг ручки
ПовРуч=0;                         //-- Угол поворота ручки
ВысотРуч=0;                       //-- Высота установки ручки
dum=120;                          //-- Размер "уголка" задней стенки под мойку
offset=14;                        //-- Нахлест накладной стенки на корпус
dst=60;                           //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                         //-- Наличие цоколя
Shiftcok=0;                       //-- Сдвиг цоколя
MinDepth=0;                       //-- Боковая стойка отсутствует
ТипЗСтен=0;                       //-- Тип задней стенки - Накладная
УстЗСтен=0;                       //-- Глубина установки задн. стенки
КрепЗС=Крепеж;           	        //-- Тип крепежа задней стенки
ЗаглПол=0;                	      //-- Заглубление полок
LongPost=0;                       //-- Опорные стойки
FasCok=0;                         //-- Размер фасок под цоколь
ForOpen=1;                        //-- Для открытого
//-------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetTypeFill.mac" Заполн;
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
//-- Построение ножек
if ((ТипУгла!=0&&(DimX>w-h_dsp||DimY>d-h_dsp)||((ТипУгла==2||ТипУгла==4)&&DimX>d-h_dsp))&&Правый==1)
{
  DimX=int(min(w,d)-h_dsp-1);
  DimY=DimX;
}
if (ТипУгла==0)
{
  nn=4;
  DimY=0;
  DimX=0;
  x[1]=w;
  y[1]=0;
  rad[1]=0;
  x[2]=w;
  y[2]=d;
  rad[2]=0;
  x[3]=0;
  y[3]=d;
  rad[3]=0;
  x[4]=0;
  y[4]=0;
}
else
{
  nn=5;
  x[1]=w;
  y[1]=0;
  x[2]=w;
  y[3]=d;
  x[4]=0
  if (ТипУгла==2||ТипУгла==4||ТипУгла==5)
  {
    DimY=DimX;
  }
  if (ТипУгла==3)
  {
    nn=6;
  }
  if (Правый==0)
  {
    y[2]=d-DimY;
    if (ТипУгла==2||ТипУгла==4||ТипУгла==5)
    {
      rad[2]=DimX*iif(ТипУгла==2,1,-1);
      rad[3]=0;
    }
    x[3]=w-DimX
    y[4]=d;
    if (ТипУгла==3)
    {
      rad[2]=0;
      rad[3]=0;
      x[3]=w-DimX;
      y[3]=d-DimY;
      x[4]=w-DimX;
      y[4]=d;
      x[5]=0;
      y[5]=d;
    }
  }
  if (Правый==1)
  {
    y[2]=d;
    if (ТипУгла==2||ТипУгла==4||ТипУгла==5)
    {
      rad[2]=0;
      rad[3]=DimX*iif(ТипУгла==2,1,-1);
    }
    x[3]=DimX;
    y[4]=d-DimY;
    if (ТипУгла==3)
    {
      rad[2]=0;
      rad[3]=0;
      x[3]=DimX;
      y[3]=d;
      x[4]=DimX;
      y[4]=d-DimY;
      x[5]=0;
      y[5]=d-DimY;
    }
  }
  rad[nn-1]=0;
  x[nn]=0;
  y[nn]=0;
//  if ((w-DimX)<=50)
//  {
//    x[3]=x[4];
//    y[3]=y[4];
//    if (ТипУгла==2||ТипУгла==4)
//    {
//      rad[2]=DimX;
//    }
//    else
//    {
//      rad[2]=0;
//    }
//    rad[3]=0;
//    x[4]=x[5];
//    y[4]=y[5];
//    nn=4;
//  }
}
macro ProtoPath+"aStBase.mac" Noga,nn,x,y,rad,50;
macro ProtoPath+"SetCorpsCorButts.mac" 0, 0;
//-------------------------------------------------
macro ProtoPath+"SetCorpsCor.mac" 0, 1, 0;
macro ProtoPath+"MakeCorpsCor.mac" 0 0 0
    w,                    //-- Ширина
    d,                    //-- Глубина
    h,                    //-- Высота
    h_c,                  //-- Высота цоколя
    MinDepth,             //-- Размер меньшей стороны
    ТипУгла,              //-- Тип угла
    DimX+h_dsp,           //-- Фаска Х/ Радиус
    DimY+h_dsp,           //-- Фаска Y
    Правый,               //-- Правая ориентация
    PrMater,        	    //-- Материал корпуса
    ЗСтМат,               //-- Материал задней стенки
    ЦвКром,               //-- Цвет кромки
    ТипКром,              //-- Тип кромки
    ТипЗСтен,             //-- Тип задней стенки
    УстЗСтен,             //-- Глубина установки задней стенки
    ЗСтВрезка,            //-- Врезка\Нахлест задней стенки
    Крепеж,               //-- Тип крепежа корпуса
    КрепЗС,      	        //-- Тип крепежа задней стенки
    Dno,                  //-- Накладное дно
    Dum,                  //-- Размер "уголка" задней стенки под мойку
    Dst,                  //-- Ширина дополнительной планки
    HasCok,               //-- Наличие цоколя
    Shiftcok              //-- Сдвиг цоколя
    LongPost              //-- Опорные стойки
    FasCok                //-- Размер фасок под цоколь
    ForOpen;              // Для открытого
;
//---------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0)  { ОтстСз=0; }		      //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		      //-- Накладной для мойки
if (ТипЗСтен==0)  { ОтстСз=h_dvp; }	    //-- Утопленный
if (ТипЗСтен==2)  { ОтстСз=0; }		      //-- Нет
if (ТипЗСтен==3)  { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	    //-- Жесткая планка

macro ProtoPath+"MakeNicheCor.mac"  iif(MinDepth>0||Правый==0,h_dsp,0)
                                    ОтстСз h_c+h_dsp
    w-iif(MinDepth<=0,1,2)*h_dsp,   //-- Ширина
    d-ОтстСз        //-- Глубина
    h-h_c-2*h_dsp,  //-- Высота
    MinDepth,       //-- Размер меньшей стороны
    ТипУгла,        //-- Тип угла
    DimX,           //-- Фаска Х/ Радиус
    DimY,           //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    КолПЯ,          //-- Количество полок или ящиков
    0,              //-- Рисунок фасада
    0,      	      //-- Угол поворота двери
    ЗаглПол,	      //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    0,              //-- Материал фасада
    0,              //-- Материал вставки в фасад
    0,              //-- Открывание двери
    0,              //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    0,              //-- Положение ручки
    0,              //-- Двойная дверь
    0,              //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    0,              //-- Тип петли
    1,              //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,				  //-- Тип крепежа
    0 	            //-- Тип двери
;
//-------------------------------------------------
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;

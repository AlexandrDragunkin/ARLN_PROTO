//-- Стол пятиугольный с гнутым фасадом
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов

defarr x[8],y[8],rad[8];
global D_DxU D_DzU D_DxN D_DzN D_DxD;

PrMater=DbVar("КорпМат",0);     //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);       //-- Материал задней стенки
w=DbVar("Шир",330);			        //-- Ширина
d=DbVar("Глуб",320);			      //-- Глубина
h=DbVar("Выс",822);			        //-- Высота
h_c=DbVar("ВысЦок",100);		    //-- Высота цоколя
MinDepth=DbVar("MinDepth",100);	//-- Ширина глухой секции
MinDepthX=DbVar("MinDepthX",100);//-- Глубина закрытой части
Радиус=DbVar("Радиус",100);		  //-- Фаска Х/ Радиус
DimY=DbVar("DimY",100);		      //-- Фаска Y
ЦвКром=DbVar("ЦвКромки",0);     //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   //-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	  //-- Тип крепежа
КрепЗС=DbVar("КрепЗС",0);   	  //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);   //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);   //-- Глубина установки задней стенки
ЗаглПол=DbVar("ЗаглПол",0);     //-- Заглубление полок
ФасМат=DbVar("ФасМат",0);       //-- Материал фасада
ФасВсМат=DbVar("ФасВсМат",0);   //-- Материал вставки в фасад
СторОткр=DbVar("СторОткр",0);   //-- Открывание двери
ТипРуч=DbVar("ТипРуч",0);       //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);   //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);       //-- Поворот ручки
ДвеДвери=DbVar("ДвеДвери",0);   //-- Двойная дверь
ОткрДверь=DbVar("ОткрДверь",0); //-- Открытая дверь
Петля=DbVar("Петля",0);         //-- Тип петли
КолПЯ=DbVar("КолПЯ",0);         //-- Количество полок
РисФас=DbVar("РисФас",0);       //-- Рисунок фасада
Noga=DbVar("Ножка",0);		      //-- Тип ножки
Dno=DbVar("Dno",0);             //-- Тип дна
СдвРуч=DbVar("СдвРуч",40);      //-- Сдвиг ручки
Rpl_r=dbvar("Rpl_r",0);		      //-- Расположение ручки
HasDuct=DbVar("HasDuct",0);     //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0); //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0); //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0); //-- Глубина короба
WallAngle=DbVar("WallAngle",90);//-- Пристенный угол
Widsth=DbVar("Widsth",100);     //-- Ширина стяжки
IsTop=DbVar("IsTop",1);         //-- Наличие крышки
WallChamfer=DbVar("WallChamfer",0);//-- Размер пристенной фаски
FixShelfRem=dbvar("FixShelfRem",0);//-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0); //-- Величина подреза съемной полки
Заполн=DbVar("Заполн",0);					  //-- Тип заполнения
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4); //-- Толщина материала задней стенки
h_fas=priceinfo(ФасМат,"Thickness",16); //-- Толщина материала фасада
ЗСтВрезка=8;                  //-- Врезка задней стенки
dum=120;                      //-- Размер "уголка" задней стенки под мойку
offset=14;                    //-- Нахлест накладной стенки на корпус
dst=60;                       //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                     //-- Наличие цоколя
Shiftcok=0;                   //-- Сдвиг цоколя
Правый=0;                     //-- Левая ориентация
Накладн=1;                    //-- Фасад накладной
LongPost=0;                   //-- Опорные стойки
FasCok=0;                     //-- Размер фасок под цоколь
ForOpen=0;                    //-- Для открытого
ConfDuct=0;                   //-- Конфигурация выреза под короб по умолчанию
Shift=50;                     //-- Сдвиг опор от края корпуса
if (WallAngle!=90)
{
  putmsg("Для данного прототипа пристенный угол, отличный от 90 градусов не реализован",0);
  WallAngle=90;
}
//---------------------------------------------------
Ширина=w-2*h_dsp;             //-- Внутренняя ширина
//-- Определяем тип угла
FurnT=priceinfo(РисФас,"furntype","500102",2);
if (right(FurnT,2)=="02") //-- Фасад гнутый
{
  ТипУгла=2;              //-- Тип угла - скругление
  DimY=abs(Радиус)
  if (Радиус<0)
  {
    ТипУгла=4;              //-- Тип угла - скругление
    MinDepth=d-abs(Радиус)-h_dsp-h_fas;
    MinDepthX=w-abs(Радиус)-h_fas-h_dsp;
  }
}
else                      //-- Фасад трапецеидальный
{
  ТипУгла=1;              //-- Тип угла - фаска
}
Радиус=abs(Радиус);
if (Правый==0)              //-- Левая ориентация
{
  dR=d;	   	                //-- Глубина справа
  dL=d-DimY-Shift;		      //-- Глубина слева
}
else
{
  dR=d-DimY-Shift;	    	   //-- Глубина справа
  dL=d;		                   //-- Глубина слева
}
d=max(dl,dr);               //-- Максимальная глубина
Ang=radian(WallAngle-90);   //-- Пристенный угол
ws=(w-d*sin(Ang))/cos(Ang); //-- Пристенная ширина
ds=(d-w*sin(Ang))/cos(Ang)+h_dsp*sin(Ang)*iif(Ang>0,1,0); //-- Пристенная глубина
Радиус=Радиус/cos(Ang);
DimY=DimY/cos(Ang);
Угол=grad(atan((DimY)*sin(pi()/2-Ang)/(Радиус-(DimY)*cos(pi()/2-Ang))));
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  ds=ds-DepthDuct;
  HasDuct=0;
}
//-------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
macro ProtoPath+"SetTypeFill.mac" Заполн;.
defarr NeedLegArr[10];	//-- в массив заносятся номера точек, где не нужно устанавливать опору
wOts=70;
//-- Построение ножек
x[1]=w;
y[1]=0;
rad[1]=0;

x[2]=w;
y[2]=dl+Shift;
rad[3]=0;

x[3]=w-wOts;
y[3]=dl+Shift;
rad[3]=((Радиус)*(1-Правый))*iif(ТипУгла==2,1,iif(ТипУгла!=4,0,-1))+wOts;

x[4]=(Радиус*Правый)+(w-Радиус)*(1-Правый);
y[4]=dr-wOts;
rad[4]=0;

x[5]=(Радиус*Правый)+(w-Радиус)*(1-Правый);
y[5]=dr;
rad[5]=0;

x[6]=0;
y[6]=dr;
rad[6]=0;

if (WallChamfer==0)
{
  x[7]=0;
  y[7]=0;
  num=7;
}
else
{
  x[7]=0;
  y[7]=WallChamfer;
  x[8]=WallChamfer;
  y[8]=0;
  rad[7]=-1;
  num=8;
}
if (ConfDuct==3)
{
  y[1]=DepthDuct;
}
if (ConfDuct==2)
{
  y[7]=DepthDuct;
}
if (ConfDuct==4)
{
  if (2*Shift>ShiftDuct)
  {
    y[7]=DepthDuct;
  }
  if (w-2*Shift<ShiftDuct+WidthDuct)
  {
    y[1]=DepthDuct;
  }
}
NeedLegArr[1]=3;
NeedLegArr[2]=4;
NULLOUT=setvarinst(1,"NeedLegArrCol",10);
NULLOUT=setArrinst(1,"NeedLegArr",NeedLegArr);	//-- определяем какие опоры не нужно устанавливать
macro ProtoPath+"aStBase.mac" Noga,num,x,y,rad,Shift;
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
macro ProtoPath+"SetWallAngle.mac" WallAngle;
macro ProtoPath+"SetCorpsCor.mac" Widsth, IsTop, w-Радиус;
macro ProtoPath+"SetWallChamfer.mac" WallChamfer;
macro ProtoPath+"SetCorpsCorButts.mac" 0, 0;
//-------------------------------------------------
macro ProtoPath+"MakeCorpsCor.mac" 0 0 0
    w,                    //-- Ширина
    d,                    //-- Глубина
    h,                    //-- Высота
    h_c,                  //-- Высота цоколя
    d-Радиус,             //-- Размер меньшей стороны
    ТипУгла,              //-- Тип угла
    Радиус,   //-- Фаска Х/ Радиус
    DimY,     //-- Фаска Y
    Правый,               //-- Правая ориентация
    PrMater,        	    //-- Материал корпуса
    ЗСтМат,               //-- Материал задней стенки
    ЦвКром,               //-- Цвет кромки
    ТипКром,              //-- Тип кромки
    ТипЗСтен,             //-- Тип задней стенки
    УстЗСтен,             //-- Глубина установки задней стенки
    offset                //-- Врезка\Нахлест задней стенки
    Крепеж,               //-- Тип крепежа корпуса
    КрепЗС,      	        //-- Тип крепежа задней стенки
    Dno,                  //-- Накладное дно
    Dum,                  //-- Размер "уголка" задней стенки под мойку
    Dst,                  //-- Ширина дополнительной планки
    HasCok,               //-- Наличие цоколя
    Shiftcok              //-- Сдвиг цоколя
    LongPost              //-- Опорные стойки
    FasCok                //-- Размер фасок под цоколь
    ForOpen;              //-- Для открытого изделия
;
//-------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0) { ОтстСз=0; }		    //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		    //-- Накладной для мойки
if (ТипЗСтен==0) { ОтстСз=h_dvp; }	  //-- Утопленный
if (ТипЗСтен==2) { ОтстСз=0; }		    //-- Нет
if (ТипЗСтен==3) { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	  //-- Жесткая планка
defarr qwe[100];
furn="'230400'";
DoorKonsrt=0
kol1=npgetbywhere(2,"furntype = "+furn,"qwe");
if (kol1>0)
{
  DoorKonsrt=qwe[1];
}
if (Dno==1)
{
  q=D_DzN;
  D_DzN=h_dsp;
}
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
if (WallChamfer>0)
{
  macro ProtoPath+"SetWallChamfer.mac" WallChamfer-iif(ТипЗСтен==1,h_dvp,ОтстСз)+h_dsp*sqrt(2);
}
macro ProtoPath+"MakeNicheCorUgl.mac" iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)) ОтстСз h_c+h_dsp
    w-h_dsp-iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)),      //-- Ширина
    d-ОтстСз-h_dsp, //-- Глубина
    h-h_c-2*h_dsp,  //-- Высота
    MinDepth,       //-- Глубина меньшей стороны
    MinDepthX+h_fas,      //-- Ширина меньшей стороны
    ТипУгла,        //-- Тип угла
    iif(ТипУгла==4,-1,1)*Радиус+h_dsp,         //-- Фаска Х/ Радиус
    DimY+h_dsp,           //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    КолПЯ,          //-- Количество полок
    РисФас,         //-- Рисунок фасада
    ЗаглПол,	      //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    ФасМат,         //-- Материал фасада
    ФасВсМат,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    Rpl_r	          //-- Расположение ручки
    0,              //-- Двойная дверь
    ОткрДверь,      //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    Петля,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,					//-- Тип крепежа
    DoorKonsrt      //-- Тип двери
;
if (Dno==1)
{
  D_DzN=q;
}
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;
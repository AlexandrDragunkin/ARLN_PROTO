//-- Шкаф клиновой верхний
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов

PrMater=DbVar("КорпМат",0);       //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);         //-- Материал задней стенки
w=DbVar("Шир",330);			          //-- Ширина
d=DbVar("Глуб",320);              //-- Глубина
Угол=DbVar("Угол",60);            //-- Угол
h=DbVar("Выс",822);			          //-- Высота
ЦвКром=DbVar("ЦвКромки",0);   	  //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   	//-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	    //-- Тип крепежа
КрепЗС=DbVar("КрепЗС",0);   	    //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);     //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);     //-- Глубина установки задней стенки
КолПЯ=DbVar("КолПЯ",0);           //-- Количество полок
РисФас=DbVar("РисФас",0);         //-- Рисунок фасада
ЗаглПол=DbVar("ЗаглПол",0);       //-- Заглубление полок
ФасМат=DbVar("ФасМат",0);         //-- Материал фасада
ФасВсМат=DbVar("ФасВсМат",0);     //-- Материал вставки в фасад
СторОткр=DbVar("СторОткр",0);     //-- Открывание двери
ТипРуч=DbVar("ТипРуч",0);         //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);     //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);         //-- Поворот ручки
ДвеДвери=DbVar("ДвеДвери",0);     //-- Двойная дверь
ОткрДверь=DbVar("ОткрДверь",0);   //-- Открытая дверь
Петля=DbVar("Петля",0);           //-- Тип петли
СдвРуч=DbVar("СдвРуч",40);        //-- Сдвиг ручки
Rpl_r=dbvar("Rpl_r",0);		        //-- Расположение ручки
HasDuct=DbVar("HasDuct",0);       //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0);   //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0);   //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0);   //-- Глубина короба
FixShelfRem=dbvar("FixShelfRem",0);//-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0); //-- Величина подреза съемной полки
Заполн=DbVar("Заполн",0);					  //-- Тип заполнения
Naves=dbvar("Naves",0);						 //-- Тип навески
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4); //-- Толщина материала задней стенки
ЗСтВрезка=8;                        //-- Врезка задней стенки
ТипУгла=1;                          //-- Тип угла - фаска
Правый=1;       //-- Правая ориентация
Накладн=1;      //-- Фасад накладной
dum=120;        //-- Размер "уголка" задней стенки под мойку
offset=14;      //-- Нахлест накладной стенки на корпус
dst=60;         //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;       //-- Наличие цоколя
Shiftcok=0;     //-- Сдвиг цоколя
Накладн=1;      //-- Фасад накладной
Dno=0;          //-- Ненакладное дно
LongPost=0;     //-- Опорные стойки
FasCok=0;       //-- Размер фасок под цоколь
ForOpen=0;      //-- Для открытого
ConfDuct=0;     //-- Конфигурация выреза под короб по умолчанию
Shift=50;       //-- Сдвиг опор от края корпуса
Butts=1;	//-- Наличие скоса на торцах корпуса (1-есть,0-нет)
//---------------------------------------------------
Ширина=w-2*h_dsp;                    //-- Внутренняя ширина
if (Угол>0)
{
  Правый=1;                          //-- Правая ориентация
  DD=Ширина*tan(radian(Угол));       //-- Изменение глубины
  dL=d;	   	                         //-- Глубина справа
  dR=d-DD;		                       //-- Глубина слева
}
if (Угол<0)
{
  Правый=0;                          //-- Левая ориентация
  DD=Ширина*tan(radian(-Угол));      //-- Изменение глубины
  dL=d-DD;	   	                     //-- Глубина справа
	dR=d;		                           //-- Глубина слева
}
if (Угол==0)
{
  DD=0;                              //-- Изменение глубины
  dR=d;	   	                         //-- Глубина справа
  dL=d;		                           //-- Глубина слева
}
MinDepth=min(dR,dL);
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  d=d-DepthDuct;
  HasDuct=0;
}
if (Butts==1)
{
 ChamfAng=iif((d-MinDepth)!=0,90-grad(atan(w/(d-MinDepth))),0); //-- Угол скоса стоек
 ChamfX=iif((d-MinDepth)!=0,h_dsp*tan(radian(ChamfAng)),0);  //-- Ширина скоса стоек
}
else
{
 ChamfAng=0;
 ChamfX=0;
}

//-------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetTypeFill.mac" Заполн;
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
macro ProtoPath+"SetCorpsCor.mac" 0, 1, 0;
macro ProtoPath+"SetNaves.mac" Naves;
macro ProtoPath+"SetCorpsCorButts.mac" Butts, Butts;
//-------------------------------------------------
macro ProtoPath+"MakeCorpsCor.mac" 0 0 0
    w,                    //-- Ширина
    d,                    //-- Глубина
    h,                    //-- Высота
    0,                    //-- Высота цоколя
    MinDepth,             //-- Размер меньшей стороны
    ТипУгла,              //-- Тип угла
    w-iif(Butts==0,h_dsp,0),                    //-- Фаска Х/ Радиус
    iif(Dno==1,dd,dd+h_dsp),//-- Фаска Y
    Правый,               //-- Правая ориентация
    PrMater,        	    //-- Материал корпуса
    ЗСтМат,               //-- Материал задней стенки
    ЦвКром,               //-- Цвет кромки
    ТипКром,              //-- Тип кромки
    ТипЗСтен,             //-- Тип задней стенки
    УстЗСтен,             //-- Глубина установки задней стенки
    offset                //-- Врезка\Нахлест задней стенки
    Крепеж,               //-- Тип крепежа корпуса
    КрепЗС,      	        //-- Тип крепежа задней стенки
    Dno,                  //-- Накладное дно
    Dum,                  //-- Размер "уголка" задней стенки под мойку
    Dst,                  //-- Ширина дополнительной планки
    HasCok,               //-- Наличие цоколя
    Shiftcok              //-- Сдвиг цоколя
    LongPost              //-- Опорные стойки
    FasCok                //-- Размер фасок под цоколь
    ForOpen;              //-- Для открытого
;
//-------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0) { ОтстСз=0; }		    //-- Накладной
if (ТипЗСтен==0) { ОтстСз=h_dvp; }	  //-- Утопленный
if (ТипЗСтен==-1) { ОтстСз=0; }		    //-- Накладной для мойки
if (ТипЗСтен==2) { ОтстСз=0; }		    //-- Нет
if (ТипЗСтен==3) { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	  //-- Жесткая планка
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
macro ProtoPath+"MakeNicheCor.mac" h_dsp ОтстСз h_dsp
    w-2*h_dsp,      //-- Ширина
    d-ОтстСз-ChamfX //-- Глубина
    h-2*h_dsp,      //-- Высота
    MinDepth,       //-- Размер меньшей стороны
    ТипУгла,        //-- Тип угла
    w-2*h_dsp,      //-- Фаска Х/ Радиус
    dd,             //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    КолПЯ,          //-- Количество полок
    РисФас,         //-- Рисунок фасада
    Угол,    	      //-- Угол поворота двери
    ЗаглПол,	      //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    ФасМат,         //-- Материал фасада
    ФасВсМат,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    Rpl_r		        //-- Расположение ручки
    ДвеДвери,       //-- Двойная дверь
    ОткрДверь,      //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    Петля,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,					//-- Тип крепежа
    0 	            //-- Тип двери
;
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;

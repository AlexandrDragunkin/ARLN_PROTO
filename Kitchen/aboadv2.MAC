//-- Шкаф двусекционный верхний
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов

PrMater=DbVar("КорпМат",0);     //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);       //-- Материал задней стенки
w=DbVar("Шир",330);			        //-- Ширина
d=DbVar("Глуб",320);			      //-- Глубина
h=DbVar("Выс",822);			        //-- Высота
h_niz=DbVar("ВысНиж",822);		  //-- Высота нижней секции
FixPlan=DbVar("FixPlan",0);	    //-- Наличие крепежной планки
ЦвКром=DbVar("ЦвКромки",0);     //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   //-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	  //-- Тип крепежа
КрепЗС=DbVar("КрепЗС",0);   	  //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);   //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);   //-- Глубина установки задней стенки
ЗаглПол=DbVar("ЗаглПол",0);     //-- Заглубление полок
ФасМат=DbVar("ФасМат",0);       //-- Материал фасада
ДноМат=DbVar("ДноМат",0);       //-- Материал дна ящика
ТипЯщ=DbVar("ТипЯщ",0);         //-- Тип ящика
ФасВсМат=DbVar("ФасВсМат",0);   //-- Материал вставки в фасад
СторОткр=DbVar("СторОткр",0);   //-- Открывание двери
ТипРуч=DbVar("ТипРуч",0);       //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);   //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);       //-- Поворот ручки
ДвеДвери=DbVar("ДвеДвери",0);   //-- Двойная дверь
ОткрДверь=DbVar("ОткрДверь",0);      //-- Открытая дверь или выдвинутые ящики
Петля=DbVar("Петля",0);         //-- Тип петли
ЕстьСушка=DbVar("Сушилка",0);   //-- Отсутствие дна (под сушилку)
//-- Нижняя секция
Заполн=DbVar("Заполн",0);       //-- Тип заполнения нижней секции
КолПЯ=DbVar("КолПЯ",0);         //-- Количество полок или ящиков нижней секции
РисФас=DbVar("РисФас",0);       //-- Рисунок фасада нижней секции
ФасМат=DbVar("ФасМат",0);       //-- Материал фасада нижней сецкии
Rpl_r=dbvar("Rpl_r",0);		      //-- Расположение ручки нижней секции
//--Для верхней секции
Заполн2=DbVar("Заполн2",0);     //-- Тип заполнения
КолПЯ2=DbVar("КолПЯ2",0);       //-- Количество полок или ящиков
РисФас2=DbVar("РисФас2",0);     //-- Рисунок фасада
ФасМатВ=DbVar("ФасМатВ",0);     //-- Материал фасада верхней сецкии
СдвРуч=DbVar("СдвРуч",40);      //-- Сдвиг ручки
Rt=dbvar("Rt",0);	           	  //-- Расположение ручки верхней секции
HasDuct=DbVar("HasDuct",0);          //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0);      //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0);      //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0);      //-- Глубина короба
FixShelfRem=dbvar("FixShelfRem",0);	//-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0);	//-- Величина подреза съемной полки
Naves=dbvar("Naves",0);						 //-- Тип навески
LiftType=dbvar("LiftType",0);			 //-- Тип подъемника нижней секции
LiftType2=dbvar("LiftType2",0);		 //-- Тип подъемника верхней секции
DryingType=dbvar("DryingType",0);	 //-- Тип посудосушителя
//---------------------------------------------------
//-- Константы
D_DxN=DbVar("D_DxN",1.5);	//-- Зазор по ширине для накладных дверей
D_DzN=DbVar("D_DzN",2);		//-- Зазор по высоте для накладных дверей
D_DxD=DbVar("D_DxD",2);		//-- Зазор между двойными дверьми
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4);   //-- Толщина материала задней стенки
ЗСтВрезка=8;                      //-- Врезка задней стенки
Накладн=1;                        //-- Фасад накладной
ЕстьКрышка=1;                     //-- Наличие крышки
dum=120;                          //-- Размер "уголка" задней стенки под мойку
offset=14;                        //-- Нахлест накладной стенки на корпус
dst=60;                           //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                         //-- Наличие цоколя
Shiftcok=0;                       //-- Сдвиг цоколя
Dno=0;                            //-- Дно ненакладное
LongPost=0;                       //-- Опорные стойки
FasCok=0;                         //-- Размер фасок под цоколь
ШирСтяж=0;                        //-- Ширина планок. На навесных шкафах их все равно нет
ConfDuct=0;                       //-- Конфигурация выреза под короб по умолчанию

//--БРАВО ПЛЮС
HardWallOpen=dbvar("HardWallOpen",0);	//-- Утопленная стенка в открытой секции
if (HardWallOpen==1)
{
 if (РисФас==0&&РисФас2>0)	//--пустая нижняя секция
 {
		ТипЗСтен=2;
		//--строим накладную стенку на верхней секции
		macro ProtoPath+"SetMat.mac" ЗСтМат;
		macro ProtoPath+"SetCuts.mac" -offset -offset -offset -offset;
		macro ProtoPath+"SetFix.mac"  0 0 0 0 ;
		macro ProtoPath+"MakePan.mac"	h_dsp 0 2*h_dsp+h_niz	h-h_niz-3*h_dsp	w-2*h_dsp	13;
		macro ProtoPath+"SetCuts.mac" 0 0 0 0;
		//--строим утопленную стенку в нижней секции
		macro ProtoPath+"SetKrom.mac" 0 0 0 0 0 ;
		macro ProtoPath+"SetMat.mac" PrMater;
		macro ProtoPath+"SetFix.mac"  Крепеж Крепеж Крепеж Крепеж ;
    macro ProtoPath+"MakePan.mac"	h_dsp 0 h_dsp	h_niz	w-2*h_dsp 14;
 }
 if (РисФас2==0&&РисФас>0)	//--пустая верхняя секция
 {
		ТипЗСтен=2;
		//--строим накладную стенку на нижней секции
		macro ProtoPath+"SetMat.mac" ЗСтМат;
		macro ProtoPath+"SetCuts.mac" -offset -offset -offset -offset;
		macro ProtoPath+"SetFix.mac"  0 0 0 0 ;
		macro ProtoPath+"MakePan.mac"	h_dsp 0 h_dsp	h_niz	w-2*h_dsp 13;
		macro ProtoPath+"SetCuts.mac" 0 0 0 0;
		//--строим утопленную стенку в верхней секции
		macro ProtoPath+"SetKrom.mac" 0 0 0 0 0 ;
		macro ProtoPath+"SetMat.mac" PrMater;
		macro ProtoPath+"SetFix.mac"  Крепеж Крепеж Крепеж Крепеж ;
    macro ProtoPath+"MakePan.mac"	h_dsp 0 2*h_dsp+h_niz	h-h_niz-3*h_dsp	w-2*h_dsp	14;
 }
}
//--БРАВО

//---------------------------------------------------
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  d=d-DepthDuct;
  HasDuct=0;
}
//---------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
macro ProtoPath+"SetNaves.mac" Naves;
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
//-------------------------------------------------
macro ProtoPath+"MakeCorps.mac" 0 0 0
    w           //-- Ширина
    d           //-- Глубина
    h           //-- Высота
    0           //-- Высота цоколя
    PrMater     //-- Материал корпуса
    ЗСтМат      //-- Материал задней стенки
    ШирСтяж     //-- Ширина стяжки
    ТипЗСтен    //-- Тип задней стенки
    УстЗСтен    //-- Глубина установки задней стенки
    offset      //-- Врезка\Нахлест задней стенки
    ЕстьКрышка  //-- Наличие крышки
    ЕстьСушка   //-- Наличие сушилки
    ЦвКром      //-- Цвет кромки
    ТипКром     //-- Тип кромки
    Крепеж      //-- Крепеж корпуса
    КрепЗС   	  //-- Тип крепежа задней стенки
    Dno         //-- Накладное дно
    Dum         //-- Размер "уголка" задней стенки под мойку
    Dst         //-- Ширина дополнительной планки
    HasCok      //-- Наличие цоколя
    Shiftcok    //-- Сдвиг цоколя
    LongPost    //-- Опорные стойки
    FasCok      //-- Размер фасок под цоколь
    FixPlan     //-- Наличие крепежной планки
;
//-------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0) { ОтстСз=0; }		    //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		    //-- Накладной для мойки
if (ТипЗСтен==0) { ОтстСз=h_dvp; }	  //-- Утопленный
if (ТипЗСтен==2) { ОтстСз=0; }		    //-- Нет
if (ТипЗСтен==3) { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	  //-- Жесткая планка
//---------------------------------------------------------------------
//-- Полка между секциями
if (Заполн2!=1)     //-- Если сверху полка
{
  macro ProtoPath+"SetMat.mac" PrMater;
  macro ProtoPath+"SetECod.mac" "1107";
  // macro ProtoPath+"SetKCod.mac" "ПОЛК" w-2*h_dsp d-ОтстСз;
  macro ProtoPath+"SetEnam.mac" "Полка межсекционная";
  macro ProtoPath+"SetFix.mac"  Крепеж Крепеж 0 0;
  macro ProtoPath+"SetBand.mac" 0 0 0 ТипКром;
  macro ProtoPath+"MakePan.mac" h_dsp ОтстСз h_dsp+h_niz w-2*h_dsp d-ОтстСз 12;
  if (HasDuct!=0) //-- Если нужен вырез, добавляем его
  {
    objident last 1 Pan;
    #Pat rectangle 3points ShiftDuct-h_dsp,0,0 ShiftDuct+WidthDuct+h_dsp,0,0 ShiftDuct+WidthDuct+h_dsp,DepthDuct+h_dsp,0;
    macro ProtoPath+"PutCutr.mac" Pan, Pat, 1, 0, 5;
    delete Pat done;
  }
}
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
//---------------------------------------------------------------------
//-- Заполнение нижней секции
//-- Устанавливаем зазоры для фасадов
macro ProtoPath+"SetFasPlas.mac" D_DxN D_DxN iif(РисФас2==0,D_DzN,(h_dsp+D_DxD)/2) D_DzN;
macro ProtoPath+"SetBoxF.mac" 0 0 iif(РисФас2>0,-(h_dsp/2)+D_DxD/2,0) 0;
macro ProtoPath+"SetLift.mac" LiftType LiftType;
macro ProtoPath+"SetDrying.mac" DryingType;
macro ProtoPath+"MakeNiche.mac" h_dsp ОтстСз h_dsp
    w-2*h_dsp  //-- Ширина
    d-ОтстСз   //-- Глубина
    h_niz      //-- Высота
    Заполн     //-- Тип заполнения
    КолПЯ      //-- Количество полок или ящиков
    РисФас     //-- Рисунок фасада
    ЗаглПол    //-- Заглубление полок
    PrMater    //-- Материал корпуса
    ФасМат     //-- Материал фасада
    ДноМат     //-- Материал дна ящика
    ТипЯщ      //-- Тип ящика
    ФасВсМат   //-- Материал  вставки в фасад
    СторОткр   //-- Открывание двери
    ТипРуч     //-- Тип ручки
    ВысотРуч   //-- Высота установки ручки
    ПовРуч     //-- Поворот ручки
    Rpl_r	     //-- Расположение ручки нижней секции
    ДвеДвери   //-- Двойная дверь
    ОткрДверь  //-- Открытая дверь
    СдвРуч     //-- Сдвиг ручки
    Петля      //-- Тип петли
    Накладн    //-- Фасад накладной
    ЦвКром     //-- Цвет кромки
    ТипКром    //-- Тип кромки
    Крепеж		 //-- Крепеж корпуса
    0          //-- Тип двери
;
//---------------------------------------------------------------
//-- Заполнение верхней секции
//-- Устанавливаем зазоры для фасадов
macro ProtoPath+"SetFasPlas.mac" D_DxN D_DxN D_DzN iif(РисФас==0,D_DzN,(h_dsp+D_DxD)/2);
macro ProtoPath+"SetBoxF.mac" 0 0 0 iif(РисФас>0,-(h_dsp/2)+D_DxD/2,0);
macro ProtoPath+"SetLift.mac" LiftType2 LiftType2;
macro ProtoPath+"SetDrying.mac" DryingType;
macro ProtoPath+"MakeNiche.mac" h_dsp ОтстСз h_niz+2*h_dsp
    w-2*h_dsp  //-- Ширина
    d-ОтстСз   //-- Глубина
    h-h_niz-3*h_dsp //-- Высота
    Заполн2    //-- Тип заполнения
    КолПЯ2     //-- Количество полок или ящиков
    РисФас2    //-- Рисунок фасада
    ЗаглПол    //-- Заглубление полок
    PrMater    //-- Материал корпуса
    ФасМатВ    //-- Материал фасада
    ДноМат     //-- Материал дна ящика
    ТипЯщ      //-- Тип ящика
    ФасВсМат   //-- Материал  вставки в фасад
    СторОткр   //-- Открывание двери
    ТипРуч     //-- Тип ручки
    ВысотРуч   //-- Высота установки ручки
    ПовРуч     //-- Поворот ручки
    Rt		     //-- Расположение ручки верхней секции
    ДвеДвери   //-- Двойная дверь
    ОткрДверь  //-- Открытая дверь
    СдвРуч     //-- Сдвиг ручки
    Петля      //-- Тип петли
    Накладн    //-- Фасад накладной
    ЦвКром     //-- Цвет кромки
    ТипКром    //-- Тип кромки
    Крепеж		 //-- Крепеж корпуса
    0          //-- Тип двери
;
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;

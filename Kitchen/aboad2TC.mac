//-- Стол терминальный полукруглый двухсекционный
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов
global D_DzN;
defarr error[10];
NULLOUT=initarray(error,"");

defarr x[5],y[5],rad[4];

PrMater=DbVar("КорпМат",0);       //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);         //-- Материал задней стенки
w=DbVar("Шир",330);			          //-- Ширина
d=DbVar("Глуб",340);              //-- Глубина
h=DbVar("Выс",822);			          //-- Высота
Правый=DbVar("Правый",1);         //-- Ориентация
h_c=DbVar("ВысЦок",100);		      //-- Высота цоколя
ЦвКром=DbVar("ЦвКромки",0);   	  //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   	//-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	    //-- Тип крепежа
КрепЗС=DbVar("КрепЗС",0);   	    //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);     //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);     //-- Глубина установки задней стенки
Заполн=DbVar("Заполн",0);         //-- Тип заполнения
КолПЯ=DbVar("КолПЯ",0);           //-- Количество полок
РисФас=DbVar("РисФас",0);         //-- Рисунок фасада
ЗаглПол=DbVar("ЗаглПол",0);       //-- Заглубление полок
ФасМат=DbVar("ФасМат",0);         //-- Материал фасада
ФасВсМат=DbVar("ФасВсМат",0);     //-- Материал вставки в фасад
ТипРуч=DbVar("ТипРуч",0);         //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);     //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);         //-- Поворот ручки
ОткрДверь=DbVar("ОткрДверь",0);   //-- Открытая дверь
Петля=DbVar("Петля",0);           //-- Тип петли
Noga=DbVar("Ножка",0);	          //-- Тип ножки
Dno=DbVar("Dno",0);               //-- Тип дна
Радиус=DbVar("Радиус",250);       //-- Фаска Х/Радиус
DimY=DbVar("DimY",250);           //-- Фаска Y
MinDepth=DbVar("MinDepth",0);     //-- Глубина закрытой части
СдвРуч=DbVar("СдвРуч",40);        //-- Сдвиг ручки
Rpl_r=dbvar("Rpl_r",0);		        //-- Расположение ручки
СторОткр=dbvar("СторОткр",1);	    //-- Сторона открывания
HasDuct=DbVar("HasDuct",0);       //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0);   //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0);   //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0);   //-- Глубина короба
FixShelfRem=dbvar("FixShelfRem",0);//-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0); //-- Величина подреза съемной полки
Заполн=DbVar("Заполн",0);					  //-- Тип заполнения

Заполн2=DbVar("Заполн2",0);         //-- Тип заполнения верхней секции
КолПЯ2=DbVar("КолПЯ2",0);           //-- Количество полок верхней секции
РисФас2=DbVar("РисФас2",0);         //-- Рисунок фасада верхней секции
ЗаглПол2=DbVar("ЗаглПол2",0);       //-- Заглубление полок верхней секции
ФасМат2=DbVar("ФасМат2",0);         //-- Материал фасада верхней секции
ВысНиж=DbVar("ВысНиж",0);						//-- Высота нижней секции
Rt=dbvar("Rt",0);		        //-- Расположение ручки верхней секции
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4); //-- Толщина материала задней стенки
h_fas1=priceinfo(РисФас,"Thickness",16,2); //-- Толщина материала фасада нижней секции
h_fas2=priceinfo(РисФас2,"Thickness",16,2); //-- Толщина материала фасада верхней секции
h_fas=max(h_fas1,h_fas2);
ЗСтВрезка=8;                  //-- Врезка задней стенки
dum=120;                      //-- Размер "уголка" задней стенки под мойку
offset=14;                    //-- Нахлест накладной стенки на корпус
dst=60;                       //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                     //-- Наличие цоколя
Shiftcok=0;                   //-- Сдвиг цоколя
Накладн=1;                    //-- Фасад накладной
LongPost=0;                   //-- Опорные стойки
FasCok=0;                     //-- Размер фасок под цоколь
ForOpen=0;                    //-- Для открытого
ConfDuct=0;                   //-- Конфигурация выреза под короб по умолчанию
Shift=50;                     //-- Сдвиг опор от края корпуса
//---------------------------------------------------
//-- Константы
D_DxN=DbVar("D_DxN",1.5);	//-- Зазор по ширине для накладных дверей
D_DzN=DbVar("D_DzN",2);		//-- Зазор по высоте для накладных дверей
D_DxD=DbVar("D_DxD",2);		//-- Зазор между двойными дверьми
//---------------------------------------------------
Ширина=w-2*h_dsp;             //-- Внутренняя ширина
//-- Определяем тип угла
if (РисФас2==0)
{
 FurnT=priceinfo(РисФас,"furntype","500102",2);
}
else
{
 FurnT=priceinfo(РисФас2,"furntype","500102",2);
}
if (right(FurnT,2)=="02") //-- Фасад гнутый
{
  ТипУгла=2;              //-- Тип угла - скругление
  DimY=Радиус;
}
else                      //-- Фасад трапецеидальный
{
  ТипУгла=1;              //-- Тип угла - фаска
}
if (Правый==0)              //-- Левая ориентация
{
  dR=d;	   	                //-- Глубина справа
  dL=d-DimY;		            //-- Глубина слева
}
else
{
  dR=d-DimY;	    	         //-- Глубина справа
  dL=d;		                   //-- Глубина слева
}
d=max(dl,dr);               //-- Максимальная глубина
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  d=d-DepthDuct;
  HasDuct=0;
}
//---------------------------------------------------
if (d<(Радиус+MinDepth+2*h_dsp))
{
  error[1]="Слишком маленькая глубина стола.";
  error[2]="Значение глубины стола должно быть больше, чем '"+str(int(Радиус+MinDepth+2*h_dsp))+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение глубины стола указано корректно."
  error[5]="Проверьте, корректно ли задано значение радиуса стола."
  error[6]="Проверьте, корректно ли задано значение глубины глухой стенки."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров стола" 5 error;
  cancel;
}
//---------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetTypeFill.mac" Заполн;
//-- Построение ножек
x[1]=w;
y[1]=0;
rad[1]=0;
x[2]=w;
y[2]=dl;
rad[2]=Радиус*(1-Правый)*iif(ТипУгла==2,1,0);
x[3]=(Радиус*Правый)+(w-Радиус)*(1-Правый);
y[3]=d;
rad[3]=Радиус*Правый*iif(ТипУгла==2,1,0);
x[4]=0;
y[4]=dr;
rad[4]=0;
x[5]=0;
y[5]=0;
if (ConfDuct==3)
{
  y[1]=DepthDuct;
}
if (ConfDuct==2)
{
  y[5]=DepthDuct;
}
if (ConfDuct==4)
{
  if (2*Shift>ShiftDuct)
  {
    y[5]=DepthDuct;
  }
  if (w-2*Shift<ShiftDuct+WidthDuct)
  {
    y[1]=DepthDuct;
  }
}
macro ProtoPath+"aStBase.mac" Noga,5,x,y,rad,Shift;
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
macro ProtoPath+"SetCorpsCor.mac" 100, 1, 0;
macro ProtoPath+"SetCorpsCorButts.mac" 0, 0;
//-------------------------------------------------
macro ProtoPath+"MakeCorpsCor.mac" 0 0 0
    w,                    //-- Ширина
    d,                    //-- Глубина
    h,                    //-- Высота
    h_c,                  //-- Высота цоколя
    MinDepth,             //-- Глубина меньшей стороны
    ТипУгла,              //-- Тип угла
    Радиус+h_dsp,         //-- Фаска Х/ Радиус
    DimY+h_fas+iif(ТипУгла==2,h_dsp,0),     //-- Фаска Y
    Правый,               //-- Правая ориентация
    PrMater,        	    //-- Материал корпуса
    ЗСтМат,               //-- Материал задней стенки
    ЦвКром,               //-- Цвет кромки
    ТипКром,              //-- Тип кромки
    ТипЗСтен,             //-- Тип задней стенки
    УстЗСтен,             //-- Глубина установки задней стенки
    offset                //-- Врезка\Нахлест задней стенки
    Крепеж,               //-- Тип крепежа корпуса
    КрепЗС,      	        //-- Тип крепежа задней стенки
    Dno,                  //-- Накладное дно
    Dum,                  //-- Размер "уголка" задней стенки под мойку
    Dst,                  //-- Ширина дополнительной планки
    HasCok,               //-- Наличие цоколя
    Shiftcok              //-- Сдвиг цоколя
    LongPost              //-- Опорные стойки
    FasCok                //-- Размер фасок под цоколь
    ForOpen;              //-- Для открытого изделия
;
//-------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0) { ОтстСз=0; }		    //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		    //-- Накладной для мойки
if (ТипЗСтен==0) { ОтстСз=h_dvp; }	  //-- Утопленный
if (ТипЗСтен==2) { ОтстСз=0; }		    //-- Нет
if (ТипЗСтен==3) { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	  //-- Жесткая планка
defarr qwe[100];
furn="'230400'";
DoorKonsrt=0
kol1=npgetbywhere(2,"furntype = "+furn,"qwe");
if (kol1>0)
{
  DoorKonsrt=qwe[1];
}
//-- Полка между секциями
macro ProtoPath+"SetMat.mac" PrMater;
macro ProtoPath+"SetECod.mac" "1107";
// macro ProtoPath+"SetKCod.mac" "ПОЛК" w-2*h_dsp d-ОтстСз;
macro ProtoPath+"SetEnam.mac" "Полка межсекционная";
macro ProtoPath+"SetKrom.mac" 0 iif(Правый==1,ТипКром,0) iif(Правый==0,ТипКром,0) 0 ТипКром;
macro ProtoPath+"SetOneFilet.mac" iif(Правый==1,4,3) ТипУгла Радиус+h_dsp DimY+h_fas+iif(ТипУгла==2,h_dsp,0) ;
if (Правый==1)
{
  macro ProtoPath+"SetFix.mac"  0 Крепеж iif(MinDepth>0,Крепеж,0) 0;
}
if (Правый==0)
{
	macro ProtoPath+"SetFix.mac"  Крепеж 0 iif(MinDepth>0,Крепеж,0) 0;
}
macro ProtoPath+"MakePan.mac" h_dsp ОтстСз h_c+h_dsp+ВысНиж w-2*h_dsp d-ОтстСз 12;

if (HasDuct!=0) //-- Если нужен вырез, добавляем его
{
	objident last 1 Pan;
	#Pat rectangle ShiftDuct-h_dsp,0,0 ShiftDuct+WidthDuct+h_dsp,DepthDuct+h_dsp,0;
	macro ProtoPath+"PutCutr.mac" Pan, Pat, 1, 0, 5;
	delete Pat done;
}
//---------------------------------------------------
//--Нижняя секция
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
macro ProtoPath+"MakeNicheCorUgl.mac" h_dsp ОтстСз h_c+h_dsp
    w-2*h_dsp,      //-- Ширина
    d-ОтстСз,       //-- Глубина
    ВысНиж,					//-- Высота
    MinDepth-ОтстСз,//-- Глубина меньшей стороны
    0,              //-- Ширина меньшей стороны
    ТипУгла,        //-- Тип угла
    Радиус,         //-- Фаска Х/ Радиус
    DimY,           //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    КолПЯ,          //-- Количество полок
    РисФас,         //-- Рисунок фасада
    ЗаглПол,	      //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    ФасМат,         //-- Материал фасада
    ФасВсМат,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    Rpl_r	          //-- Расположение ручки
    0,              //-- Двойная дверь
    ОткрДверь,      //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    Петля,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,				  //-- Тип крепежа
    DoorKonsrt      //-- Тип двери
;
//--Верхняя секция
macro ProtoPath+"SetTypeFill.mac" Заполн2;
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
macro ProtoPath+"MakeNicheCorUgl.mac" h_dsp ОтстСз h_c+2*h_dsp+ВысНиж
    w-2*h_dsp,      //-- Ширина
    d-ОтстСз,       //-- Глубина
    h-h_c-3*h_dsp-ВысНиж,  //-- Высота
    MinDepth-ОтстСз,//-- Глубина меньшей стороны
    0,              //-- Ширина меньшей стороны
    ТипУгла,        //-- Тип угла
    Радиус,         //-- Фаска Х/ Радиус
    DimY,           //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    КолПЯ2,         //-- Количество полок
    РисФас2,        //-- Рисунок фасада
    ЗаглПол2,	      //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    ФасМат2,        //-- Материал фасада
    ФасВсМат,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    Rt		          //-- Расположение ручки
    0,              //-- Двойная дверь
    ОткрДверь,      //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    Петля,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,				  //-- Тип крепежа
    DoorKonsrt      //-- Тип двери
;
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;





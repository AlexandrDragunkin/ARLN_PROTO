// -- Создание объекта - шкаф кухонный шестиугольный
global ProtoPath;		//-- Путь к папке с макропрограммами общей библиотеке прототипов
//-- Параметры для расчета распашных дверей
global D_DxU D_DzU D_DxN D_DzN D_DxD;

PrMater=DbVar("КорпМат",0);     //-- Материал корпуса
ЗСтМат=DbVar("ЗСтМат",0);       //-- Материал задней стенки
w=DbVar("Шир",330);			        //-- Ширина
d=DbVar("Глуб",320);			      //-- Глубина
h=DbVar("Выс",822);			        //-- Высота
ШирГлух=DbVar("ШирС",100);		  //-- Ширина глухой секции
ШирГлух2=DbVar("ШирС2",ШирГлух);		  //-- Ширина глухой секции справа
ЦвКром=DbVar("ЦвКромки",0);     //-- Цвет кромки
ТипКром=DbVar("ТипКромки",0);   //-- Тип кромки
Крепеж=DbVar("Крепеж",0);   	  //-- Тип крепежа
КрепЗС=DbVar("КрепЗС",0);   	  //-- Тип крепежа задней стенки
ТипЗСтен=DbVar("ТипЗСтен",0);   //-- Тип задней стенки
УстЗСтен=DbVar("УстЗСтен",0);   //-- Глубина установки задней стенки
ЗаглПол=DbVar("ЗаглПол",0);     //-- Заглубление полок
ФасМат=DbVar("ФасМат",0);       //-- Материал фасада
ФасВсМат=DbVar("ФасВсМат",0);   //-- Материал вставки в фасад
СторОткр=DbVar("СторОткр",0);   //-- Открывание двери
ТипРуч=DbVar("ТипРуч",0);       //-- Тип ручки
ВысотРуч=DbVar("ВысотРуч",0);   //-- Высота установки ручки
ПовРуч=DbVar("ПовРуч",0);       //-- Поворот ручки
ОткрДверь=DbVar("ОткрДверь",0); //-- Открытая дверь
Петля=DbVar("Петля",0);         //-- Тип петли
КолПЯ=DbVar("КолПЯ",0);         //-- Количество полок или ящиков
РисФас=DbVar("РисФас",0);       //-- Рисунок фасада
СдвРуч=DbVar("СдвРуч",40);      //-- Сдвиг ручки
ЗазорДв=DbVar("ЗазорДв",0);     //-- Зазор между дверьми
Rpl_r=dbvar("Rpl_r",0);		      //-- Расположение ручки
HasDuct=DbVar("HasDuct",0);     //-- Наличие короба
ShiftDuct=DbVar("ShiftDuct",0); //-- Сдвиг короба
WidthDuct=DbVar("WidthDuct",0); //-- Ширина короба
DepthDuct=DbVar("DepthDuct",0); //-- Глубина короба
WallAngle=DbVar("WallAngle",90);//-- Пристенный угол
WallChamfer=DbVar("WallChamfer",0);//-- Размер пристенной фаски
FixShelfRem=dbvar("FixShelfRem",0);//-- Тип крепежа съемной полки
ShelfRemCut=dbvar("ShelfRemCut",0); //-- Величина подреза съемной полки
Заполн=DbVar("Заполн",0);					  //-- Тип заполнения

Заполн2=DbVar("Заполн2",0);         //-- Тип заполнения верхней секции
КолПЯ2=DbVar("КолПЯ2",0);           //-- Количество полок верхней секции
РисФас2=DbVar("РисФас2",0);         //-- Рисунок фасада верхней секции
ЗаглПол2=DbVar("ЗаглПол2",0);       //-- Заглубление полок верхней секции
ФасМат2=DbVar("ФасМат2",0);         //-- Материал фасада верхней секции
ВысНиж=DbVar("ВысНиж",0);						//-- Высота нижней секции
Rt=dbvar("Rt",0);										//-- Расположение ручки верхней секции
Naves=dbvar("Naves",0);						 //-- Тип навески
//---------------------------------------------------
h_dsp=PriceInfo(PrMater,"Thickness",16); //-- Толщина материала корпуса
h_dvp=PriceInfo(ЗСтМат,"Thickness",4);   //-- Толщина материала задней стенки
ЗСтВрезка=8;                      //-- Врезка задней стенки
Угол=0;                           //-- Угол стола
ТипУгла=3;                        //-- Cтол с прямоугольным вырезом
dum=120;                          //-- Размер "уголка" задней стенки под мойку
offset=14;                        //-- Нахлест накладной стенки на корпус
dst=60;                           //-- Ширина дополнительной планки для стенки под мойку
HasCok=0;                         //-- Наличие цоколя
Shiftcok=0;                       //-- Сдвиг цоколя
Накладн=1;                        //-- Фасад накладной
Правый=0;                         //-- Левая ориентация
Dno=0;                            //-- Ненакладное дно
LongPost=0;                       //-- Опорные стойки
FasCok=0;                         //-- Размер фасок под цоколь
ForOpen=0;                        //-- Для открытого
ConfDuct=0;                       //-- Конфигурация выреза под короб по умолчанию
Shift=50;                         //-- Сдвиг опор от края корпуса
//-- Установим тип двери карусельная
defarr DOORARR[1] ;
furn="'230300'"
DOORTYPE=0
kol1=npgetbywhere(2,"furntype = "+furn,"DOORARR");
if (kol1>0)
{
  DOORTYPE=DOORARR[1];
}
if (WallAngle!=90)
{
  putmsg("Для данного прототипа пристенный угол, отличный от 90 градусов не реализован",0);
  WallAngle=90;
}
//-------------------------------------------------
DimX=w-ШирГлух2;
DimY=((d-ШирГлух)-h_dsp*iif(Dno==0,1,1));
MinDepth=ШирГлух;
MinDepthX=ШирГлух2;
Ang=radian(WallAngle-90);   //-- Пристенный угол
ws=(w-d*sin(Ang))/cos(Ang); //-- Пристенная ширина
ds=(d-w*sin(Ang))/cos(Ang)+h_dsp*sin(Ang)*iif(Ang>0,1,0); //-- Пристенная глубина
DimX=DimX/cos(Ang);
DimY=DimY/cos(Ang);
if (ТипЗСтен==3)
{
  offset=ЗСтВрезка;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  ds=ds-DepthDuct;
  HasDuct=0;
}
//-------------------------------------------------
NULLOUT=getsnap();
NULLOUT=pushinst(1);
macro ProtoPath+"SetRemShelfCut.mac" ShelfRemCut;
macro ProtoPath+"SetRemShelfFix.mac" FixShelfRem;
macro ProtoPath+"SetTypeFill.mac" Заполн;
//-------------------------------------------------
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct,WidthDuct,DepthDuct;
macro ProtoPath+"SetWallAngle.mac" WallAngle;
macro ProtoPath+"SetCorpsCor.mac" 0, 1, ШирГлух2;
macro ProtoPath+"SetWallChamfer.mac" WallChamfer;
macro ProtoPath+"SetNaves.mac" Naves;
macro ProtoPath+"SetCorpsCorButts.mac" 0, 0;
//-------------------------------------------------
macro ProtoPath+"MakeCorpsCor.mac" 0 0 0
    w,                    //-- Ширина
    d,                    //-- Глубина
    h,                    //-- Высота
    0,                    //-- Высота цоколя
    MinDepth,             //-- Размер меньшей стороны
    ТипУгла,              //-- Тип угла
    DimX,                 //-- Фаска Х/ Радиус
    DimY+h_dsp,                 //-- Фаска Y
    Правый,               //-- Правая ориентация
    PrMater,        	    //-- Материал корпуса
    ЗСтМат,               //-- Материал задней стенки
    ЦвКром,               //-- Цвет кромки
    ТипКром,              //-- Тип кромки
    ТипЗСтен,             //-- Тип задней стенки
    УстЗСтен,             //-- Глубина установки задней стенки
    offset                //-- Врезка\Нахлест задней стенки
    Крепеж,               //-- Тип крепежа корпуса
    КрепЗС,      	        //-- Тип крепежа задней стенки
    Dno,                  //-- Накладное дно
    Dum,                  //-- Размер "уголка" задней стенки под мойку
    Dst,                  //-- Ширина дополнительной планки
    HasCok,               //-- Наличие цоколя
    Shiftcok              //-- Сдвиг цоколя
    LongPost              //-- Опорные стойки
    FasCok                //-- Размер фасок под цоколь
    ForOpen;              //-- Для открытого изделия
;
//-------------------------------------------------
//-- Определим отступ сзади от типа задней стенки
if (ТипЗСтен!=0)  { ОтстСз=0; }		      //-- Накладной
if (ТипЗСтен==-1) { ОтстСз=0; }		      //-- Накладной для мойки
if (ТипЗСтен==0)  { ОтстСз=h_dvp; }	    //-- Утопленный
if (ТипЗСтен==2)  { ОтстСз=0; }		      //-- Нет
if (ТипЗСтен==3)  { ОтстСз=УстЗСтен; }	//-- Врезной
if (ТипЗСтен==4) { ОтстСз=h_dvp; }	    //-- Жесткая планка
macro ProtoPath+"SetDuct.mac" HasDuct,ShiftDuct-iif(ConfDuct==2,0,2*h_dsp),WidthDuct+iif(ConfDuct==4,2*h_dsp,0),DepthDuct+h_dsp-ОтстСз;
if (WallChamfer>0)
{
  macro ProtoPath+"SetWallChamfer.mac" WallChamfer-2*iif(ТипЗСтен==1,h_dvp,ОтстСз)+h_dsp*sqrt(2);
}
//-- Нижняя секция
macro ProtoPath+"MakeNicheCor.mac" iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)) ОтстСз h_dsp
    w-h_dsp-iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)),      //-- Ширина
    d-ОтстСз-h_dsp, //-- Глубина
    ВысНиж,      //-- Высота
    MinDepth,       //-- Размер меньшей стороны
    ТипУгла,        //-- Тип угла
    DimX-h_dsp+ЗаглПол,   //-- Фаска Х/ Радиус
    DimY+ЗаглПол, //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    КолПЯ,          //-- Количество полок
    0,              //-- Рисунок фасада
    Угол,    	      //-- Угол поворота двери
    0, 	            //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    ФасМат,         //-- Материал фасада
    ФасВсМат,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    Rpl_r	          //-- Расположение ручки
    0,              //-- Двойная дверь
    ОткрДверь,      //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    Петля,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,				  //-- Тип крепежа
    0 	            //-- Тип двери
;
//--Межекционная полка
macro ProtoPath+"SetTypeFill.mac" 0;
macro ProtoPath+"MakeNicheCor.mac" iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)) ОтстСз 0
    w-h_dsp-iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)),      //-- Ширина
    d-ОтстСз-h_dsp, //-- Глубина
    h_dsp+160,  //-- Высота
    MinDepth,       //-- Размер меньшей стороны
    ТипУгла,        //-- Тип угла
    DimX-h_dsp,   //-- Фаска Х/ Радиус
    DimY, //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    1,          //-- Количество полок
    0,              //-- Рисунок фасада
    0,    	      //-- Угол поворота двери
    0, 	            //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    0,         //-- Материал фасада
    0,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    0,       //-- Высота установки ручки
    0,         //-- Угол поворота ручки
    0	          //-- Расположение ручки
    0,              //-- Двойная дверь
    0,      //-- Открытая дверь
    0,         //-- Сдвиг ручки
    0,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,				  //-- Тип крепежа
    0 	            //-- Тип двери
;
move last 1 done 0 0 h_dsp+ВысНиж-80 nocopy;
//--Верхняя секция
macro ProtoPath+"SetTypeFill.mac" Заполн2;
macro ProtoPath+"MakeNicheCor.mac" iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)) ОтстСз 2*h_dsp+ВысНиж
    w-h_dsp-iif(WallChamfer==0,h_dsp,iif(ТипЗСтен==1,h_dvp,ОтстСз)),      //-- Ширина
    d-ОтстСз-h_dsp, //-- Глубина
    h-3*h_dsp-ВысНиж,  //-- Высота
    MinDepth,       //-- Размер меньшей стороны
    ТипУгла,        //-- Тип угла
    DimX-h_dsp+ЗаглПол2,   //-- Фаска Х/ Радиус
    DimY+ЗаглПол2, //-- Фаска Y
    Правый, 	      //-- Правая ориентация
    КолПЯ2,          //-- Количество полок
    0,              //-- Рисунок фасада
    Угол,    	      //-- Угол поворота двери
    0, 	            //-- Заглубление полок
    PrMater,        //-- Материал заполнения
    ФасМат2,         //-- Материал фасада
    ФасВсМат,       //-- Материал вставки в фасад
    СторОткр,       //-- Открывание двери
    ТипРуч,         //-- Тип ручки
    ВысотРуч,       //-- Высота установки ручки
    ПовРуч,         //-- Угол поворота ручки
    Rt		          //-- Расположение ручки
    0,              //-- Двойная дверь
    ОткрДверь,      //-- Открытая дверь
    СдвРуч,         //-- Сдвиг ручки
    Петля,          //-- Тип петли
    Накладн,        //-- Фасад накладной
    ЦвКром,         //-- Цвет кромки
    ТипКром,        //-- Тип кромки
    Крепеж,				  //-- Тип крепежа
    0 	            //-- Тип двери
;
defarr qwe[1];
furn="'230300'"
kol1=npgetbywhere(2,"furntype = "+furn,"qwe");
if (kol1>0)
{
   DoorKonst=qwe[1];	        //-- Тип двери карусельная
}
//--Нижняя секция
if (РисФас>0)
{
	macro ProtoPath+"SetDoorK.mac" DoorKonst;
	macro ProtoPath+"SetFasdN.mac" ФасМат РисФас ЦвКром ТипКром ФасВсМат ФасВсМат ;
	macro ProtoPath+"SetDoor.mac" СторОткр 0 ОткрДверь 2 Петля 0 h_dsp;
	macro ProtoPath+"SetHand.mac" 0 Rpl_r ;
	macro ProtoPath+"SetHandl.mac" ТипРуч, //-- Тип ручки
      СдвРуч,    //-- Сдвиг ручки по x
      ВысотРуч,  //-- Сдвиг ручки по z
      ПовРуч;    //-- Поворот ручки

	macro ProtoPath+"MakeDoorKar.mac"
	ШирГлух2 d-h_dsp h_dsp		//-- Правый нижний дальний угол
	W-ШирГлух2-h_dsp D-ШирГлух-h_dsp ВысНиж					 //-- Размер по ширине, глубине и высоте
		h_dsp-D_DxN																		 //-- Изменения фасада: Cправа
		h_dsp-D_DxN																		 //--                   Слева
		iif(РисФас2==0,h_dsp-D_DzN,h_dsp/2-D_DzN/2)    //--                   Сверху
		h_dsp-D_DzN																		 //--                   Снизу
		ЗазорДв																				 //-- Зазор по ширине
	;
}
//--Верхняя секция
if (РисФас2>0)
{
	macro ProtoPath+"SetDoorK.mac" DoorKonst;
	macro ProtoPath+"SetFasdN.mac" ФасМат2 РисФас2 ЦвКром ТипКром ФасВсМат ФасВсМат ;
	macro ProtoPath+"SetDoor.mac" СторОткр 0 ОткрДверь 2 Петля 0 h_dsp;
	macro ProtoPath+"SetHand.mac" 0 Rpl_r ;
	macro ProtoPath+"SetHandl.mac" ТипРуч, //-- Тип ручки
      СдвРуч,    //-- Сдвиг ручки по x
      ВысотРуч,  //-- Сдвиг ручки по z
      ПовРуч;    //-- Поворот ручки

	macro ProtoPath+"MakeDoorKar.mac"
	ШирГлух2 d-h_dsp 2*h_dsp+ВысНиж		//-- Правый нижний дальний угол
	W-ШирГлух2-h_dsp D-ШирГлух-h_dsp H-h_dsp*3-ВысНиж   //-- Размер по ширине, глубине и высоте
		h_dsp-D_DxN																 //-- Изменения фасада: Cправа
		h_dsp-D_DxN																 //--                   Слева
		h_dsp-D_DzN																 //--                   Сверху
		iif(РисФас==0,h_dsp-D_DzN,h_dsp/2-D_DzN/2) //--                   Снизу
		ЗазорДв																		 //-- Зазор по ширине
	;
}
macro ProtoPath+"SetDuct.mac" 0,0,0,0;
NULLOUT=popinst(1);
NULLOUT=resnap();
exit;

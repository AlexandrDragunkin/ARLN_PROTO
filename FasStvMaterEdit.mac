//-- Изменить материал фасада створки
selbyattr "Posit==10" child all done;
karkas_kol=sysvar(61);
karkas_num=GetCarcNumb();
if (karkas_kol==0)
{
 putmsg("Сначала надо создать мебельный каркас",0);
 exit;
}
if (karkas_kol==1)
{
 #karkas MBCarcase set yes;
}
if (karkas_kol>1&&karkas_num==0)
{
 #karkas MBCarcase set no yes :
}


global ProtoPath;
global pnt, iPNT;
defarr arr[3];
defarr DMatarr[1] ;

DMat=0;
kol=npgetbywhere(1,"","DMatarr",getyadsubst(25));
      if (kol>0) {
        DMat=DMatarr[1];
      }
DefFasDMat=str(getyadsubst(25))+"#"+str(DMat) ;                    // Формируем строку умолчаний.

#ok_flag
setvar
"Изменить материал фасада створки"
""
left
"Определите параметры замены"
done

string auto button 6 default DefFasDMat     "Материал фасада:" NDMat
str listonly
	  current "По указанию"
		"Все створки дверей"
done
"Способ замены:"    S_zam
done;
if (ok_flag==0)
{
  exit;
}
macro Protopath+"ChgSel.mac" S_zam ;
splitbydelim(NDMat,"#",Arr);
E_col=Arr[2];
if (S_zam=="По указанию")
{
  Loop2:
  onerror final;
  switch autosingle on;
  selbyattr "Left(FurnType,2)==\"21\"" prompt "Укажите створку двери:" wholly : ;
  switch autosingle off;
  if (sysvar(61)==0)
  {
    goto final;
  }
  pnt1=getselnum(1);
  offerror;
  err=1;
  err=GetProtoVal(pnt1,"PrMater1",PrMat);
  if (err==1)
  {
    protoobj edit list pnt1 nodc "PrMater1" E_col
                            "PrMater2" E_col
                            "PrMater3" E_col
                            "PrMater4" E_col
                            "PrMater5" E_col
                            "PrMater6" E_col ;
  }
  goto Loop2;
}
else
{
  i=0;
  LoopAll2:
  if (i<iPNT)
  {
    i=i+1;
    pnt1=pnt[i];
    err=1;
    err=GetProtoVal(pnt1,"PrMater1",PrMat);
    if (err==1)
    {
    protoobj edit list pnt1 nodc "PrMater1" E_col
                            "PrMater2" E_col
                            "PrMater3" E_col
                            "PrMater4" E_col
                            "PrMater5" E_col
                            "PrMater6" E_col ;
    }
    goto LoopAll2;
  }
}
final:
switch autosingle off;
exit;

//SetPlProject.mac
//*********************************************
//       Определение основных значений полей
//            в печатных формах
//              Драгункин А.Р.
//              ГеоС 2008-2011 г.
//*********************************************

Global
FormList
Pl_Obozn
Pl_name
Pl_name1
Pl_MatrZag
Pl_Razrab
Pl_Prov
Pl_Utv
Bl_OverPlot
Pl_Pref
Bl_OverVis
Bl_OverEdit
g_DirDrawLn           //Все файлы чертежей длинномеров размещать в отдельной папке 0-нет 1-да
g_DrawBlank           //Чертежи на бланках 0-нет 1-да
g_DrawSpec            //Формировать спецификацию 0-нет 1-да
g_DrawCod             //Способ обозначения чертежа 0-относительный по первому вхождению 1-абстрагированный код
g_DrawStamp           //Ключ запроса информации для штампа бланка из файла stamp.dbf и из файла ZAKAZ.dbf =0 только stamp.dbf =1
g_FormStamp           //Ключ запроса формы штампа 0-не запрашивать 1-регулярно
g_PgFileDraw          //Каждый лист чертежа в отдельном файле 0-нет 1-да
g_PgFileSpec          //Каждый лист спецификации в отдельном файле 0-нет 1-да
g_GroupDoc            //Формировать групповой чертеж по всем документам 0-нет 1-да
g_dataNaz				// дата назначенная
g_currBl				// Номер текущего бланка
N_ListPlot  // число копий при печати
g_keyres
g_osn
;
getpar l_dialog;
//global Support ;
ProtoPath=MPathExpand("<Proto>")+"\\"; //+Support
g_keyres=0;
metDefault=0
defarr arrinf[100];
nullout=sysarr(81,arrinf);
g_currBl=iif(isvardef("g_currBl")==0,3,g_currBl);				// Номер текущего бланка
Pl_obozn=iif(isvardef("Pl_obozn")==0,"000",pl_obozn);
g_dataNaz=iif(isvardef("g_dataNaz")==0,str(arrinf[1])+"."+str(arrinf[2])+"."+str(arrinf[3]),g_dataNaz);
SB_Nam1="sss"
Pl_Razrab="Кульман И.К."
Pl_Prov="Ватман А.Л."
Pl_Utv="Дизйнер Н.И."
//Считываем данные по умолчанию для штампа
gosub DefStamp ;
if g_DrawStamp==0 { gosub GetZakaz ; }
CurGrk=Sysvar(76) ;
DefaultRem=1
if l_dialog
{
	#ok_flag   //Если после setvar будет 0 - Нажали кнопку "Отмена"
	setvar
	"Параметры проекта"           // Заголовок окна
	""                  // Имя файла иллюстрации
	// Пояснительный текст
	center         //с выравниванием слева
	" Определите значения для заполнения полей" "спецификации и чертежей"
	done          // конец пояснительного текста

	str default Pl_obozn  "Номер проекта"     Pl_obozn
	str default g_dataNaz "Дата отгрузки"    g_dataNaz
	str default Pl_Razrab "Фамилия И.О. разработчика"     Pl_Razrab
	str default Pl_Prov   "Фамилия И.О. проверяющего"     Pl_Prov
	str default Pl_Utv    "Фамилия И.О. утверждающего"    Pl_Utv
	//str default SB_Nam1  "Наименование чертежа сборки"    SB_Nam1
	//logical  default Bl_OverPlot    "Чертежи Печатать ?"   Bl_OverPlot
	//real  auto    listonly  1 2 3 4 5 6 7 8 9 10 done "Число копий"  N_ListPlot
	//logical  default metDefault    " Сохранить умолчания? "  DefaultRem
	str auto 	listonly
			//if g_currBl==1 	{ current }
			//"BlankKD"
			//if g_currBl==2 { current }
			//"BlankDS"
			if g_currBl==3 { current }
			"gost_a3"
			if g_currBl==4 { current }
			"gost_a4"
			"Без_бланка_портрет"
			"Без_бланка_альбом"
		done
					"Предпочтительный формат для чертежей:"    FormList

	done  // Конец команды
	if ok_flag==0 
	{
		g_keyres=1;
		exit;
		
	}
}
Bl_OverPlot=0
N_ListPlot=0
if FormList=="gost_a4" { g_currBl=4 }
if FormList=="gost_a3" { g_currBl=3 }
if g_osn==0 { FormList="Без_бланка_портрет"; }  // Если без основной надписи
if defaultRem==1 { gosub NewStData ; }


exit;
//================
DefStamp:
//Умолчания из Stamp.dbf
F_stamp=ProtoPath+"Stamp.dbf"
metdfst:
if GetCount(f_stamp)==0 {
	gosub sNewtable;
	metDefault=1 ;
	goto metdfst ;
}
DbOpen cst,f_stamp,""
nfld=dbFldCount(cst)
dbClose cst ;
if nfld<10 {
	//nullout=removefile(f_stamp);
	gosub sNewtable;
	metDefault=1 ;
	goto metdfst ;
   }
else {
     DbOpen cst,f_stamp,""
     DbMoveFirst cst;
     Pl_Obozn=DbGetValue(cst,0,"")
     Pl_name=DbGetValue(cst,1,"")

     Pl_name1=DbGetValue(cst,2,"")
     Pl_MatrZag=DbGetValue(cst,3,"")
     Pl_Razrab=DbGetValue(cst,4,"")
     Pl_Prov=DbGetValue(cst,5,"")
     Pl_Utv=DbGetValue(cst,6,"")
	 FormList=DbGetValue(cst,7,"")
	 g_currBl=DbGetValue(cst,8,3);
     dbClose cst ;
     }
return;
//================
NewStData:
//сохраняем новые данные в stamp.dbf
DbOpen cst,f_stamp,""
DbMoveFirst cst;
   DbSetValue cst,"PlObozn",Pl_Obozn;
   DbSetValue cst,"Plname",Pl_name;
   DbSetValue cst,"Plname1",Pl_name1;
   DbSetValue cst,"PlMatrZag",Pl_MatrZag;
   DbSetValue cst,"PlRazrab",Pl_Razrab;
   DbSetValue cst,"PlProv",Pl_Prov;
   DbSetValue cst,"PlUtv",Pl_Utv,
   DbSetValue cst,"FormList",FormList,
   DbSetValue cst,"CurrBl",g_currBl
   ;
   DbUpdate cst ;
   DbClose cst ;
return;

GetZakaz:
F_zakaz=SysVar(1)+"Zakaz.dbf"
if GetCount(F_zakaz)==0 {
   return ;
}
     DbOpen cst1,F_zakaz,""
     DbMoveFirst cst1;
   Pl_Obozn=DbGetValue(cst1,"CUSTNUM","xxx")
   Pl_Razrab=DbGetValue(cst1,"OPERATOR","xxx")
   dbClose cst1 ;
return;

sNewtable:
   Pl_Obozn="%elemname%,   дет.%commonpos%,  %oname%, %onumber%"
   Pl_name="%elemname%"
   Pl_name1=""
   Pl_MatrZag="%{priseinfo(getattr(0,\"priceid\",0),\"MATNAME\")%"
   Pl_Razrab="Иванов И.И."
   Pl_Prov="Иванов И.И."
   Pl_UTV="Иванов И.И."
   FormList="gost_a3"
   DbCreate cst,F_stamp,
   "PlObozn","C",150,0,
   "Plname","C",150,0,
   "Plname1","C",150,0,
   "PlMatrZag","C",150,0,
   "PlRazrab","C",150,0,
   "PlProv","C",150,0,
   "PlUtv","C",150,0,
   "FormList","C",150,0,
   "CurrBl","N",5,0,
   "TID","N",5,0;
   
   
   DbAddNew cst;
   DbSetValue cst,"PlObozn",Pl_Obozn;
   DbSetValue cst,"Plname",Pl_name;
   DbSetValue cst,"Plname1",Pl_name1;
   DbSetValue cst,"PlMatrZag",Pl_MatrZag;
   DbSetValue cst,"PlRazrab",Pl_Razrab;
   DbSetValue cst,"PlProv",Pl_Prov;
   DbSetValue cst,"PlUtv",Pl_Utv;
   DbSetValue cst,"FormList",FormList;
   DbSetValue cst,"CurrBl",g_currBl;
   DbSetValue cst,"TID",1;
   ;
   DbUpdate cst ;
   DbClose cst ;
return;   
//13/05/04..2008
//*********************************************
//        Заполнение спецификации
//              Драгункин А.Р.
//              ГеоС 2008-2014 г.
//*********************************************
//

global
FormList
Pl_Obozn
Pl_name
Pl_Razrab
Pl_Prov
Pl_Utv
//k
CBDpath
TMPpath
ProtoPath
setsbKEY
U_layer
finalcbd
FTmpName
KodeDop
SSB_obozn
ProtoPath
L_SUPPORT
g_nameFK3 
g_nameFWMF
FileNameSP
;
setsbKEY=0;

getpar
Ltype
Seln
 ;
//PL_obozn="222.01.00"



//Pl_Razrab="Кульман И.К."
//Pl_Prov="Ватман А.Л."
//Pl_Utv="Дизйнер Н.И."
FTMPname="TmpDetL"+str(Ltype)+".dbf"
//;
mp="<K3Files>"
ffPath=MPathExpand(mp)+"\\Штампы\\";

dimgcoef 1;
stvi=0 //ключик для стандартных и покупных чтобы два раза не вывести
setsbKEY=1;

//========================================================== 1
global StrArr;
defarray StrArr[256] info[8] info1[8] arr[3] arr1[5] data[8] ;
global
N_str   //Число заполненных элементов
;

N_str=0
//Сохраняем текущие установки текста
=initarray(strarr,".");
#tx text "a" done 0 0 0 1 0 0 ;
Result=GetTextInfo(tx,info1) ;  //Функция заполняет массив Info информацией об объекте obj типа «Текст»
delete tx done ;
=SysArr(43,arr) ;
//========================================================== 1

Start:


//Открываем базу TMPDet.dbf
CBDpath=MPathExpand("<appdata>")+"\\"; //ProtoPath  ;
TMPpath=MPathExpand("<appdata>")+"\\"; //ProtoPath  ;


//fnk=TMPpath+"TmpDet.dbf"
fnk=TMPpath+FTmpName
if GetCount(fnk) {  DbOpen rcdTMP,fnk;   //открыли базу проекта
   n=DbRecCount(rcdTMP) ;//ОПРЕДЕЛИЛИ ЧИСЛО ЗАПИСЕЙ В ОТКРЫТОЙ БАЗЕ
   if n<1 { #ok_flag alternative "Внимание!!" msgbox text "Записи в базе с именем "+fnk+" не обнаружены."
   done "Ок" done ;
   //Vimage on;
   'regen all
   ;exit ;
   goto metend1 ; }
   
}
DbMoveFirst rcdTMP;                  // Встали на первую запись
defarr POZI[n] ; //массив данных POZ
i=0
i1=0
POZI[1]=-999
maxK=27; //кол строк в бланке
ListBl=1
met1:
If !DbEOF(rcdTMP) {  //*
   i=i+1
   ValPoz=DbGetValue(rcdTMP,"POZ",-99); //CПИСАЛИ ЗНАЧЕНИЕ ИЗ ПОЛЯ POZ

   if ValPoz==-99 { goto met3 ; }                // Переход на след

   j=0
   met2:
   if (i>0)&&(j<i) {
      j=j+1
      if (POZI[J]!=ValPoz) {
          if i==j {
             i1=i1+1 ;
             POZI[i1]=ValPoz ;
          } //i1
      }
      else { goto met3 ; }
      goto met2 ;
    }

met3:
   DbMoveNext rcdTMP;                 // Переход на следующую вниз
goto met1;
}  //*

gosub sortpoz ;
//сортируем массив по возрастанию
//В первую строку вписываем данные о сборочном чертеже
//Создаем секцию сборочные еденицы
          //парметр SYSSB==1
          //списываем данные из POZ NAME DESCR + ДАННЫЕ О ФОРМАТЕ ЧЕРТЕЖА
          //ищем минимальное значение POZ
          //подcчитываем число записей и присваиваем переменной KOL
i=0
Lin=0
FileName=SysVar(2) ;
ProjPath=getfilepath(FileName);
NAmetemp=ProjPath+"TempLn.k3"
//save auto overwrite;

select all;
if sysvar(61)>0
{
if GetCount(NAmetemp)!=0 { save select all done NAmetemp [ overwrite ]  ; }
else { save select all done NAmetemp ; }
}
new ;
//FileName1=FileName-3 ;
//FileNameSP=FileName1+"_sp"+str(Ltype)+".k3" ;


FileName1=GetFileTitle(FileName)
FileNameSP=ProjPath+"Reports"+g_nameFK3+FileName1-3+"_sp"+str(Ltype)+".k3" ;
VPORT ZOOM 3 ;
vport zoom,, ;
ObjS=SysVar(60) ;
append ffPath+"\\_SP1ф.K3" 0 0 0 ;

startpos=0
//GoSub SBDoc ; //заносим информацию о документации

DbMoveFirst rcdTMP;                  // Встали на первую запись
k=0
met4:
if i<4 {
   i=i+1 ;
   Izag=1 ;
   DbFilter rcdTMP,"SYSSB==i" ;
   n1=DbRecCount(rcdTMP) ;//ОПРЕДЕЛИЛИ ЧИСЛО ЗАПИСЕЙ В ОТКРЫТОМ НАБОРЕ

   if n1>0 {
      j=0
      met5:
      if j<i1 {
      ;
         j=j+1
         DbFilter rcdTMP,"(SYSSB==i)&&(POZ==POZI[j])";
         n2=DbRecCount(rcdTMP) ;//ОПРЕДЕЛИЛИ ЧИСЛО ЗАПИСЕЙ В ОТКРЫТОМ НАБОРЕ

         IF N2==0 { GOTO MET5 ; }
         if (Izag==1)&&(i>1) {
            Izag=-2
            kol=0
            SPozJ="."
            ValDescr="."
            ValNAME="." ;
            ValFormat="."
            j=j-1
            goto met6;
            }

         if (Izag==1)||(Izag==-2) {
            IzagPr=Izag
            kol=0
            SPozJ="."
            ValDescr="."
            ValFormat="."
            LnPrim="."
            Izag=0
            if startpos==0 {
               startpos=1
               kol=1
               ValFormat="."
               if FormList=="gost_a3" { ValFormat="А3" }
               if FormList=="gost_a4" { ValFormat="А4" }
               ValDescr=PL_obozn+".0"+Str(Ltype+1)+".00СБ"
               ValNAME="Сборочный чертеж" ;
               if Seln>1  { LnPrim=Str(Seln)+" Стр."  }
               Izag=IzagPr
               j=j-1
               goto met6;
            }
            if i==1 { ValNAME="%%uСборочные %%uединицы" ; }
            if i==2 { ValNAME="%%uДетали" ; }
            if ((i==3)||(i==4))&&(stvi!=1) { ValNAME="%%uСтандартные %%uи %%uпокупные %%uизделия" ; stvi=1 }
            j=j-1
            goto met6;
            }
            if Izag==0 {
            Izag=-1
            kol=0
            SPozJ="."
            ValDescr="."
            ValNAME="." ;
            ValFormat="." ;
            LnPrim="."
            j=j-1
            goto met6;
            }
         DbMoveFirst rcdTMP;                  // Встали на первую запись
         KOL=n2
         ValFormat=DbGetValue(rcdTMP,"DRWFORMAT","б/ч"); //CПИСАЛИ ЗНАЧЕНИЕ ИЗ ПОЛЯ DRWFORMAT
         if ValFormat=="" { ValFormat="б/ч"  }
         ValDescr=DbGetValue(rcdTMP,"DESCR","-99") ; //CПИСАЛИ ЗНАЧЕНИЕ ИЗ ПОЛЯ DESCR
         if valDescr=="" {
         ValDescr="." ; }
         ValNAME=DbGetValue(rcdTMP,"NAME","-99"); //CПИСАЛИ ЗНАЧЕНИЕ ИЗ ПОЛЯ NAME

         ValFaser=DbGetValue(rcdTMP,"FASER","-99"); //CПИСАЛИ ЗНАЧЕНИЕ ИЗ ПОЛЯ FASER
         
         LnPrim=DbGetValue(rcdTMP,"LNPRIM","."); //CПИСАЛИ ЗНАЧЕНИЕ ИЗ ПОЛЯ LNPRIM
         
         dop=""
         if POZI[j]<10 { dop="  " }
         //else {
         if POZI[j]<100 { dop=" " }
         // }
         
         SPozJ=dop+str(POZI[j]);
met6:
//======================================================================1
         ;selbyattr "NAIMENCH" all done;
         if Sysvar(61)==0 {
            GoSub AddDopBlank ;
         }
         SPName=getselnum(1);
           Valname=Trim(ValName) ;
           Simb=Len(ValNAME) ;
           Valname1=ValName;
//======================================================================2
         //Преобразуем строку в многострочный текст с сохранением ширины
//Текущие значения параметров Текста
=GetObjProp(SPName,arr1) ;
Result=GetTextInfo(SPName,info) ;  //Функция заполняет массив Info информацией об объекте obj типа «Текст»
                                //1 Имя шрифта STRING
                                //2 Высота символа DOUBLE
                                //3 Отношение ширины к высоте (в процентах) DOUBLE
                                //4 Угол наклона шрифта (в градусах) DOUBLE
                                //5 Разрядка между символами по горизонтали (впроцентах)DOUBLE
                                //6 Разрядка между символами по вертикали (впроцентах)DOUBLE
                                //7 Количество строк в объекте типа «Текст» INT
                                //8 Содержимое первой строки текста STRING
=initarray(strarr,".");
theight info[2]  ; //Команда устанавливает высоту текста равной <K>.
tscale  info[3]  ; //Команда устанавливает отношение ширины символа к высоте (в процентах)
tangle  info[4]  ; //Команда устанавливает угол наклона символов шрифта (в градусах) равным <K>.
tdchar  info[5]  ; //Команда устанавливает разрядку между символами (в процентах) равную <K>.
tdstring info[6] ; //Команда устанавливает разрядку между строками (в процентах) равную <K>.
grfcoeff arr1[5] ;

StrArr[1]=Valname ;
;macro L_support+"ChStrTxLn.mac" 50 ;  //Преобразуем строку в массив строк по заданной ширине
theight info1[2] ; //Команда устанавливает высоту текста равной <K>.
tscale  info1[3] ;//Команда устанавливает отношение ширины символа к высоте (в процентах)
tangle  info1[4] ; //Команда устанавливает угол наклона символов шрифта (в градусах) равным <K>.
tdchar  info1[5] ; //Команда устанавливает разрядку между символами (в процентах) равную <K>.
tdstring info1[6] ; //Команда устанавливает разрядку между строками (в процентах) равную <K>.
grfcoeff arr[3] ;


//======================================================================2
psi=0
met6a:
If psi<N_str {
           ; psi=psi+1   ;
            //Вписали в спецификацию
           Gosub SpecString ;

           text edit SPFormat ValFormat ;
           text edit SPpozn SPozJ ;
		   
		   if IsVarDef("ValDescr")==5 { 
				ValDescr=str(ValDescr) 
			}
		   else {
			macro L_support+"EdStrTyp.mac" ByRef ValDescr;
			}
           Text edit SPobozn  ValDescr ;  objident last 1 Spobozn ;
		   //putmsg(ValDescr);
		   
           ;macro L_support+"EdStrLong.mac" Spobozn 60 0 ; //Размещаем строку в заданную ширину за счет изменения высоты символа
            text edit SPName   StrArr[psi] ;
            SPozJ="."
            ValDescr="."
            ValFormat="."
            ;
            if psi==N_str {
               dop=""
               if kol<100 { dop=" " }
               if kol==0 {Skol="." ; LnPrim="." }
               else { Skol=dop+str(kol); }
               text edit SPkol   skol ;
               if IsVarDef("LnPrim")==0 { LnPrim="." }
               text edit SPPrim   LnPrim ;
               objident last t1 ;
               ;macro L_support+"ChStrTxLnG.mac" t1 23 ;  //Преобразует длину текста до заданной, без изменения параметров стиля самого текста.
            }
            else {
                 text edit SPkol   "." ;
                 text edit SPPrim   "." ;
                 }

            goto met6a ;
}
         //text edit SPName   ValNAME1 ;

         goto met5 ;
      }
   }
goto met4 ;
}

//присваиваем в штампе значения

;macro L_support+"EdTextAttrib.mac"  "oboznSB!=0" PL_obozn+".0"+Str(Ltype+1)+".00" ;

objident last t1 ;
;macro L_support+"EdStrLong.mac" t1 120 0 ; //Размещаем строку в заданную ширину за счет изменения высоты символа

gosub LongName ;
;macro L_support+"EdTextAttrib.mac"  "NaimenSB1!=0" SB_Nam1 ;
objident last t1 ;
;macro L_support+"ChStrTxLnG.mac" t1 60 ;  //Преобразует длину текста до заданной, без изменения параметров стиля самого текста.

;macro L_support+"EdTextAttrib.mac"  "RazrabSp!=0" Pl_Razrab ;
objident last t1 ;
;macro L_support+"EdStrLong.mac" t1 20 0 ; //Размещаем строку в заданную ширину за счет изменения высоты символа

;macro L_support+"EdTextAttrib.mac"  "ProvSP!=0" Pl_Prov ;
objident last t1 ;
;macro L_support+"EdStrLong.mac" t1 20 0 ; //Размещаем строку в заданную ширину за счет изменения высоты символа

;macro L_support+"EdTextAttrib.mac"  "UtvSP!=0" Pl_Utv ;
objident last t1 ;
;macro L_support+"EdStrLong.mac" t1 20 0 ; //Размещаем строку в заданную ширину за счет изменения высоты символа

;macro L_support+"EdTextAttrib.mac"  "ListSP_N!=0" ListBl ;
objident last t1 ;
attrobj attach "ListSP_N" done t1 -99  ;

=SysArr(81,data) ; Curdata=FileNameSP+"  "+str(data[1])+"-"+str(data[2])+"-"+str(data[3])+"  "+str(data[5])+":"+str(data[6])+":"+str(data[7])
;macro L_support+"EdTextAttrib.mac"  "CurDataSp!=0" Curdata ;
objident last t1 ;
;macro L_support+"EdStrLong.mac" t1 110 0 ; //Размещаем строку в заданную ширину за счет изменения высоты символа

//==================================================================================================================
//очистка пустых
If (!IsAttrdef("PozN")) { Attribute Create "PozN" "Чертежная позиция" Real 3 0 ; }
#l1 line 0 0 0 0 0 1 ;
attrobj attach "PozN" done l1 0  ;
         ;selbyattr "PozN!=-1" all done;

delete previous         done ;
#l1 line 0 0 0 0 0 1 ;
attrobj attach "OboznCH" done l1 0  ;
;selbyattr "OboznCH!=-1" all done;
delete previous      done ;
#l1 line 0 0 0 0 0 1 ;
attrobj attach "NaimenCH" done l1 0  ;
;selbyattr "NaimenCH!=-1" all done;
delete previous     done ;
#l1 line 0 0 0 0 0 1 ;
attrobj attach "KolCH" done l1 0  ;
;selbyattr "KolCH!=-1" all done;
delete previous        done ;
#l1 line 0 0 0 0 0 1 ;
attrobj attach "Format" done l1 0  ;
;selbyattr "Format!=-1" all done;
delete previous       done ;
#l1 line 0 0 0 0 0 1 ;
attrobj attach "PrimCH" done l1 0  ;
;selbyattr "PrimCH!=-1" all done;
delete previous       done ;
goto metend1 ;
//====
//====


metend1:
DbClose rcdTMP ;
lwidth 0;
zoom all ;
//redraw,, ;
dimgcoef 1;
//Удаление всех точек
gosub delTxPn ;

            if GetCount(FileNameSP)!=0 { save  auto FileNameSP [ overwrite ] ; }
            else { save auto  FileNameSP  ; }

metend2:
Open FileName ;
invisible all done ;
append NAmetemp 0 0 0 ;
//dimgcoef 3;
fname=NAmetemp;
gosub sDelBackF;
fname=NAmetemp-3+".~k3";
gosub sDelBackF;
fname=FileNameSP-3+".~k3";
gosub sDelBackF;
exit;



//==========
//Сортировка массивов по возрастанию
SortPOZ:
a=0
//#ok_flag alternative "Внимание!!" msgbox text "NextPoint="+str(NextPoint)+", "+str(XmObjY[1])+", "+str(XmObjY[2])+", "+str(XmObjY[3])+", "+str(XmObjY[4])+", "+str(XmObjY[5])+", "+str(XmObjY[6])+", "  done "Ок" done
SorI=0 ;
SorJ=0 ;
lab_sor4:
 SorI=0 ;
 SorJ=Sorj+1
  lab_sor3:
   SorI=SorI+1
//   a=a+1
if sori==n { return ; }
   if POZI[SorI]>POZI[SorI+1] {
      maxX=POZI[SorI]
      POZI[SorI]=POZI[SorI+1]
      POZI[SorI+1]=maxX
     //=POZI[SORI]
       }
  if (SorI<i1-1) { goto lab_sor3 ; }
if (SorJ<i1-1) { goto lab_sor4 ; }
//#ok_flag alternative "Внимание!!" msgbox text "FINAL VALUE"+"a="+str(a)+"XmObjY="+str(XmObjY[1])+", "+str(XmObjY[2])+", "+str(XmObjY[3])+", "+str(XmObjY[4])+", "+str(XmObjY[5])+", "+str(XmObjY[6])+", "  done "Ок" done
return;


delTxPn:
If (!IsAttrdef("PozN")) { Attribute Create "PozN" "Чертежная позиция" Real 3 0 ; }
#l1 line 0 0 0 0 0 1 ;
attrobj attach "PozN" done l1 0  ;
object_s=sYsvar(60) ;
i=0
met999:
if i<object_s {
   i=i+1
  //=i
   pnt=getobjnum(i);
   if getobjtype(pnt)==0 {
      if gettextstr(pnt)=="." {
         attrobj attach "PozN" done pnt 1 ;
         //delete pnt done ;
         //i=0
         //object_s=sYsvar(60) ;
         }
   }
   goto met999;
}
;selbyattr "PozN!=-1" all done;
delete previous  done ;
return;



 
//======================================================================
SpecString:
         if k==maxk {
            GoSub  AddDopBlank ;
            }
           ;
           k=k+1 ;
         //select attribute "PozN==k" done;
         ;selbyattr "PozN=="+str(k) all done;
         SPpozn=getselnum(1);
         attrobj attach "PozN" done SPpozn -1  ;
         //select attribute "OboznCH==k" done;
         ;selbyattr "OboznCH=="+str(k) all done;
         SPobozn=getselnum(1);
         attrobj attach "OboznCH" done SPobozn -1  ;
         //select attribute "NAIMENCH==k" done;
         ;selbyattr "NAIMENCH=="+str(k) all done;
         SPName=getselnum(1);
         attrobj attach "NAIMENCH" done SPName -1  ;
         //select attribute "kolCH==k" done;
         ;selbyattr "kolCH=="+str(k) all done;
         SPkol=getselnum(1);
         attrobj attach "kolCH" done SPkol -1  ;
         //select attribute "Format==k" done;
         ;selbyattr "Format=="+str(k) all done;
         SPFormat=getselnum(1);
         attrobj attach "Format" done SPFormat -1  ;
         //select attribute "PrimCh==k" done;
         ;selbyattr "PrimCh=="+str(k) all done;
         SPPrim=getselnum(1);
         attrobj attach "PrimCh" done SPPrim -1  ;

return ;



//Заполняем поля с именами
SetPlName:

return;

//===================
LongName:
         LtypeS=Ltype+1
         If LtypeS!=8 { nametyp=GetLongsAI(LtypeS,1) ; }
         else { nametyp="Балюстрада" }
         SB_Nam1=nameTyp+" заказ "+Pl_obozn
return;

SBDoc:

return ;
AddDopBlank:
            k=0 ;
            maxk=32 ;
            append ffPath+"\\_SP12ф.K3" -210*ListBl 0 0 ;
            ListBl=ListBl+1 ;
            ;macro L_support+"EdTextAttrib.mac"  "ListSP_i!=0" ListBl ;
            ;macro L_support+"EdTextAttrib.mac"  "ListSP_N!=0" ListBl ;
             objident last t1 ;
            attrobj attach "ListSP_N" done t1 -99  ;
            zoom all ;
            //redraw,, ;
return;
//-----------------------
//--удаляет файл с именем fName
sDelBackF:
if FileExist(fName)
{
	Nullout=RemoveFile(fName)
}
return;
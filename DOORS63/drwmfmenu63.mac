//-- Программа создает диалоговую карточку с выбором схемы расположения дверей
//-- Входные параметры:
//-- nd - количество створок
//-- Выходные данные:
//-- g_DSet - тип расположения дверей
//-- g_DSet - текстовая строка, кодирующая расположение дверей.
//-- Каждая створка обозначается знаком "+" или "-" ("ближе" или "дальше" она стоит)
//-- Если все створки стоят в один ряд ставится "+"
//-- Затем следует знак "0" или "1" (сдвинута или нет створка на велинину нахлеста)
//-- Например, шахматное расположение четырех створок может быть представлено
//-- строкой "+0-1+1-1" или "-0+1-1+1" (в зависимости от того, "ближе" или "дальше"
//-- расположена первая створка.
//-- Расположение четырех дверей "две крайние сзади, две центральные спереди"
//-- "-0+1+0-1" и т.д.
//-- Если g_DSet="[число]" запрашивать количество перехлестов

global ProtoPath, g_DSet,g_NBTrack,g_moveType;
getpar nd g_key_prev;
g_DSet=str(nd-1);
g_NBTrack=2;
PicturesPath=mpathexpand("<Pictures>\\");   //-- Папка с картинками
if (g_movetype==2)&&(nd==1) //Если распашная одна
{
  g_DSet=1;
  g_NBTrack=1;
  //Определить тип открывания Левая или Правая
  goto nahend;
}
if (g_movetype==3) //Если складная
{
  g_DSet="+1+1+1+1";
  g_NBTrack=1;
  //Определить тип открывания Левая или Правая
  goto nahend;
}

if (nd>=2&&nd<=5)
{
  defarr ddk[13],ddt[13],NbrAr[13]; //-- Массив с именами файлов и
  NbrAr[1]=2; ddk[1]="-0+1"; ddt[1]="Две с нахлестом";
  NbrAr[2]=2; ddk[2]="+0-1"; ddt[2]="Две с нахлестом";
  NbrAr[3]=1;   ddk[3]="+0+0"; ddt[3]="Две встык";
  NbrAr[4]=2; ddk[4]="-0+1-1"; ddt[4]="Три с нахлестом";
  NbrAr[5]=2; ddk[5]="+0-1+1"; ddt[5]="Три с нахлестом";
  NbrAr[6]=2; ddk[6]="-0+1-1+1"; ddt[6]="Четыре с нахлестом";
  NbrAr[7]=2; ddk[7]="+0-1+1-1"; ddt[7]="Четыре с нахлестом";
  NbrAr[8]=2; ddk[8]="+0-1-0+1"; ddt[8]="Четыре встык сзади";
  NbrAr[9]=2; ddk[9]="-0+1+0-1"; ddt[9]="Четыре встык спереди";
  NbrAr[10]=2; ddk[10]="-0+1-1+1-1"; ddt[10]="Пять с нахлестом";
  NbrAr[11]=2; ddk[11]="+0-1+1-1+1"; ddt[11]="Пять с нахлестом";
  NbrAr[12]=2; ddk[12]="-0+1+0+0-1"; ddt[12]="Пять встык спереди";
  NbrAr[13]=2; ddk[13]="+0-1-0-0+1"; ddt[13]="Пять встык сзади";
  gosub getplace;
}
else
{
  next:
  #ok_flag
  setvar
  "Расположение дверных створок"
  ""
  left
  "Укажите количество мест перекрытия"
  "Количество створок: "+str(nd)
  done
  real auto default nd-1 "Количество мест перекрытия:"	NJoint
  done;
  if (ok_flag==0)
  {
    cancel;
  }
  NJoint=int(NJoint);
  if (NJoint<0)
  {
    #ok_flag
       alternative "Ошибка"
       msgbox text "Количество мест перекрытия должно быть положительным"
       done
       "  OK  "
       done;
    goto next;
  }
  // Ключ построения непрофильной створки
  nullout=getvarinst(1,"KeyFas",KeyFas,0);
  if nd==1&&KeyFas { g_NBTrack=1; } // Для одной створки-фасада один трек
  g_DSet=str(NJoint);
}
nahend:

if (g_movetype==5) //Если раздвижная подвесная
{
	g_NBTrack=1;
}

nullout=setvarinst(1,"g_NBTrack",g_NBTrack);
nullout=setvarinst(1,"g_DSet",g_DSet);
//putmsg(g_DSet)
exit;
//==============================================================================
//-- Подпрограмма заполнения массива картинками и надписями дверных створок
//-- Входные параметры:
//-- nd - количество створок
//-- Выходные параметры:
//-- na - количество возможных "картинок"
//-- altfl[na] - массив имен файлов с картинками
//-- altxt[na] - массив кодов расположения
getplace:
//-- nb - начало в массиве ddk коджов расположения
if (nd==2) {na=3; nb=1;}
if (nd==3) {na=2; nb=4;}
if (nd==4) {na=4; nb=6;}
if (nd==5) {na=4; nb=10;}

i=1;
defarr altfl[na],altxt[na];
loopi:
altfl[i]=PicturesPath+"//DRwmf//"+str(nd)+str(i)+".wmf"
if (i<na)
{
  i=i+1;
  goto loopi;
}
if g_movetype==1||g_movetype==5||g_movetype==6
{
 #altnum
 alternative
 "Расположение дверных створок"
 picbox
 text
 center
 "Выберите расположение дверных створок"
 done
 default 1
 let j=1
 LoopJ:
 ddt[nb+j-1]  altfl[j]
 if (j<na) { Let j=j+1 goto LoopJ }
 done
 if (altnum!=0)
 {
  g_DSet=ddk[nb+altnum-1];
  g_NBTrack=NbrAr[nb+altnum-1]
  g_key_prev=1
  }
 else
 {
  g_key_prev=0
  goto nahend;
 }
}
if g_movetype==2
{
  g_DSet=ddk[2];
  g_NBTrack=1;
}

return;
//------------------------------------------------------------------------------

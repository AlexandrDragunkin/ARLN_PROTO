//-- Добавить вырез на панель с параметрами положения
//-- Входные параметры:
//-- Panel - объект "Панель"
//--         или 0 - если панель не надо инициализировать
//-- Pat - объект "Контур выреза"
//-- CutType - тип выреза:
//--           0 - линия маркировки
//--           1 - вырез
//--           8 - нарост
//--           2 - симметрия относительно оси X
//--           4 - симметрия относительно оси Y)
//-- Dept - глубина выреза:
//--           0 - если вырез сквозной;
//--           > 0 - не сквозной со стороны пласти A
//--           < 0 - не сквозной со стороны пласти F
//-- Map - секция раскрашивания
//-- Параметры положения
//-- ShiftX - сдвиг вдольоси Х
//-- ShiftY - сдвиг вдольоси Y
//-- Ang - угол поворота

global ProtoPath;
global g_OutPar;
defarr arr[24];

getpar Panel, Pat, CutType, Dept, Map, ShiftX, ShiftY, Ang;

//-- Зачастую приходится добавлять по многу вырезов в панель. Если при этом
//-- перестраивать панель после каждого выреза, то получится очень долго
//-- Поэтому инициализируем панель в вызывающем макросе и там же ее перестраиваем.
//-- А вырезы добавляем здесь.

//-- Если панель задана, то ее инициализируем.
if (isvardef("Panel")==16)
{
  arr[1]=Panel;
  err=GetPan6Par(1,arr);
}

#PPP pline path Pat ;
//-- Параметры формы выреза
InitArray(arr,0);
arr[1]=CutType; //-- Тип выреза
arr[2]=1;       //-- Форма выреза
arr[3]=PPP;     //-- Полилиния
SetPan6Par(7,arr);
//-- Параметры положения выреза
InitArray(arr,0);
arr[1]=9;		    //-- Привязка выреза к одному из торцев (1,3,5,7) или свободная привязка (9)
arr[2]=ShiftX;	//-- Если привязка к торцу, то сдвиг вдоль торца, иначе - абсолютная координата X в ЛСК панели
arr[3]=ShiftY;  //-- Если привязка к торцу, то сдвиг вглубь панели, иначе - абсолютная координата Y в ЛСК панели
arr[4]=Ang;  	  //-- Угол поворота выреза в градусах
arr[5]=Dept;  	//-- Глубина выреза: 0, если вырез сквозной; > 0 - не сквозной со стороны пласти A; < 0 - не сквозной со стороны пласти F
SetPan6Par(8,arr);
arr[1]=Map;     //-- Секция раскрашивания
g_OutPar=SetPan6Par(9,arr);  //-- Добавить вырез, параметры которого определены кодами 7 и 8
//-- Делаем панель, если она задана
if (isvardef("Panel")==16)
{
  MBPanel execute  Panel ;
}
delete PPP;
exit;
//-- Макропрограмма устанавливает эллиптическую столешницу

defarr basep[3];

NULLOUT=getvarinst(2,"ProtoPath",ProtoPath,getprotomac("Shkaf"));      //-- Путь к папке с макропрограммами
NULLOUT=getvarinst(2,"Typ_Kro",Typ_Kro,0);          //-- Тип лицевой кромки
NULLOUT=getvarinst(2,"FDMater",PrMater,0);          //-- Умолчание для материалов фигурных столешниц

NULLOUT=setvarinst(2,"MacroSw",1);                //-- Текущий способ построения

Thick=priceinfo(PrMater,"Thickness",16);
onerror final;
NULLOUT=getsnap();
NULLOUT=pushinst(1);
defarr arrin[3], arrout[8];
arrin[1]=Thick;  // Высота резиновой панели
mbget "Укажите положение панели" shell arrin arrout :
basep[1]=arrout[1];
basep[2]=arrout[2];
basep[3]=arrout[3];
xgab=arrout[4];
ygab=arrout[5];
Numb=arrout[6];
Shift=arrout[7];
//mbget "Укажите точку установки эллиптической столешницы" shell
//  basep[1] basep[2] basep[3] xgab ygab Numb Shift :

macro ProtoPath+"SetMat.mac" PrMater ;
macro ProtoPath+"SetBand.mac" Typ_Kro Typ_Kro Typ_Kro Typ_Kro ;
macro ProtoPath+"SetTextureAngle.mac" 0;
macro ProtoPath+"SetEnam.mac" "Эллиптическая столешница";
macromode undo off;
macro ProtoPath+"MakeEllipse.mac" basep[1]+xgab/2 basep[2]+ygab/2 basep[3] xgab, ygab;

objident last 1 polka;
if (Numb>1)
{
  move multicopy Numb-1 polka done 0 0 shift;
}
final:
offerror;
if (isvardef("polka")==16)
{
  macromode undo on "Ellipse_stol.mac" polka ;
}
else
{
  macromode undo on;
}
NULLOUT=resnap();
NULLOUT=popinst(1);
exit;

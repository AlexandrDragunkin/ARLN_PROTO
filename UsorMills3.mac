//-- Макропрограмма формирования фрезеровки на фасаде
//-- Входные параметры:
//-- xn,yn,zn - координаты правого нижнего дальнего угла фасада
//-- dx,dz - размеры фасада по ширине и высоте
//-- PictPar - параметры рисунка
//-- Panel - Объект панель
//-- ArrMillID - Массив с ID фрезеровки
//-- Picture - Номер рисунка фасада
//-- Выходные парамтеры:
//-- g_OutPar - ID фрезеровки

global ProtoPath;
global g_OutPar;

getpar xn,yn,zn,dx,dz,
PictPar, Panel, ArrMillID, Picture;

defarr PicArr[8];  //-- Массив ссылок на контура направляющих вырезов
defarr arr[10];    //-- Вспомогательный массив

//---------------------------------------------------------------------------
ShMill=5; //-- Сдвиг фрезеровки внутрь панели в мм
//---------------------------------------------------------------------------
NULLOUT=getvarinst(1,"UsorMillsPar1",dep,1);
//-- Формируем контуры направляющих фрезеровки
macro ProtoPath+"GetPictPath.mac" xn,yn,zn,dx,dz, Picture PictPar ByRef PictNum PicArr;

if (PictNum==0) //-- Если число сформированных контуров равно нулю - выходим
{
  exit;
}
//-- Создаем фрезеровку по контурам
i=1;
sec=11; //-- Номер секции раскрашивателя
arr[1]=Panel;
err=GetPan6Par(1,arr);
Loop:
Pat=PicArr[i];
//-- Каждому номеру контура должен соответствовать ID фрезы по этому контуру
//-- Если так получилось, что размер массива под фрезы меньше количества контуров,
//-- то оставшиеся контура фрезеруются последней фрезой.
min_i=min(i,getdimarray(ArrMillID));
MillID=ArrMillID[min_i];
gosub Mill;
i=i+1;
sec=sec+1;
if (sec>12)
{
  sec=5;
}
if (i<=PictNum)
{
  goto Loop;
}
MBPanel execute Panel;
exit;
//========================================================================
//-- Создание фрезеровки по контуру
//-- Входные параметры:
//-- Pat - контур направляющей выреза
//-- MillID - ID фрезы
//-- ShMill - сдвиг фрезеровки в мм внутрь панели
Mill:
if (MillID>0)
{
  macro ProtoPath+"PutCutr.mac"
    0,      //-- Панель инициализирована выше
    Pat     //-- Объект контур выреза
    0       //-- Тип выреза: вырез(1), нарост(8), (0) маркировка
    dep       //-- Глубина выреза: 0, если вырез сквозной; > 0 - не сквозной со стороны пласти A; < 0 - не сквозной со стороны пласти F
    sec;    //-- Секция раскрашивателя

  if (g_OutPar>0)
  {
    macro ProtoPath+"PutMill.mac"
    0,            //-- Панель инициализирована выше
    g_OutPar      //-- Полилиния выреза (контур)
    -1
    MillID,       //-- ID профиля в базе
    0,            //-- Сдвиг в долях толщины
    ShMill,       //-- Сдвиг в мм
    1
    sec           //-- Секция раскрашивателя
    "FigureMills";//-- Имя фрезеровки
    delete Pat done;
  }
}
return;
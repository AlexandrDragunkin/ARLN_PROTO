//-- Создание прямого дугообразного корпуса
global ProtoPath;
global MacroSw;       //-- Переключатель режимов работы 0 - в пар.макро 1 - разбор.
global PanMater;      //-- Материал панелей
KarkasNumb=getcarcnumb();  //-- Порядковый номер каркаса в сцене
global g_FurnType;    //-- Тип мебельного объекта

global HoldName;      //-- Имя хозяина

getpar
xpos, ypos,zpos,      //-- Координаты точки установки
w,                    //-- Ширина
d,                    //-- Глубина
h,                    //-- Высота
h_c,                  //-- Высота цоколя
PrMater,        	    //-- Материал корпуса
MatBp,                //-- Материал задней стенки
Widsth,               //-- Ширина стяжки
TypeBp,               //-- Тип задней стенки
DepthBp,              //-- Глубина установки задней стенки
InCutBp,              //-- Врезка\Нахлест задней стенки
IsTop,                //-- Наличие крышки
IsBottom,             //-- Наличие сушилки
BandColor,            //-- Цвет кромки
BandType,             //-- Тип кромки
FixCorp,              //-- Тип крепежа корпуса
FixBp,      	        //-- Тип крепежа задней стенки
Dno,                  //-- Накладное дно
Dum,                  //-- Размер "уголка" задней стенки под мойку
Dst,                  //-- Ширина дополнительной планки
HasCok                //-- Наличие цоколя
ShiftCok,             //-- Сдвиг цоколя
LongPost,             //-- Опорные стойки
FasCok,               //-- Размер фасок под цоколь
FixPlan,              //-- Наличие крепежной планки
Sag;                  //-- Прогиб корпуса
//------------------------------------------------------------------------------
FurnType="110101";
Namescr="ProtoParams";
position=249;   //-- Номер прототипа прямого дугообразного корпуса
CutLeft=0;      //-- Подрезка дна и крышки слева
CutRight=0;     //-- Подрезка дна и крышки справа
CutTop=0;       //-- Подрезка боковых стоек сверху
CutBottom=0;    //-- Подрезка боковых стоек снизу
ConfDuct=0;     //-- Конфигурация выреза под короб по умолчанию.
defarr error[10];
NULLOUT=initarray(error,"");
h_dsp=priceinfo(PrMater,"Thickness",DbVar("HDSP",16)); //-- Толщина материала корпуса
h_dvp=priceinfo(MatBp,"Thickness",DbVar("HDVP",4));   //-- Толщина материала задней стенки
NULLOUT=pushinst(1);
NULLOUT=getvarinst(1,"HasDuct",HasDuct,0);
NULLOUT=getvarinst(1,"ShiftDuct",ShiftDuct,0);
NULLOUT=getvarinst(1,"WidthDuct",WidthDuct,100);
NULLOUT=getvarinst(1,"DepthDuct",DepthDuct,100);
ElemCName="Корпус прямой дугообразный"+iif(HasDuct!=0," с вырезом","");
if (TypeBp==1||TypeBp==-1||TypeBp==2)
{
  DepthBp=0;
}
if (TypeBp==3&&DepthBp<h_dsp&&FixPlan==1)
{
  error[1]="Невозможно установить крепежную планку.";
  error[2]="При данном типе и глубине установки задней стенки крепежная планка не ставится";
  error[3]=" ";
  error[4]="Убедитесь, что значение глубины установки задней стенки указано корректно."
  error[5]="Проверьте, тот ли тип задней стенки выбран."
  error[6]="Проверьте, необходима ли крепежная планка."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 6 error;
  cancel;
}
if (LongPost==1&&Dno==1)
{
  error[1]="Невозможно построить корпус с опорными стойками и накладным дном.";
  error[2]=" ";
  error[3]="Проверьте правильность этих двух параметров. Хотя бы один из них должен быть выключен."
  error[4]="Убедитесь, что выбран правильный тип корпуса"
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 4 error;
  cancel;
}
if (FasCok>h_c)
{
  error[1]="Слишком большой размер фасок под плинтус.";
  error[2]="Размер фасок под плинтус не должен превышать высоту цоколя, равную '"+str(h_c)+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение высоты цоколя указано корректно."
  error[5]="Проверьте, корректно ли заданы размеры фасок под плинтус."
  error[6]="Убедитесь, что выбран правильный тип корпуса"
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 6 error;
  cancel;
}
if (h_c>int(h/3))
{
  error[1]="Слишком большая высота цоколя.";
  error[2]="Значение высоты цоколя не должно превышать '"+str(int(h/3))+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение высоты цоколя указано корректно."
  error[5]="Проверьте, корректно ли задано значение высоты стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 5 error;
  cancel;
}
if (DepthBp>(d-Sag)/3)
{
  error[1]="Слишком большая глубина установки задней стенки.";
  error[2]="Значение глубины задней стенки не должно превышать '"+str(int((d-Sag)/3))+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение глубины установки задней стенки указано корректно."
  error[5]="Проверьте, корректно ли задано значение глубины стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 5 error;
  cancel;
}
if (Widsth>int((d-Sag)/3))
{
  error[1]="Слишком большая ширина планки.";
  error[2]="Значение ширины планки не должно превышать '"+str(int((d-Sag)/3))+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение ширины планки указано корректно."
  error[5]="Проверьте, корректно ли задано значение глубины стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 5 error;
  cancel;
}
if (Shiftcok>int((d-Sag)/3))
{
  error[1]="Слишком большая величина сдвига цоколя.";
  error[2]="Значение ширины сдвига цоколя не должно превышать '"+str(int((d-Sag)/3))+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение сдвига цоколя указано корректно."
  error[5]="Проверьте, корректно ли задано значение глубины стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 5 error;
  cancel;
}
if (abs(Sag)>w/2-2*h_dsp)
{
  error[1]="Слишком большая величина прогиба корпуса.";
  error[2]="Абсолютное значение величины прогиба корпуса не должно быть больше '"+str(w/2-2*h_dsp)+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение величины прогиба корпуса указано корректно."
  error[5]="Проверьте, корректно ли задано значение ширины стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 5 error;
  cancel;
}
if (HasDuct!=0&&(WidthDuct<=2*h_dsp||DepthDuct<=2*h_dsp))
{
  error[1]="Слишком маленький размер короба.";
  error[2]="Значение ширины и/или глубины короба не должно быть меньше '"+str(2*h_dsp)+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение ширины и/или глубины короба указаны корректно."
  error[5]="Убедитесь, в данном изделии вырез под короб необходим."
  error[6]="Проверьте, корректно ли задано значение глубины стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 6 error;
  NULLOUT=popinst(1);
  cancel;
}
if (HasDuct!=0&&(WidthDuct+ShiftDuct)>w)
{
  error[1]="Слишком большая ширина короба.";
  error[2]="Значение ширины плюс сдвига короба не должно быть больше '"+str(w)+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение ширины короба указано корректно."
  error[5]="Убедитесь, что сдвиг короба указан корректно."
  error[6]="Убедитесь, в данном изделии вырез под короб необходим."
  error[7]="Проверьте, корректно ли задано значение ширины стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 7 error;
  NULLOUT=popinst(1);
  cancel;
}
if (HasDuct!=0&&DepthDuct>d-2*h_dsp)
{
  error[1]="Слишком большая глубина короба.";
  error[2]="Значение глубины короба не должно быть больше '"+str(d-2*h_dsp)+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение глубины короба указано корректно."
  error[5]="Убедитесь, в данном изделии вырез под короб необходим."
  error[6]="Проверьте, корректно ли задано значение глубины стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 6 error;
  NULLOUT=popinst(1);
  cancel;
}
if (HasDuct!=0&&TypeBp==3&&DepthBp>DepthDuct-2*h_dsp)
{
  error[1]="Слишком большая глубина установки врезной задней стенки.";
  error[2]="Значение глубины взезной задней стенки не должно быть больше '"+str(DepthDuct-2*h_dsp)+"' мм.";
  error[3]=" ";
  error[4]="Убедитесь, что значение глубины врезной задней стенки указано корректно."
  error[5]="Убедитесь, что значение глубины выреза под короб указано корректно."
  error[6]="Убедитесь, в данном изделии вырез под короб необходим."
  error[7]="Убедитесь, в данном изделии необходима именно врезная задняя стенка."
  error[8]="Проверьте, корректно ли задано значение глубины стола или шкафа."
  macro Protopath+"ShowSmartError.mac" "Ошибка параметров корпуса" 8 error;
  NULLOUT=popinst(1);
  cancel;
}
//--------------------------------------------------------------------------
CorpsId=getprotoid("Shkaf","Корпус прямой дугообразный","ProtoMacro","CorpsP");  //-- ID прототипа корпуса
//------------------------------------------------------------------------------
NULLOUT=getsnap();
if MacroSw==0
{
  gosub SimpleCorps;
}
else
{
  protoobj create "Shkaf.ptl" CorpsID //-- 249-й прототип "Корпус прямой дугообразный"
   "w"          w                    //-- Ширина
   "d"          d                    //-- Глубина
   "h"          h                    //-- Высота
   "h_c"        h_c                  //-- Высота цоколя
   "PrMater"    PrMater        	     //-- Материал корпуса
   "MatBp"      MatBp                //-- Материал задней стенки
   "Widsth"     Widsth               //-- Ширина стяжки
   "TypeBp"     TypeBp               //-- Тип задней стенки
   "DepthBp"    DepthBp              //-- Глубина установки задней стенки
   "InCutBp"    InCutBp              //-- Врезка\Нахлест задней стенки
   "IsTop"      IsTop                //-- Наличие крышки
   "IsBottom"   IsBottom             //-- Наличие сушилки
   "Sag"        Sag                  //-- Прогиб корпуса
   "BandColor"  BandColor            //-- Цвет кромки
   "BandType"   BandType             //-- Тип кромки
   "FixCorp"    FixCorp              //-- Тип крепежа корпуса
   "FixBp"      FixBp      	         //-- Тип крепежа задней стенки
   "Dno"        Dno                  //-- Накладное дно
   "Dum"        Dum                  //-- Размер "уголка" задней стенки под мойку
   "Dst"        Dst                  //-- Ширина дополнительной планки
   "HasCok"     HasCok               //-- Наличие цоколя
   "ShiftCok"   ShiftCok             //-- Сдвиг цоколя
   "LongPost"   LongPost             //-- Опорные стойки
   "FasCok"     FasCok               //-- Размер фасок под цоколь
   "FixPlan"    FixPlan              //-- Наличие крепежной планки
   "HasDuct"    HasDuct              //-- Наличие короба
   "ShiftDuct"  ShiftDuct            //-- Сдвиг короба
   "WidthDuct"  WidthDuct            //-- Ширина короба
   "DepthDuct"  DepthDuct            //-- Глубина короба
  done
  xpos ypos zpos;
  MacroSw=1;
}
NULLOUT=resnap();
NULLOUT=popinst(1);
//-- Присваиваем объекту атрибут с порядковым номером каркаса
if (isassign("KarkasNumb",0))
{
  attrobj copy record "KarkasNumb" done last 1 done;
}
else
{
  attrobj attach "KarkasNumb" done last 1 KarkasNumb;
}
if (isassign("HoldName",0))
{
  attrobj copy record "HoldName" done last 1 done;
}
else
{
  attrobj attach "HoldName" done last 1 HoldName ;
}
attrobj attach "Posit" done last 1 position;
attrobj attach "ElemName" done last 1 ElemCName;
attrobj attach "FurnType" done last 1 FurnType;
attrobj attach "Assembly" done last 1 1;        //-- Признак сборочной единицы
attrobj attach "XUnit" "YUnit" "ZUnit" done last 1 w d h-h_c*(!HasCok);
exit;
//-----------------------------------------------------------------------------
SimpleCorps:
if (Dno==0) //-- Не опорное дно
{
  CutLeft=h_dsp;
  CutRight=h_dsp;
}
else  //-- Опорное дно
{
  CutTop=0;
  CutBottom=h_dsp;
}
if (LongPost==1)  //-- Опорные стойки
{
  CutBottom=-h_c;
}
else              //-- Если стойки не опорные, размер фасок равен нулю
{
  FasCok=0;
}
//-- Определяем конфигурацию выреза под короб
if (HasDuct!=0)
{
  if (ShiftDuct<=h_dsp&&WidthDuct>=(w-2*h_dsp))
  {
    ConfDuct=1;  //-- Вырез на всю ширину корпуса
  }
  if (ShiftDuct<=h_dsp&&WidthDuct<(w-2*h_dsp))
  {
    ConfDuct=2;  //-- Вырез примыкает справа
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)>=(w-2*h_dsp))
  {
    ConfDuct=3;  //-- Вырез примыкает слева
  }
  if (ShiftDuct>h_dsp&&(WidthDuct+ShiftDuct)<=(w-2*h_dsp))
  {
    ConfDuct=4;  //-- Вырез внутри корпуса
  }
}
if (ConfDuct==1)  //-- Если короб на всю ширину, то просто уменьшаем глубину корпуса
{
  d=d-DepthDuct;
  HasDuct=0;
}
//---------------------------------------------------------------------
object_0=sysvar(60);
macro ProtoPath+"SetConstrInit.mac" ;
macro ProtoPath+"SetMat.mac" PrMater ;
//-- Рисуем контур выреза
if (HasDuct!=0)
{
  #Pat rectangle 3points ShiftDuct,0,0 ShiftDuct,DepthDuct,0 ShiftDuct+WidthDuct,DepthDuct,0;
  //-- Рисуем обрамление выреза
  macro ProtoPath+"PutDuct.mac" ConfDuct, ShiftDuct, h_c+h_dsp, WidthDuct, DepthDuct, h-h_c-h_dsp-iif((TypeBp==-1||TypeBp==4)&&IsTop==0,0,h_dsp), PrMater, FixCorp, TypeBp, FixBp, DepthBp, InCutBp, MatBp;
}
//-- Построение боковых стоек
macro ProtoPath+"SetECod.mac" "1101" ;
// macro ProtoPath+"SetKCod.mac" "СТЕН" h-h_c d;
macro ProtoPath+"SetEnam.mac" "Стенка боковая правая" ;
macro ProtoPath+"SetCuts.mac" CutBottom CutTop iif(HasDuct!=0&&(ConfDuct==2),DepthDuct,0) 0;
macro ProtoPath+"SetFilet.mac" 1 FasCok FasCok 0 0 0 0 0 0 0 0 0 ;
macro ProtoPath+"SetKrom.mac" BandColor iif(Dno==0&&LongPost==0,BandType,0)
                              BandType 0 BandType ;
macro ProtoPath+"SetFix.mac"  iif(Dno==0,0,FixCorp) 0
                              iif((TypeBp==1||TypeBp==-1)&&ConfDuct!=2,FixBp,0) 0 ;
//-- Если задняя стенка врезная - добавляем пропилы
if (TypeBp==3)
{
  //-- Пропил в правой стойке
  macro ProtoPath+"SetProp.mac" "F" "D" DepthBp-h_dvp  h_dvp InCutBp ;
}
macro ProtoPath+"MakePan.mac" 0 0 h_c h-h_c d-Sag 11;
macro ProtoPath+"SetPropNull.mac" ;
macro ProtoPath+"SetECod.mac" "1102";
// macro ProtoPath+"SetKCod.mac" "СТЕН" h-h_c d-Sag;
macro ProtoPath+"SetEnam.mac" "Стенка боковая левая" ;
macro ProtoPath+"SetFix.mac"  iif(Dno==0,0,FixCorp) 0
                              iif((TypeBp==1||TypeBp==-1)&&ConfDuct!=3,FixBp,0) 0 ;
macro ProtoPath+"SetCuts.mac" CutBottom CutTop iif(HasDuct!=0&&(ConfDuct==3),DepthDuct,0) 0;
if (TypeBp==3)
{
  //-- Пропил в левой стойке
  macro ProtoPath+"SetProp.mac" "A" "D" DepthBp-h_dvp h_dvp InCutBp ;
}
macro ProtoPath+"MakePan.mac" w-h_dsp 0 h_c h-h_c d-Sag 11;
macro ProtoPath+"SetPropNull.mac" ;
macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 0 0 0 ;
if (IsBottom==0)
{
  //-- Построение дна
  macro ProtoPath+"SetECod.mac" "1103" ;
  // macro ProtoPath+"SetKCod.mac" "ДНО" w-2*h_dsp d;
  macro ProtoPath+"SetEnam.mac" "Дно" ;
  macro ProtoPath+"SetCuts.mac" CutRight CutLeft 0 0;
  macro ProtoPath+"SetFixover.mac" 1;
  macro ProtoPath+"SetFix.mac"      iif(Dno==0,FixCorp,0) iif(Dno==0,FixCorp,0)
                                    iif(TypeBp==1||TypeBp==-1,FixBp,0) 0 ;
  macro ProtoPath+"SetKrom.mac"     BandColor iif(Dno==0,0,BandType)
                                    iif(Dno==0,0,BandType) 0 BandType ;
  macro ProtoPath+"SetBend.mac" 0 0 0 Sag 0 0 0 0;
  if (TypeBp==3)
  {
    //-- Пропил в дне
    macro ProtoPath+"SetProp.mac" "A" "D" DepthBp-h_dvp  h_dvp InCutBp;
  }
  macro ProtoPath+"MakePan.mac" 0 0 h_c w d 12 ;
  macro ProtoPath+"SetBend.mac" 0 0 0 0 0 0 0 0;
  if (HasDuct!=0) //-- Если нужен вырез, добавляем его
  {
    objident last 1 Pan;
    macro ProtoPath+"PutCutr.mac" Pan, Pat, 1, 0, 5;
  }
  macro ProtoPath+"SetPropNull.mac" ;
  macro ProtoPath+"SetFixover.mac" 0;
}
macro ProtoPath+"SetCuts.mac" 0 0 0 0;
//-- Построение задней стенки
macro ProtoPath+"SetMat.mac" MatBp;
macro ProtoPath+"SetECod.mac" "1106";
if (h_dvp<h_dsp)
{
  macro ProtoPath+"SetECod.mac" "1206";
}
// macro ProtoPath+"SetKCod.mac" "ЗДНС" h-2*h_dsp-h_c w-2*h_dsp;
macro ProtoPath+"SetEnam.mac" "Задняя стенка" ;
macro ProtoPath+"SetKrom.mac" BandColor 0 0 0 0 ;
if (TypeBp==0)     //-- Утопленная задняя стенка
{
	macro ProtoPath+"SetFix.mac"  FixCorp 0 FixCorp FixCorp ;
  if (HasDuct==0)
  {
    macro ProtoPath+"MakePan.mac" h_dsp 0 h_c+h_dsp  h-2*h_dsp-h_c w-2*h_dsp 14 ;
  }
  else
  {
    if (ConfDuct==2)
    {
      macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct 0 h_c+h_dsp  h-2*h_dsp-h_c w-2*h_dsp-WidthDuct 14 ;
    }
    if (ConfDuct==3)
    {
      macro ProtoPath+"MakePan.mac" h_dsp 0 h_c+h_dsp h-2*h_dsp-h_c w-2*h_dsp-WidthDuct 14 ;
    }
    if (ConfDuct==4)
    {
      macro ProtoPath+"MakePan.mac" h_dsp 0 h_c+h_dsp h-2*h_dsp-h_c ShiftDuct-2*h_dsp 14 ;
      macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct+ShiftDuct 0 h_c+h_dsp  h-2*h_dsp-h_c w-2*h_dsp-WidthDuct-ShiftDuct 14 ;
    }
  }
  macro ProtoPath+"SetPropNull.mac";
}
if (TypeBp==1)     //-- Накладная задняя стенка
{
  macro ProtoPath+"SetCuts.mac" -InCutBp -InCutBp -InCutBp -InCutBp;
	macro ProtoPath+"SetFix.mac"  0 0 0 0 ;
  if (HasDuct==0)
  {
    macro ProtoPath+"MakePan.mac" h_dsp 0 h_c+h_dsp h-2*h_dsp-h_c w-2*h_dsp 13 ;
  }
  else
  {
    if (ConfDuct==2)
    {
      macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct 0 h_c+h_dsp h-2*h_dsp-h_c w-2*h_dsp-WidthDuct 13 ;
    }
    if (ConfDuct==3)
    {
      macro ProtoPath+"MakePan.mac" h_dsp 0 h_c+h_dsp h-2*h_dsp-h_c w-2*h_dsp-WidthDuct 13 ;
    }
    if (ConfDuct==4)
    {
      macro ProtoPath+"MakePan.mac" h_dsp 0 h_c+h_dsp  h-2*h_dsp-h_c ShiftDuct-2*h_dsp 13 ;
      macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct+ShiftDuct 0 h_c+h_dsp  h-2*h_dsp-h_c w-2*h_dsp-WidthDuct-ShiftDuct 13 ;
    }
  }
	//-- Устанавливаем навеску
	objident last 1 Stenka;
	NULLOUT=getvarinst(1,"Naves",Naves,0);
	if (Naves>0)
	{
		macro ProtoPath+"MakeNaves.mac" 
		Naves,								//-- Навеска
		w,                    //-- Ширина
		d,                    //-- Глубина
		h,                    //-- Высота
		0,										//-- Глубина установки задней стенки
		Stenka,								//-- Ссылка на стенку
		0,										//-- Ссылка на вторую стенку (если корпус угловой)
		h_dsp,								//-- Толщина материала корпуса
		h_dvp 								//-- Толщина материала стенки
		0;										//-- Тип корпуса (0-прямой, 1-угловой)
	}
  macro ProtoPath+"SetPropNull.mac";
  macro ProtoPath+"SetCuts.mac" 0 0 0 0;
}
if (TypeBp==-1)     //-- Стенка для мойки
{
  if (HasDuct==0)
  {
    macro ProtoPath+"SetFix.mac" 0 0 0 0;
    macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 1 dum dum 0 0 0 ;
    macro ProtoPath+"MakePan.mac" h_dsp-InCutBp 0 h_c+h_dsp-InCutBp dum dum 13 ;

    macro ProtoPath+"SetFilet.mac" 0 0 0 1 dum dum 0 0 0 0 0 0 ;
    macro ProtoPath+"MakePan.mac" w-h_dsp+InCutBp-dum 0 h_c+h_dsp-InCutBp dum dum 13 ;

    macro ProtoPath+"SetFilet.mac" 1 dum dum 0 0 0 0 0 0 0 0 0;
    macro ProtoPath+"MakePan.mac" w-h_dsp+InCutBp-dum 0 h-h_dsp+InCutBp-dum dum dum 13;

    macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 1 dum dum ;
    macro ProtoPath+"MakePan.mac" h_dsp-InCutBp 0 h-h_dsp+InCutBp-dum dum dum 13;

    //-- Дополнительная планка
    macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 0 0 0 ;
    macro ProtoPath+"SetECod.mac" "1109" ;
    // macro ProtoPath+"SetKCod.mac" "ЗДНЦ" h-2*h_dsp-h_c w-2*h_dsp;
    macro ProtoPath+"SetEnam.mac" "Задняя царга" ;
    macro ProtoPath+"SetKrom.mac" BandColor BandType BandType 0 0 ;
    macro ProtoPath+"SetMat.mac" PrMater ;
    macro ProtoPath+"SetFix.mac"  0 0 FixCorp FixCorp ;
    macro ProtoPath+"MakePan.mac" h_dsp 0 h-dst-iif(IsTop==1,h_dsp,0) dst w-2*h_dsp 14 ;
  }
  else
  {
    if (ConfDuct==2)
    {
      macro ProtoPath+"SetFix.mac" 0 0 0 0;
      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 1 dum dum 0 0 0 ;
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp+WidthDuct 0 h_c+h_dsp-InCutBp dum dum 13 ;

      macro ProtoPath+"SetFilet.mac" 0 0 0 1 dum dum 0 0 0 0 0 0 ;
      macro ProtoPath+"MakePan.mac" w-h_dsp+InCutBp-dum 0 h_c+h_dsp-InCutBp dum dum 13 ;

      macro ProtoPath+"SetFilet.mac" 1 dum dum 0 0 0 0 0 0 0 0 0;
      macro ProtoPath+"MakePan.mac" w-h_dsp+InCutBp-dum 0 h-h_dsp+InCutBp-dum dum dum 13;

      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 1 dum dum ;
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp+WidthDuct 0 h-h_dsp+InCutBp-dum dum dum 13;

      //-- Дополнительная планка
      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 0 0 0 ;
      macro ProtoPath+"SetECod.mac" "1109" ;
      // macro ProtoPath+"SetKCod.mac" "ЗДНЦ" h-2*h_dsp-h_c w-2*h_dsp;
      macro ProtoPath+"SetEnam.mac" "Задняя царга" ;
      macro ProtoPath+"SetKrom.mac" BandColor BandType BandType 0 0 ;
      macro ProtoPath+"SetMat.mac" PrMater ;
      macro ProtoPath+"SetFix.mac"  0 0 FixCorp FixCorp ;
      macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct 0 h-dst-iif(IsTop==1,h_dsp,0) dst w-2*h_dsp-WidthDuct 14 ;
    }
    if (ConfDuct==3)
    {
      macro ProtoPath+"SetFix.mac" 0 0 0 0;
      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 1 dum dum 0 0 0 ;
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp 0 h_c+h_dsp-InCutBp dum dum 13 ;

      macro ProtoPath+"SetFilet.mac" 0 0 0 1 dum dum 0 0 0 0 0 0 ;
      macro ProtoPath+"MakePan.mac" w-h_dsp+InCutBp-dum-WidthDuct 0 h_c+h_dsp-InCutBp dum dum 13 ;

      macro ProtoPath+"SetFilet.mac" 1 dum dum 0 0 0 0 0 0 0 0 0;
      macro ProtoPath+"MakePan.mac" w-h_dsp+InCutBp-dum-WidthDuct 0 h-h_dsp+InCutBp-dum dum dum 13;

      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 1 dum dum ;
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp 0 h-h_dsp+InCutBp-dum dum dum 13;

      //-- Дополнительная планка
      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 0 0 0 ;
      macro ProtoPath+"SetECod.mac" "1109" ;
      // macro ProtoPath+"SetKCod.mac" "ЗДНЦ" h-2*h_dsp-h_c w-2*h_dsp;
      macro ProtoPath+"SetEnam.mac" "Задняя царга" ;
      macro ProtoPath+"SetKrom.mac" BandColor BandType BandType 0 0 ;
      macro ProtoPath+"SetMat.mac" PrMater ;
      macro ProtoPath+"SetFix.mac"  0 0 FixCorp FixCorp ;
      macro ProtoPath+"MakePan.mac" h_dsp 0 h-dst-iif(IsTop==1,h_dsp,0) dst w-2*h_dsp-WidthDuct 14 ;
    }
    if (ConfDuct==4)
    {
      macro ProtoPath+"SetFix.mac" 0 0 0 0;
      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 1 dum dum 0 0 0 ;
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp 0 h_c+h_dsp-InCutBp dum dum 13 ;
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp+WidthDuct+ShiftDuct 0 h_c+h_dsp-InCutBp dum dum 13 ;

      macro ProtoPath+"SetFilet.mac" 0 0 0 1 dum dum 0 0 0 0 0 0 ;
      macro ProtoPath+"MakePan.mac" w-h_dsp+InCutBp-dum 0 h_c+h_dsp-InCutBp dum dum 13 ;
      macro ProtoPath+"MakePan.mac" ShiftDuct-h_dsp+InCutBp-dum 0 h_c+h_dsp-InCutBp dum dum 13 ;

      macro ProtoPath+"SetFilet.mac" 1 dum dum 0 0 0 0 0 0 0 0 0;
      macro ProtoPath+"MakePan.mac" w-h_dsp+InCutBp-dum 0 h-h_dsp+InCutBp-dum dum dum 13;
      macro ProtoPath+"MakePan.mac" ShiftDuct-h_dsp+InCutBp-dum 0 h-h_dsp+InCutBp-dum dum dum 13;

      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 1 dum dum ;
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp 0 h-h_dsp+InCutBp-dum dum dum 13;
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp+WidthDuct+ShiftDuct 0 h-h_dsp+InCutBp-dum dum dum 13;

      //-- Дополнительная планка
      macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 0 0 0 ;
      macro ProtoPath+"SetECod.mac" "1109" ;
      // macro ProtoPath+"SetKCod.mac" "ЗДНЦ" h-2*h_dsp-h_c w-2*h_dsp;
      macro ProtoPath+"SetEnam.mac" "Задняя царга" ;
      macro ProtoPath+"SetKrom.mac" BandColor BandType BandType 0 0 ;
      macro ProtoPath+"SetMat.mac" PrMater ;
      macro ProtoPath+"SetFix.mac"  0 0 FixCorp FixCorp ;
      macro ProtoPath+"MakePan.mac" h_dsp 0 h-dst-iif(IsTop==1,h_dsp,0) dst ShiftDuct-2*h_dsp 14 ;
      macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct+ShiftDuct 0 h-dst-iif(IsTop==1,h_dsp,0) dst w-2*h_dsp-WidthDuct-ShiftDuct 14 ;
    }
  }
}
if (TypeBp==3)      //-- Врезная задняя стенка
{
  macro ProtoPath+"SetFix.mac"  0 0 0 0 ;
  if (HasDuct==0)
  {
    macro ProtoPath+"MakePan.mac" h_dsp-InCutBp  DepthBp  h_c+h_dsp-InCutBp
                  h-h_c-2*(h_dsp-InCutBp)  w-2*(h_dsp-InCutBp) 13 ;
  }
  else
  {
    if (ConfDuct==2)
    {
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp+WidthDuct DepthBp  h_c+h_dsp-InCutBp
                      h-h_c-2*(h_dsp-InCutBp) w-2*(h_dsp-InCutBp)-WidthDuct 13 ;
    }
    if (ConfDuct==3)
    {
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp  DepthBp  h_c+h_dsp-InCutBp
                    h-h_c-2*(h_dsp-InCutBp) w-2*(h_dsp-InCutBp)-WidthDuct 13 ;
    }
    if (ConfDuct==4)
    {
      macro ProtoPath+"MakePan.mac" h_dsp-InCutBp  DepthBp  h_c+h_dsp-InCutBp
                  h-h_c-2*(h_dsp-InCutBp) ShiftDuct-2*(h_dsp-InCutBp) 13 ;
      macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct+ShiftDuct-InCutBp DepthBp h_c+h_dsp-InCutBp
                  h-h_c-2*(h_dsp-InCutBp) w-2*(h_dsp-InCutBp)-WidthDuct-ShiftDuct 13 ;
    }
  }
	//-- Устанавливаем навеску
	objident last 1 Stenka;
	NULLOUT=getvarinst(1,"Naves",Naves,0);
	if (Naves>0)
	{
		macro ProtoPath+"MakeNaves.mac" 
		Naves,								//-- Навеска
		w,                    //-- Ширина
		d,                    //-- Глубина
		h,                    //-- Высота
		DepthBp								//-- Глубина установки задней стенки
		Stenka,								//-- Ссылка на стенку
		0,										//-- Ссылка на вторую стенку (если корпус угловой)
		h_dsp,								//-- Толщина материала корпуса
		h_dvp 								//-- Толщина материала стенки
		0;										//-- Тип корпуса (0-прямой, 1-угловой)
	}
  //-- Крепежная планка
  if (FixPlan==1)
  {
    macro ProtoPath+"SetFilet.mac" 0 0 0 0 0 0 0 0 0 0 0 0 ;
    macro ProtoPath+"SetECod.mac" "1109" ;
    // macro ProtoPath+"SetKCod.mac" "ЗДНЦ" h-2*h_dsp-h_c w-2*h_dsp;
    macro ProtoPath+"SetEnam.mac" "Задняя царга" ;
    macro ProtoPath+"SetKrom.mac" BandColor BandType BandType 0 0 ;
    macro ProtoPath+"SetMat.mac" PrMater ;
    macro ProtoPath+"SetFix.mac"  0 0 FixCorp FixCorp ;
    if (HasDuct==0)
    {
      macro ProtoPath+"MakePan.mac" h_dsp 0 h-dst-h_dsp dst w-2*h_dsp 14 ;
    }
    else
    {
      if (ConfDuct==2)
      {
        macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct 0 h-dst-h_dsp dst w-2*h_dsp-WidthDuct 14 ;
      }
      if (ConfDuct==3)
      {
        macro ProtoPath+"MakePan.mac" h_dsp 0 h-dst-h_dsp dst w-2*h_dsp-WidthDuct 14 ;
      }
      if (ConfDuct==4)
      {
        macro ProtoPath+"MakePan.mac" h_dsp 0 h-dst-h_dsp dst ShiftDuct-2*h_dsp 14 ;
        macro ProtoPath+"MakePan.mac" h_dsp+WidthDuct+ShiftDuct 0 h-dst-h_dsp dst w-2*h_dsp-WidthDuct-ShiftDuct 14 ;
      }
    }
  }
}
macro ProtoPath+"SetMat.mac" PrMater;
macro ProtoPath+"SetFix.mac"  FixCorp FixCorp 0 0;
//-- Если задняя стенка накладная, крепим ее крепежом задней стенки
if (TypeBp==1)
{
  macro ProtoPath+"SetFix.mac" FixCorp FixCorp FixBp 0 ;
}
//-- Если нет крышки, рисуем планки
macro ProtoPath+"SetCuts.mac" h_dsp h_dsp 0 0;
if (IsTop==0)
{
  macro ProtoPath+"SetECod.mac" "1105" ;
  // macro ProtoPath+"SetKCod.mac" "ПЛНК" w-2*h_dsp Widsth;
  macro ProtoPath+"SetEnam.mac" "Планка" ;
  macro ProtoPath+"SetKrom.mac" BandColor  0 0 0 BandType ;
  //-- Если тип задней стенки не "под мойку", ставим заднюю планку
  if (TypeBp!=-1)
  {
    if (TypeBp==3)
    {
      //-- Пропил
      macro ProtoPath+"SetProp.mac" "F" "D" DepthBp-h_dvp  h_dvp InCutBp;
    }
    if (d>2*Widsth+DepthDuct+2*h_dsp) //-- Если глубина корпуса больше, чем глубина двух стяжек, рисуем заднюю стяжку
    {
      macro ProtoPath+"MakePan.mac" 0 0 h-h_dsp	w iif(HasDuct==0,Widsth,max(Widsth,DepthDuct+2*h_dsp)) 12 ;
    }
    if (HasDuct!=0) //-- Если нужен вырез, добавляем его
    {
      objident last 1 Pan;
      macro ProtoPath+"PutCutr.mac" Pan, Pat, 1, 0, 5;
  }
  macro ProtoPath+"SetPropNull.mac" ;
  }
  macro ProtoPath+"SetKrom.mac" BandColor 0 0 BandType BandType ;
  macro ProtoPath+"SetFix.mac"  FixCorp FixCorp 0 0 ;
  macro ProtoPath+"SetBend.mac" 0 0 0 Sag 0 0 0 0;
  macro ProtoPath+"MakePan.mac" 0 d-Widsth-Sag h-h_dsp w Widsth+Sag 12 ;
  macro ProtoPath+"SetBend.mac" 0 0 0 0 0 0 0 0;
}
else //-- Крышка есть
{
  macro ProtoPath+"SetECod.mac" "1106" ;
  // macro ProtoPath+"SetKCod.mac" "КРШК" w-2*h_dsp d;
  macro ProtoPath+"SetEnam.mac" "Крышка" ;
  macro ProtoPath+"SetKrom.mac" BandColor 0 0 0 BandType;
  if (TypeBp==3)
	{
		//-- Пропил
		macro ProtoPath+"SetProp.mac" "F" "D" DepthBp-h_dvp  h_dvp InCutBp;
	}
  macro ProtoPath+"SetBend.mac" 0 0 0 Sag 0 0 0 0;
  macro ProtoPath+"MakePan.mac" 0 0 h-h_dsp w d 12;
  macro ProtoPath+"SetBend.mac" 0 0 0 0 0 0 0 0;
  if (HasDuct!=0) //-- Если нужен вырез, добавляем его
  {
    objident last 1 Pan;
    macro ProtoPath+"PutCutr.mac" Pan, Pat, 1, 0, 5;
  }
  macro ProtoPath+"SetPropNull.mac";
}
//-- Наличие цоколя
if (HasCok!=0)
{
  macro ProtoPath+"SetCuts.mac" 0 0 iif(LongPost==1,h_dsp,0)
                                    iif(LongPost==1,h_dsp,0);
  macro ProtoPath+"SetFix.mac"  0 FixCorp
                                  iif(LongPost==1,FixCorp,0)
                                  iif(LongPost==1,FixCorp,0) ;
  macro ProtoPath+"SetECod.mac" "1107" ;
  // macro ProtoPath+"SetKCod.mac" "ЦОКЛ" w-2*h_dsp d;
  macro ProtoPath+"SetEnam.mac" "Цоколь" ;
  macro ProtoPath+"SetKrom.mac" BandColor 0 0 iif(LongPost==0,BandType,0)
                                iif(LongPost==0,BandType,0);
  macro ProtoPath+"MakePan.mac" 0 d-h_dsp-Shiftcok 0 h_c w 14 ;
}
macro ProtoPath+"SetCuts.mac" 0 0 0 0;
if (HasDuct!=0) //-- Если есть вырез, удаляем контур выреза. Он нам больше не нужен
{
  delete Pat done;
}
//-------------------------------------------------------------------
object_3=sysvar(60)-object_0;
move last object_3 done xpos,ypos,zpos nocopy;
#Corps group last object_3 done;
g_FurnType=FurnType;
ScrMod=InitScratch();
err=AddScratch(ScrMod,FurnType,"w",w);
err=AddScratch(ScrMod,FurnType,"d",d);
err=AddScratch(ScrMod,FurnType,"h",h);
err=AddScratch(ScrMod,FurnType,"h_c",h_c);
err=AddScratch(ScrMod,FurnType,"PrMater",PrMater);
err=AddScratch(ScrMod,FurnType,"MatBp",MatBp);
err=AddScratch(ScrMod,FurnType,"Widsth",Widsth);
err=AddScratch(ScrMod,FurnType,"TypeBp",TypeBp);
err=AddScratch(ScrMod,FurnType,"DepthBp",DepthBp);
err=AddScratch(ScrMod,FurnType,"InCutBp",InCutBp);
err=AddScratch(ScrMod,FurnType,"IsTop",IsTop);
err=AddScratch(ScrMod,FurnType,"IsBottom",IsBottom);
err=AddScratch(ScrMod,FurnType,"Sag",Sag);
err=AddScratch(ScrMod,FurnType,"BandColor",BandColor);
err=AddScratch(ScrMod,FurnType,"BandType",BandType);
err=AddScratch(ScrMod,FurnType,"FixCorp",FixCorp);
err=AddScratch(ScrMod,FurnType,"FixBp",FixBp);
err=AddScratch(ScrMod,FurnType,"Dno",Dno);
err=AddScratch(ScrMod,FurnType,"Dum",Dum);
err=AddScratch(ScrMod,FurnType,"Dst",Dst);
err=AddScratch(ScrMod,FurnType,"HasCok",HasCok);
err=AddScratch(ScrMod,FurnType,"ShiftCok",ShiftCok);
err=AddScratch(ScrMod,FurnType,"LongPost",LongPost);
err=AddScratch(ScrMod,FurnType,"FasCok",FasCok);
err=AddScratch(ScrMod,FurnType,"FixPlan",FixPlan);
err=AddScratch(ScrMod,FurnType,"HasDuct",HasDuct);
err=AddScratch(ScrMod,FurnType,"ShiftDuct",ShiftDuct);
err=AddScratch(ScrMod,FurnType,"WidthDuct",WidthDuct);
err=AddScratch(ScrMod,FurnType,"DepthDuct",DepthDuct);
NULLOUT=writescratch(ScrMod,Namescr,Corps);
NULLOUT=TermScratch(ScrMod);
NULLOUT=addattrpi(Corps,1,CorpsID);
return;

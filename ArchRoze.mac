//-- Программа построения электрической розетки
//-- Входные параметры:
//-- ne - число электрических розеток
//-- np - число телефонных розеток
//-- nc - число компьютерных розеток

getpar ne, np, nc;

sx=40;	//-- размер розетки по оси X
sy=5;		//-- размер розетки по оси Y
sz=40;	//-- размер розетки по оси Z
//------------------------------------------------------------------------
//-- Строим электирическое розетки
i=1;
dx=0;		//-- Положение первой розетки
electro:
if (i<=ne) 
{
	#b1 box dx 0 0 (dx+sx) sy sz;
	#c1 cylinder (dx+sx/2) sy/2 (sz/2)	(dx+sx/2) sy*2 (sz/2)	(0.4*sx);
	#box boolean sub b1 c1 done;
  i=i+1;
  dx=dx+sx;
  goto electro;
}
//------------------------------------------------------------------------
//-- Строим телефонные розетки
sx=30;
i=1;
phone:
if (i<=np) 
{
	#b1 box dx 0 0 (dx+sx) sy sz;
	#c1 box (dx+sx/2-4) (sy/2-4) (sz/2-4)	(dx+sx/2+4) (sy/2+4) (sz/2+4);
	#box boolean sub b1 c1 done;
	Attrobj Attach "$Material" "$TSmooth" "$TMix" "$TAngle" Done box 55 1 1 0;
  i=i+1;
  dx=dx+sx;
  goto phone;
}
//------------------------------------------------------------------------
//-- Строим компьютерные розетки
i=1;
comput:
if (i<=nc) 
{
	#b1 box dx 0 0 (dx+sx) sy sz;
	#c1 box (dx+sx/2-5) (sy/2-5) (sz/2-5)	(dx+sx/2+5) (sy/2+5) (sz/2+5);
	#b1 boolean sub b1 c1 done;
	#c1 box (dx+sx/2-3) (sy/2+2) (sz/2)	(dx+sx/2+3) (sy/2+12) (sz/2+8);
	#box boolean sub b1 c1 done;
	Attrobj Attach "$Material" "$TSmooth" "$TMix" "$TAngle" Done box 55 1 1 0;
  i=i+1;
  dx=dx+sx;
  goto comput;
}
//------------------------------------------------------------------------
exit;